<!DOCTYPE HTML>

<html><head><meta charset="utf-8"/><title>Data.Maybe.Base</title><link href="Agda.css" rel="stylesheet"/><script src="highlight-hover.js" type="text/javascript"></script></head><body><div class="navbar"><a class="active" href="README.html">Everything</a><a href="index.html" style="float:right">Home</a></div><div class="main"><pre class="Agda"><a class="Comment" id="1">------------------------------------------------------------------------</a>
<a class="Comment" id="74">-- The Agda standard library</a>
<a class="Comment" id="103">--</a>
<a class="Comment" id="106">-- The Maybe type and some operations</a>
<a class="Comment" id="144">------------------------------------------------------------------------</a>

<a class="Comment" id="218">-- The definitions in this file are reexported by Data.Maybe.</a>

<a class="Symbol" id="281">{-#</a> <a class="Keyword" id="285">OPTIONS</a> <a class="Pragma" id="293">--cubical-compatible</a> <a class="Pragma" id="314">--safe</a> <a class="Symbol" id="321">#-}</a>

<a class="Keyword" id="326">module</a> <a class="Module" href="Data.Maybe.Base.html" id="333">Data.Maybe.Base</a> <a class="Keyword" id="349">where</a>

<a class="Keyword" id="356">open</a> <a class="Keyword" id="361">import</a> <a class="Module" href="Level.html" id="368">Level</a> <a class="Keyword" id="374">using</a> <a class="Symbol" id="380">(</a><a class="Postulate" href="Agda.Primitive.html#742" id="381">Level</a><a class="Symbol" id="386">;</a> <a class="Record" href="Level.html#409" id="388">Lift</a><a class="Symbol" id="392">)</a>
<a class="Keyword" id="394">open</a> <a class="Keyword" id="399">import</a> <a class="Module" href="Data.Bool.Base.html" id="406">Data.Bool.Base</a> <a class="Keyword" id="421">using</a> <a class="Symbol" id="427">(</a><a class="Datatype" href="Agda.Builtin.Bool.html#173" id="428">Bool</a><a class="Symbol" id="432">;</a> <a class="InductiveConstructor" href="Agda.Builtin.Bool.html#198" id="434">true</a><a class="Symbol" id="438">;</a> <a class="InductiveConstructor" href="Agda.Builtin.Bool.html#192" id="440">false</a><a class="Symbol" id="445">;</a> <a class="Function" href="Data.Bool.Base.html#951" id="447">not</a><a class="Symbol" id="450">)</a>
<a class="Keyword" id="452">open</a> <a class="Keyword" id="457">import</a> <a class="Module" href="Data.Unit.Base.html" id="464">Data.Unit.Base</a> <a class="Keyword" id="479">using</a> <a class="Symbol" id="485">(</a><a class="Record" href="Agda.Builtin.Unit.html#175" id="486">⊤</a><a class="Symbol" id="487">)</a>
<a class="Keyword" id="489">open</a> <a class="Keyword" id="494">import</a> <a class="Module" href="Data.These.Base.html" id="501">Data.These.Base</a> <a class="Keyword" id="517">using</a> <a class="Symbol" id="523">(</a><a class="Datatype" href="Data.These.Base.html#573" id="524">These</a><a class="Symbol" id="529">;</a> <a class="InductiveConstructor" href="Data.These.Base.html#631" id="531">this</a><a class="Symbol" id="535">;</a> <a class="InductiveConstructor" href="Data.These.Base.html#659" id="537">that</a><a class="Symbol" id="541">;</a> <a class="InductiveConstructor" href="Data.These.Base.html#687" id="543">these</a><a class="Symbol" id="548">)</a>
<a class="Keyword" id="550">open</a> <a class="Keyword" id="555">import</a> <a class="Module" href="Data.Product.Base.html" id="562">Data.Product.Base</a> <a class="Symbol" id="580">as</a> <a class="Module" id="583">Prod</a> <a class="Keyword" id="588">using</a> <a class="Symbol" id="594">(</a><a class="Function Operator" href="Data.Product.Base.html#1618" id="595">_×_</a><a class="Symbol" id="598">;</a> <a class="InductiveConstructor Operator" href="Agda.Builtin.Sigma.html#235" id="600">_,_</a><a class="Symbol" id="603">)</a>
<a class="Keyword" id="605">open</a> <a class="Keyword" id="610">import</a> <a class="Module" href="Function.Base.html" id="617">Function.Base</a> <a class="Keyword" id="631">using</a> <a class="Symbol" id="637">(</a><a class="Function Operator" href="Function.Base.html#1134" id="638">_∘_</a><a class="Symbol" id="641">;</a> <a class="Function" href="Function.Base.html#704" id="643">id</a><a class="Symbol" id="645">;</a> <a class="Function" href="Function.Base.html#725" id="647">const</a><a class="Symbol" id="652">)</a>
<a class="Keyword" id="654">import</a> <a class="Module" href="Relation.Nullary.Decidable.Core.html" id="661">Relation.Nullary.Decidable.Core</a> <a class="Symbol" id="693">as</a> <a class="Module" id="696">Dec</a>

<a class="Keyword" id="701">private</a>
  <a class="Keyword" id="711">variable</a>
    <a class="Generalizable" href="Data.Maybe.Base.html#724" id="724">a</a> <a class="Generalizable" href="Data.Maybe.Base.html#726" id="726">b</a> <a class="Generalizable" href="Data.Maybe.Base.html#728" id="728">c</a> <a class="Symbol" id="730">:</a> <a class="Postulate" href="Agda.Primitive.html#742" id="732">Level</a>
    <a class="Generalizable" href="Data.Maybe.Base.html#742" id="742">A</a> <a class="Symbol" id="744">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="746">Set</a> <a class="Generalizable" href="Data.Maybe.Base.html#724" id="750">a</a>
    <a class="Generalizable" href="Data.Maybe.Base.html#756" id="756">B</a> <a class="Symbol" id="758">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="760">Set</a> <a class="Generalizable" href="Data.Maybe.Base.html#726" id="764">b</a>
    <a class="Generalizable" href="Data.Maybe.Base.html#770" id="770">C</a> <a class="Symbol" id="772">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="774">Set</a> <a class="Generalizable" href="Data.Maybe.Base.html#728" id="778">c</a>

<a class="Comment" id="781">------------------------------------------------------------------------</a>
<a class="Comment" id="854">-- Definition</a>

<a class="Keyword" id="869">open</a> <a class="Keyword" id="874">import</a> <a class="Module" href="Agda.Builtin.Maybe.html" id="881">Agda.Builtin.Maybe</a> <a class="Keyword" id="900">public</a>
  <a class="Keyword" id="909">using</a> <a class="Symbol" id="915">(</a><a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="916">Maybe</a><a class="Symbol" id="921">;</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="923">just</a><a class="Symbol" id="927">;</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="929">nothing</a><a class="Symbol" id="936">)</a>

<a class="Comment" id="939">------------------------------------------------------------------------</a>
<a class="Comment" id="1012">-- Some operations</a>

<a id="boolToMaybe"></a><a class="Function" href="Data.Maybe.Base.html#1032" id="1032">boolToMaybe</a> <a class="Symbol" id="1044">:</a> <a class="Datatype" href="Agda.Builtin.Bool.html#173" id="1046">Bool</a> <a class="Symbol" id="1051">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1053">Maybe</a> <a class="Record" href="Agda.Builtin.Unit.html#175" id="1059">⊤</a>
<a class="Function" href="Data.Maybe.Base.html#1032" id="1061">boolToMaybe</a> <a class="InductiveConstructor" href="Agda.Builtin.Bool.html#198" id="1073">true</a>  <a class="Symbol" id="1079">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="1081">just</a> <a class="Symbol" id="1086">_</a>
<a class="Function" href="Data.Maybe.Base.html#1032" id="1088">boolToMaybe</a> <a class="InductiveConstructor" href="Agda.Builtin.Bool.html#192" id="1100">false</a> <a class="Symbol" id="1106">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="1108">nothing</a>

<a id="is-just"></a><a class="Function" href="Data.Maybe.Base.html#1117" id="1117">is-just</a> <a class="Symbol" id="1125">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1127">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="1133">A</a> <a class="Symbol" id="1135">→</a> <a class="Datatype" href="Agda.Builtin.Bool.html#173" id="1137">Bool</a>
<a class="Function" href="Data.Maybe.Base.html#1117" id="1142">is-just</a> <a class="Symbol" id="1150">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="1151">just</a> <a class="Symbol" id="1156">_)</a> <a class="Symbol" id="1159">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Bool.html#198" id="1161">true</a>
<a class="Function" href="Data.Maybe.Base.html#1117" id="1166">is-just</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="1174">nothing</a>  <a class="Symbol" id="1183">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Bool.html#192" id="1185">false</a>

<a id="is-nothing"></a><a class="Function" href="Data.Maybe.Base.html#1192" id="1192">is-nothing</a> <a class="Symbol" id="1203">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1205">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="1211">A</a> <a class="Symbol" id="1213">→</a> <a class="Datatype" href="Agda.Builtin.Bool.html#173" id="1215">Bool</a>
<a class="Function" href="Data.Maybe.Base.html#1192" id="1220">is-nothing</a> <a class="Symbol" id="1231">=</a> <a class="Function" href="Data.Bool.Base.html#951" id="1233">not</a> <a class="Function Operator" href="Function.Base.html#1134" id="1237">∘</a> <a class="Function" href="Data.Maybe.Base.html#1117" id="1239">is-just</a>

<a class="Comment" id="1248">-- A dependent eliminator.</a>

<a id="maybe"></a><a class="Function" href="Data.Maybe.Base.html#1276" id="1276">maybe</a> <a class="Symbol" id="1282">:</a> <a class="Symbol" id="1284">∀</a> <a class="Symbol" id="1286">{</a><a class="Bound" href="Data.Maybe.Base.html#1287" id="1287">A</a> <a class="Symbol" id="1289">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="1291">Set</a> <a class="Generalizable" href="Data.Maybe.Base.html#724" id="1295">a</a><a class="Symbol" id="1296">}</a> <a class="Symbol" id="1298">{</a><a class="Bound" href="Data.Maybe.Base.html#1299" id="1299">B</a> <a class="Symbol" id="1301">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1303">Maybe</a> <a class="Bound" href="Data.Maybe.Base.html#1287" id="1309">A</a> <a class="Symbol" id="1311">→</a> <a class="Primitive" href="Agda.Primitive.html#388" id="1313">Set</a> <a class="Generalizable" href="Data.Maybe.Base.html#726" id="1317">b</a><a class="Symbol" id="1318">}</a> <a class="Symbol" id="1320">→</a>
        <a class="Symbol" id="1330">((</a><a class="Bound" href="Data.Maybe.Base.html#1332" id="1332">x</a> <a class="Symbol" id="1334">:</a> <a class="Bound" href="Data.Maybe.Base.html#1287" id="1336">A</a><a class="Symbol" id="1337">)</a> <a class="Symbol" id="1339">→</a> <a class="Bound" href="Data.Maybe.Base.html#1299" id="1341">B</a> <a class="Symbol" id="1343">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="1344">just</a> <a class="Bound" href="Data.Maybe.Base.html#1332" id="1349">x</a><a class="Symbol" id="1350">))</a> <a class="Symbol" id="1353">→</a> <a class="Bound" href="Data.Maybe.Base.html#1299" id="1355">B</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="1357">nothing</a> <a class="Symbol" id="1365">→</a> <a class="Symbol" id="1367">(</a><a class="Bound" href="Data.Maybe.Base.html#1368" id="1368">x</a> <a class="Symbol" id="1370">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1372">Maybe</a> <a class="Bound" href="Data.Maybe.Base.html#1287" id="1378">A</a><a class="Symbol" id="1379">)</a> <a class="Symbol" id="1381">→</a> <a class="Bound" href="Data.Maybe.Base.html#1299" id="1383">B</a> <a class="Bound" href="Data.Maybe.Base.html#1368" id="1385">x</a>
<a class="Function" href="Data.Maybe.Base.html#1276" id="1387">maybe</a> <a class="Bound" href="Data.Maybe.Base.html#1393" id="1393">j</a> <a class="Bound" href="Data.Maybe.Base.html#1395" id="1395">n</a> <a class="Symbol" id="1397">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="1398">just</a> <a class="Bound" href="Data.Maybe.Base.html#1403" id="1403">x</a><a class="Symbol" id="1404">)</a> <a class="Symbol" id="1406">=</a> <a class="Bound" href="Data.Maybe.Base.html#1393" id="1408">j</a> <a class="Bound" href="Data.Maybe.Base.html#1403" id="1410">x</a>
<a class="Function" href="Data.Maybe.Base.html#1276" id="1412">maybe</a> <a class="Bound" href="Data.Maybe.Base.html#1418" id="1418">j</a> <a class="Bound" href="Data.Maybe.Base.html#1420" id="1420">n</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="1422">nothing</a>  <a class="Symbol" id="1431">=</a> <a class="Bound" href="Data.Maybe.Base.html#1420" id="1433">n</a>

<a class="Comment" id="1436">-- A non-dependent eliminator.</a>

<a id="maybe′"></a><a class="Function" href="Data.Maybe.Base.html#1468" id="1468">maybe′</a> <a class="Symbol" id="1475">:</a> <a class="Symbol" id="1477">(</a><a class="Generalizable" href="Data.Maybe.Base.html#742" id="1478">A</a> <a class="Symbol" id="1480">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="1482">B</a><a class="Symbol" id="1483">)</a> <a class="Symbol" id="1485">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="1487">B</a> <a class="Symbol" id="1489">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1491">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="1497">A</a> <a class="Symbol" id="1499">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="1501">B</a>
<a class="Function" href="Data.Maybe.Base.html#1468" id="1503">maybe′</a> <a class="Symbol" id="1510">=</a> <a class="Function" href="Data.Maybe.Base.html#1276" id="1512">maybe</a>

<a class="Comment" id="1519">-- A defaulting mechanism</a>

<a id="fromMaybe"></a><a class="Function" href="Data.Maybe.Base.html#1546" id="1546">fromMaybe</a> <a class="Symbol" id="1556">:</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="1558">A</a> <a class="Symbol" id="1560">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1562">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="1568">A</a> <a class="Symbol" id="1570">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="1572">A</a>
<a class="Function" href="Data.Maybe.Base.html#1546" id="1574">fromMaybe</a> <a class="Symbol" id="1584">=</a> <a class="Function" href="Data.Maybe.Base.html#1468" id="1586">maybe′</a> <a class="Function" href="Function.Base.html#704" id="1593">id</a>

<a class="Comment" id="1597">-- A safe variant of "fromJust". If the value is nothing, then the</a>
<a class="Comment" id="1664">-- return type is the unit type.</a>

<a class="Keyword" id="1698">module</a> <a class="Module" href="Data.Maybe.Base.html#1705" id="1705">_</a> <a class="Symbol" id="1707">{</a><a class="Bound" href="Data.Maybe.Base.html#1708" id="1708">a</a><a class="Symbol" id="1709">}</a> <a class="Symbol" id="1711">{</a><a class="Bound" href="Data.Maybe.Base.html#1712" id="1712">A</a> <a class="Symbol" id="1714">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="1716">Set</a> <a class="Bound" href="Data.Maybe.Base.html#1708" id="1720">a</a><a class="Symbol" id="1721">}</a> <a class="Keyword" id="1723">where</a>

  <a class="Function" href="Data.Maybe.Base.html#1732" id="1732">From-just</a> <a class="Symbol" id="1742">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1744">Maybe</a> <a class="Bound" href="Data.Maybe.Base.html#1712" id="1750">A</a> <a class="Symbol" id="1752">→</a> <a class="Primitive" href="Agda.Primitive.html#388" id="1754">Set</a> <a class="Bound" href="Data.Maybe.Base.html#1708" id="1758">a</a>
  <a class="Function" href="Data.Maybe.Base.html#1732" id="1762">From-just</a> <a class="Symbol" id="1772">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="1773">just</a> <a class="Symbol" id="1778">_)</a> <a class="Symbol" id="1781">=</a> <a class="Bound" href="Data.Maybe.Base.html#1712" id="1783">A</a>
  <a class="Function" href="Data.Maybe.Base.html#1732" id="1787">From-just</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="1797">nothing</a>  <a class="Symbol" id="1806">=</a> <a class="Record" href="Level.html#409" id="1808">Lift</a> <a class="Bound" href="Data.Maybe.Base.html#1708" id="1813">a</a> <a class="Record" href="Agda.Builtin.Unit.html#175" id="1815">⊤</a>

  <a class="Function" href="Data.Maybe.Base.html#1820" id="1820">from-just</a> <a class="Symbol" id="1830">:</a> <a class="Symbol" id="1832">(</a><a class="Bound" href="Data.Maybe.Base.html#1833" id="1833">x</a> <a class="Symbol" id="1835">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1837">Maybe</a> <a class="Bound" href="Data.Maybe.Base.html#1712" id="1843">A</a><a class="Symbol" id="1844">)</a> <a class="Symbol" id="1846">→</a> <a class="Function" href="Data.Maybe.Base.html#1732" id="1848">From-just</a> <a class="Bound" href="Data.Maybe.Base.html#1833" id="1858">x</a>
  <a class="Function" href="Data.Maybe.Base.html#1820" id="1862">from-just</a> <a class="Symbol" id="1872">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="1873">just</a> <a class="Bound" href="Data.Maybe.Base.html#1878" id="1878">x</a><a class="Symbol" id="1879">)</a> <a class="Symbol" id="1881">=</a> <a class="Bound" href="Data.Maybe.Base.html#1878" id="1883">x</a>
  <a class="Function" href="Data.Maybe.Base.html#1820" id="1887">from-just</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="1897">nothing</a>  <a class="Symbol" id="1906">=</a> <a class="Symbol" id="1908">_</a>

<a class="Comment" id="1911">-- Functoriality: map</a>

<a id="map"></a><a class="Function" href="Data.Maybe.Base.html#1934" id="1934">map</a> <a class="Symbol" id="1938">:</a> <a class="Symbol" id="1940">(</a><a class="Generalizable" href="Data.Maybe.Base.html#742" id="1941">A</a> <a class="Symbol" id="1943">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="1945">B</a><a class="Symbol" id="1946">)</a> <a class="Symbol" id="1948">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1950">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="1956">A</a> <a class="Symbol" id="1958">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="1960">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="1966">B</a>
<a class="Function" href="Data.Maybe.Base.html#1934" id="1968">map</a> <a class="Bound" href="Data.Maybe.Base.html#1972" id="1972">f</a> <a class="Symbol" id="1974">=</a> <a class="Function" href="Data.Maybe.Base.html#1276" id="1976">maybe</a> <a class="Symbol" id="1982">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="1983">just</a> <a class="Function Operator" href="Function.Base.html#1134" id="1988">∘</a> <a class="Bound" href="Data.Maybe.Base.html#1972" id="1990">f</a><a class="Symbol" id="1991">)</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="1993">nothing</a>

<a class="Comment" id="2002">-- Applicative: ap</a>

<a id="ap"></a><a class="Function" href="Data.Maybe.Base.html#2022" id="2022">ap</a> <a class="Symbol" id="2025">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2027">Maybe</a> <a class="Symbol" id="2033">(</a><a class="Generalizable" href="Data.Maybe.Base.html#742" id="2034">A</a> <a class="Symbol" id="2036">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="2038">B</a><a class="Symbol" id="2039">)</a> <a class="Symbol" id="2041">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2043">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2049">A</a> <a class="Symbol" id="2051">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2053">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="2059">B</a>
<a class="Function" href="Data.Maybe.Base.html#2022" id="2061">ap</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2064">nothing</a>  <a class="Symbol" id="2073">=</a> <a class="Function" href="Function.Base.html#725" id="2075">const</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2081">nothing</a>
<a class="Function" href="Data.Maybe.Base.html#2022" id="2089">ap</a> <a class="Symbol" id="2092">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2093">just</a> <a class="Bound" href="Data.Maybe.Base.html#2098" id="2098">f</a><a class="Symbol" id="2099">)</a> <a class="Symbol" id="2101">=</a> <a class="Function" href="Data.Maybe.Base.html#1934" id="2103">map</a> <a class="Bound" href="Data.Maybe.Base.html#2098" id="2107">f</a>

<a class="Comment" id="2110">-- Monad: bind</a>

<a class="Keyword" id="2126">infixl</a> <a class="Number" id="2133">1</a> <a class="Function Operator" href="Data.Maybe.Base.html#2141" id="2135">_&gt;&gt;=_</a>
<a id="_&gt;&gt;=_"></a><a class="Function Operator" href="Data.Maybe.Base.html#2141" id="2141">_&gt;&gt;=_</a> <a class="Symbol" id="2147">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2149">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2155">A</a> <a class="Symbol" id="2157">→</a> <a class="Symbol" id="2159">(</a><a class="Generalizable" href="Data.Maybe.Base.html#742" id="2160">A</a> <a class="Symbol" id="2162">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2164">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="2170">B</a><a class="Symbol" id="2171">)</a> <a class="Symbol" id="2173">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2175">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="2181">B</a>
<a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2183">nothing</a> <a class="Function Operator" href="Data.Maybe.Base.html#2141" id="2191">&gt;&gt;=</a> <a class="Bound" href="Data.Maybe.Base.html#2195" id="2195">f</a> <a class="Symbol" id="2197">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2199">nothing</a>
<a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2207">just</a> <a class="Bound" href="Data.Maybe.Base.html#2212" id="2212">a</a>  <a class="Function Operator" href="Data.Maybe.Base.html#2141" id="2215">&gt;&gt;=</a> <a class="Bound" href="Data.Maybe.Base.html#2219" id="2219">f</a> <a class="Symbol" id="2221">=</a> <a class="Bound" href="Data.Maybe.Base.html#2219" id="2223">f</a> <a class="Bound" href="Data.Maybe.Base.html#2212" id="2225">a</a>

<a class="Comment" id="2228">-- Alternative: &lt;∣&gt;</a>

<a class="Keyword" id="2249">infixr</a> <a class="Number" id="2256">6</a> <a class="Function Operator" href="Data.Maybe.Base.html#2264" id="2258">_&lt;∣&gt;_</a>
<a id="_&lt;∣&gt;_"></a><a class="Function Operator" href="Data.Maybe.Base.html#2264" id="2264">_&lt;∣&gt;_</a> <a class="Symbol" id="2270">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2272">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2278">A</a> <a class="Symbol" id="2280">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2282">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2288">A</a> <a class="Symbol" id="2290">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2292">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2298">A</a>
<a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2300">just</a> <a class="Bound" href="Data.Maybe.Base.html#2305" id="2305">x</a>  <a class="Function Operator" href="Data.Maybe.Base.html#2264" id="2308">&lt;∣&gt;</a> <a class="Bound" href="Data.Maybe.Base.html#2312" id="2312">my</a> <a class="Symbol" id="2315">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2317">just</a> <a class="Bound" href="Data.Maybe.Base.html#2305" id="2322">x</a>
<a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2324">nothing</a> <a class="Function Operator" href="Data.Maybe.Base.html#2264" id="2332">&lt;∣&gt;</a> <a class="Bound" href="Data.Maybe.Base.html#2336" id="2336">my</a> <a class="Symbol" id="2339">=</a> <a class="Bound" href="Data.Maybe.Base.html#2336" id="2341">my</a>

<a class="Comment" id="2345">-- Just when the boolean is true</a>

<a id="when"></a><a class="Function" href="Data.Maybe.Base.html#2379" id="2379">when</a> <a class="Symbol" id="2384">:</a> <a class="Datatype" href="Agda.Builtin.Bool.html#173" id="2386">Bool</a> <a class="Symbol" id="2391">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2393">A</a> <a class="Symbol" id="2395">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2397">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2403">A</a>
<a class="Function" href="Data.Maybe.Base.html#2379" id="2405">when</a> <a class="Bound" href="Data.Maybe.Base.html#2410" id="2410">b</a> <a class="Bound" href="Data.Maybe.Base.html#2412" id="2412">c</a> <a class="Symbol" id="2414">=</a> <a class="Function" href="Data.Maybe.Base.html#1934" id="2416">map</a> <a class="Symbol" id="2420">(</a><a class="Function" href="Function.Base.html#725" id="2421">const</a> <a class="Bound" href="Data.Maybe.Base.html#2412" id="2427">c</a><a class="Symbol" id="2428">)</a> <a class="Symbol" id="2430">(</a><a class="Function" href="Data.Maybe.Base.html#1032" id="2431">boolToMaybe</a> <a class="Bound" href="Data.Maybe.Base.html#2410" id="2443">b</a><a class="Symbol" id="2444">)</a>

<a class="Comment" id="2447">------------------------------------------------------------------------</a>
<a class="Comment" id="2520">-- Aligning and zipping</a>

<a id="alignWith"></a><a class="Function" href="Data.Maybe.Base.html#2545" id="2545">alignWith</a> <a class="Symbol" id="2555">:</a> <a class="Symbol" id="2557">(</a><a class="Datatype" href="Data.These.Base.html#573" id="2558">These</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2564">A</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="2566">B</a> <a class="Symbol" id="2568">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#770" id="2570">C</a><a class="Symbol" id="2571">)</a> <a class="Symbol" id="2573">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2575">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2581">A</a> <a class="Symbol" id="2583">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2585">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="2591">B</a> <a class="Symbol" id="2593">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2595">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#770" id="2601">C</a>
<a class="Function" href="Data.Maybe.Base.html#2545" id="2603">alignWith</a> <a class="Bound" href="Data.Maybe.Base.html#2613" id="2613">f</a> <a class="Symbol" id="2615">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2616">just</a> <a class="Bound" href="Data.Maybe.Base.html#2621" id="2621">a</a><a class="Symbol" id="2622">)</a> <a class="Symbol" id="2624">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2625">just</a> <a class="Bound" href="Data.Maybe.Base.html#2630" id="2630">b</a><a class="Symbol" id="2631">)</a> <a class="Symbol" id="2633">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2635">just</a> <a class="Symbol" id="2640">(</a><a class="Bound" href="Data.Maybe.Base.html#2613" id="2641">f</a> <a class="Symbol" id="2643">(</a><a class="InductiveConstructor" href="Data.These.Base.html#687" id="2644">these</a> <a class="Bound" href="Data.Maybe.Base.html#2621" id="2650">a</a> <a class="Bound" href="Data.Maybe.Base.html#2630" id="2652">b</a><a class="Symbol" id="2653">))</a>
<a class="Function" href="Data.Maybe.Base.html#2545" id="2656">alignWith</a> <a class="Bound" href="Data.Maybe.Base.html#2666" id="2666">f</a> <a class="Symbol" id="2668">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2669">just</a> <a class="Bound" href="Data.Maybe.Base.html#2674" id="2674">a</a><a class="Symbol" id="2675">)</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2677">nothing</a>  <a class="Symbol" id="2686">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2688">just</a> <a class="Symbol" id="2693">(</a><a class="Bound" href="Data.Maybe.Base.html#2666" id="2694">f</a> <a class="Symbol" id="2696">(</a><a class="InductiveConstructor" href="Data.These.Base.html#631" id="2697">this</a> <a class="Bound" href="Data.Maybe.Base.html#2674" id="2702">a</a><a class="Symbol" id="2703">))</a>
<a class="Function" href="Data.Maybe.Base.html#2545" id="2706">alignWith</a> <a class="Bound" href="Data.Maybe.Base.html#2716" id="2716">f</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2718">nothing</a>  <a class="Symbol" id="2727">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2728">just</a> <a class="Bound" href="Data.Maybe.Base.html#2733" id="2733">b</a><a class="Symbol" id="2734">)</a> <a class="Symbol" id="2736">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2738">just</a> <a class="Symbol" id="2743">(</a><a class="Bound" href="Data.Maybe.Base.html#2716" id="2744">f</a> <a class="Symbol" id="2746">(</a><a class="InductiveConstructor" href="Data.These.Base.html#659" id="2747">that</a> <a class="Bound" href="Data.Maybe.Base.html#2733" id="2752">b</a><a class="Symbol" id="2753">))</a>
<a class="Function" href="Data.Maybe.Base.html#2545" id="2756">alignWith</a> <a class="Bound" href="Data.Maybe.Base.html#2766" id="2766">f</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2768">nothing</a>  <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2777">nothing</a>  <a class="Symbol" id="2786">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2788">nothing</a>

<a id="zipWith"></a><a class="Function" href="Data.Maybe.Base.html#2797" id="2797">zipWith</a> <a class="Symbol" id="2805">:</a> <a class="Symbol" id="2807">(</a><a class="Generalizable" href="Data.Maybe.Base.html#742" id="2808">A</a> <a class="Symbol" id="2810">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="2812">B</a> <a class="Symbol" id="2814">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#770" id="2816">C</a><a class="Symbol" id="2817">)</a> <a class="Symbol" id="2819">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2821">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2827">A</a> <a class="Symbol" id="2829">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2831">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="2837">B</a> <a class="Symbol" id="2839">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2841">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#770" id="2847">C</a>
<a class="Function" href="Data.Maybe.Base.html#2797" id="2849">zipWith</a> <a class="Bound" href="Data.Maybe.Base.html#2857" id="2857">f</a> <a class="Symbol" id="2859">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2860">just</a> <a class="Bound" href="Data.Maybe.Base.html#2865" id="2865">a</a><a class="Symbol" id="2866">)</a> <a class="Symbol" id="2868">(</a><a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2869">just</a> <a class="Bound" href="Data.Maybe.Base.html#2874" id="2874">b</a><a class="Symbol" id="2875">)</a> <a class="Symbol" id="2877">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#173" id="2879">just</a> <a class="Symbol" id="2884">(</a><a class="Bound" href="Data.Maybe.Base.html#2857" id="2885">f</a> <a class="Bound" href="Data.Maybe.Base.html#2865" id="2887">a</a> <a class="Bound" href="Data.Maybe.Base.html#2874" id="2889">b</a><a class="Symbol" id="2890">)</a>
<a class="CatchallClause Function" href="Data.Maybe.Base.html#2797" id="2892">zipWith</a><a class="CatchallClause" id="2899"> </a><a class="CatchallClause Symbol" id="2900">_</a><a class="CatchallClause" id="2901"> </a><a class="CatchallClause Symbol" id="2902">_</a><a class="CatchallClause" id="2903">        </a><a class="CatchallClause Symbol" id="2911">_</a>        <a class="Symbol" id="2920">=</a> <a class="InductiveConstructor" href="Agda.Builtin.Maybe.html#194" id="2922">nothing</a>

<a id="align"></a><a class="Function" href="Data.Maybe.Base.html#2931" id="2931">align</a> <a class="Symbol" id="2937">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2939">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2945">A</a> <a class="Symbol" id="2947">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2949">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="2955">B</a> <a class="Symbol" id="2957">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="2959">Maybe</a> <a class="Symbol" id="2965">(</a><a class="Datatype" href="Data.These.Base.html#573" id="2966">These</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="2972">A</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="2974">B</a><a class="Symbol" id="2975">)</a>
<a class="Function" href="Data.Maybe.Base.html#2931" id="2977">align</a> <a class="Symbol" id="2983">=</a> <a class="Function" href="Data.Maybe.Base.html#2545" id="2985">alignWith</a> <a class="Function" href="Function.Base.html#704" id="2995">id</a>

<a id="zip"></a><a class="Function" href="Data.Maybe.Base.html#2999" id="2999">zip</a> <a class="Symbol" id="3003">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="3005">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="3011">A</a> <a class="Symbol" id="3013">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="3015">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="3021">B</a> <a class="Symbol" id="3023">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="3025">Maybe</a> <a class="Symbol" id="3031">(</a><a class="Generalizable" href="Data.Maybe.Base.html#742" id="3032">A</a> <a class="Function Operator" href="Data.Product.Base.html#1618" id="3034">×</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="3036">B</a><a class="Symbol" id="3037">)</a>
<a class="Function" href="Data.Maybe.Base.html#2999" id="3039">zip</a> <a class="Symbol" id="3043">=</a> <a class="Function" href="Data.Maybe.Base.html#2797" id="3045">zipWith</a> <a class="InductiveConstructor Operator" href="Agda.Builtin.Sigma.html#235" id="3053">_,_</a>

<a class="Comment" id="3058">------------------------------------------------------------------------</a>
<a class="Comment" id="3131">-- Injections.</a>

<a id="thisM"></a><a class="Function" href="Data.Maybe.Base.html#3147" id="3147">thisM</a> <a class="Symbol" id="3153">:</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="3155">A</a> <a class="Symbol" id="3157">→</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="3159">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="3165">B</a> <a class="Symbol" id="3167">→</a> <a class="Datatype" href="Data.These.Base.html#573" id="3169">These</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="3175">A</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="3177">B</a>
<a class="Function" href="Data.Maybe.Base.html#3147" id="3179">thisM</a> <a class="Bound" href="Data.Maybe.Base.html#3185" id="3185">a</a> <a class="Symbol" id="3187">=</a> <a class="Function" href="Data.Maybe.Base.html#1468" id="3189">maybe′</a> <a class="Symbol" id="3196">(</a><a class="InductiveConstructor" href="Data.These.Base.html#687" id="3197">these</a> <a class="Bound" href="Data.Maybe.Base.html#3185" id="3203">a</a><a class="Symbol" id="3204">)</a> <a class="Symbol" id="3206">(</a><a class="InductiveConstructor" href="Data.These.Base.html#631" id="3207">this</a> <a class="Bound" href="Data.Maybe.Base.html#3185" id="3212">a</a><a class="Symbol" id="3213">)</a>

<a id="thatM"></a><a class="Function" href="Data.Maybe.Base.html#3216" id="3216">thatM</a> <a class="Symbol" id="3222">:</a> <a class="Datatype" href="Agda.Builtin.Maybe.html#135" id="3224">Maybe</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="3230">A</a> <a class="Symbol" id="3232">→</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="3234">B</a> <a class="Symbol" id="3236">→</a> <a class="Datatype" href="Data.These.Base.html#573" id="3238">These</a> <a class="Generalizable" href="Data.Maybe.Base.html#742" id="3244">A</a> <a class="Generalizable" href="Data.Maybe.Base.html#756" id="3246">B</a>
<a class="Function" href="Data.Maybe.Base.html#3216" id="3248">thatM</a> <a class="Symbol" id="3254">=</a> <a class="Function" href="Data.Maybe.Base.html#1468" id="3256">maybe′</a> <a class="InductiveConstructor" href="Data.These.Base.html#687" id="3263">these</a> <a class="InductiveConstructor" href="Data.These.Base.html#659" id="3269">that</a>

<a class="Comment" id="3275">------------------------------------------------------------------------</a>
<a class="Comment" id="3348">-- DEPRECATED NAMES</a>
<a class="Comment" id="3368">------------------------------------------------------------------------</a>
<a class="Comment" id="3441">-- Please use the new names as continuing support for the old names is</a>
<a class="Comment" id="3512">-- not guaranteed.</a>

<a class="Comment" id="3532">-- Version 2.1</a>
<a class="Comment" id="3547">-- decToMaybe</a>

<a class="Keyword" id="3562">open</a> <a class="Module" href="Relation.Nullary.Decidable.Core.html" id="3567">Dec</a> <a class="Keyword" id="3571">using</a> <a class="Symbol" id="3577">(</a><a class="Function" href="Relation.Nullary.Decidable.Core.html#7645" id="3578">decToMaybe</a><a class="Symbol" id="3588">)</a> <a class="Keyword" id="3590">public</a>
</pre></div></body></html>