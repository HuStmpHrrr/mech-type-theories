<!DOCTYPE HTML>

<html><head><meta charset="utf-8"/><title>Cumulative.Soundness.Equiv</title><link href="Agda.css" rel="stylesheet"/><script src="highlight-hover.js" type="text/javascript"></script></head><body><div class="navbar"><a class="active" href="README.html">Everything</a><a href="index.html" style="float:right">Home</a></div><div class="main"><pre class="Agda"><a class="Symbol" id="1">{-#</a> <a class="Keyword" id="5">OPTIONS</a> <a class="Pragma" id="13">--without-K</a> <a class="Pragma" id="25">--safe</a> <a class="Symbol" id="32">#-}</a>

<a class="Comment" id="37">-- Equivalence of the Sound formulation and the other two formulations</a>
<a class="Keyword" id="108">module</a> <a class="Module" href="Cumulative.Soundness.Equiv.html" id="115">Cumulative.Soundness.Equiv</a> <a class="Keyword" id="142">where</a>

<a class="Keyword" id="149">open</a> <a class="Keyword" id="154">import</a> <a class="Module" href="Lib.html" id="161">Lib</a>

<a class="Keyword" id="166">open</a> <a class="Keyword" id="171">import</a> <a class="Module" href="Cumulative.Statics.Full.html" id="178">Cumulative.Statics.Full</a> <a class="Symbol" id="202">as</a> <a class="Module" id="205">F</a>
<a class="Keyword" id="207">open</a> <a class="Keyword" id="212">import</a> <a class="Module" href="Cumulative.Statics.Concise.html" id="219">Cumulative.Statics.Concise</a> <a class="Symbol" id="246">as</a> <a class="Module" id="249">C</a>
<a class="Keyword" id="251">open</a> <a class="Keyword" id="256">import</a> <a class="Module" href="Cumulative.Statics.Equiv.html" id="263">Cumulative.Statics.Equiv</a>
<a class="Keyword" id="288">open</a> <a class="Keyword" id="293">import</a> <a class="Module" href="Cumulative.Statics.Presup.html" id="300">Cumulative.Statics.Presup</a>
<a class="Keyword" id="326">open</a> <a class="Keyword" id="331">import</a> <a class="Module" href="Cumulative.Statics.Misc.html" id="338">Cumulative.Statics.Misc</a>
<a class="Keyword" id="362">open</a> <a class="Keyword" id="367">import</a> <a class="Module" href="Cumulative.Statics.Properties.Pi.html" id="374">Cumulative.Statics.Properties.Pi</a>
<a class="Keyword" id="407">open</a> <a class="Keyword" id="412">import</a> <a class="Module" href="Cumulative.Soundness.Typing.html" id="419">Cumulative.Soundness.Typing</a> <a class="Symbol" id="447">as</a> <a class="Module" id="450">S</a>

<a class="Comment" id="453">-- The Sound and Full formulations are equivalent.</a>
<a class="Keyword" id="504">mutual</a>
  <a id="S⇒F-⊢"></a><a class="Function" href="Cumulative.Soundness.Equiv.html#513" id="513">S⇒F-⊢</a> <a class="Symbol" id="519">:</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#898" id="521">S.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="525">Γ</a> <a class="Symbol" id="527">→</a>
          <a class="Comment" id="539">-------</a>
          <a class="Datatype Operator" href="Cumulative.Statics.Full.html#763" id="557">F.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="561">Γ</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#513" id="565">S⇒F-⊢</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#923" id="571">⊢[]</a>        <a class="Symbol" id="582">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#788" id="584">⊢[]</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#513" id="590">S⇒F-⊢</a> <a class="Symbol" id="596">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#938" id="597">⊢∷</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#600" id="600">⊢Γ</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#603" id="603">⊢T</a><a class="Symbol" id="605">)</a> <a class="Symbol" id="607">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#803" id="609">⊢∷</a> <a class="Symbol" id="612">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#513" id="613">S⇒F-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#600" id="619">⊢Γ</a><a class="Symbol" id="621">)</a> <a class="Symbol" id="623">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="624">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#603" id="631">⊢T</a><a class="Symbol" id="633">)</a>


  <a id="S⇒F-tm"></a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="639">S⇒F-tm</a> <a class="Symbol" id="646">:</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="648">Γ</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#1044" id="650">S.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2139" id="654">t</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#1044" id="656">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2091" id="658">T</a> <a class="Symbol" id="660">→</a>
           <a class="Comment" id="673">-------------</a>
           <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="698">Γ</a> <a class="Datatype Operator" href="Cumulative.Statics.Full.html#1298" id="700">F.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2139" id="704">t</a> <a class="Datatype Operator" href="Cumulative.Statics.Full.html#1298" id="706">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2091" id="708">T</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="712">S⇒F-tm</a> <a class="Symbol" id="719">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1084" id="720">N-wf</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#725" id="725">i</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#727" id="727">⊢Γ</a><a class="Symbol" id="729">)</a>          <a class="Symbol" id="740">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1338" id="742">N-wf</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#725" id="747">i</a> <a class="Symbol" id="749">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#513" id="750">S⇒F-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#727" id="756">⊢Γ</a><a class="Symbol" id="758">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="762">S⇒F-tm</a> <a class="Symbol" id="769">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1179" id="770">Se-wf</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#776" id="776">i</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#778" id="778">⊢Γ</a><a class="Symbol" id="780">)</a>         <a class="Symbol" id="790">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1433" id="792">Se-wf</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#776" id="798">i</a> <a class="Symbol" id="800">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#513" id="801">S⇒F-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#778" id="807">⊢Γ</a><a class="Symbol" id="809">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="813">S⇒F-tm</a> <a class="Symbol" id="820">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1286" id="821">Π-wf</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#826" id="826">⊢S</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#829" id="829">⊢T</a><a class="Symbol" id="831">)</a>         <a class="Symbol" id="841">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1540" id="843">Π-wf</a> <a class="Symbol" id="848">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="849">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#826" id="856">⊢S</a><a class="Symbol" id="858">)</a> <a class="Symbol" id="860">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="861">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#829" id="868">⊢T</a><a class="Symbol" id="870">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="874">S⇒F-tm</a> <a class="Symbol" id="881">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1436" id="882">vlookup</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#890" id="890">⊢Γ</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#893" id="893">T∈Γ</a><a class="Symbol" id="896">)</a>     <a class="Symbol" id="902">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1690" id="904">vlookup</a> <a class="Symbol" id="912">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#513" id="913">S⇒F-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#890" id="919">⊢Γ</a><a class="Symbol" id="921">)</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#893" id="923">T∈Γ</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="929">S⇒F-tm</a> <a class="Symbol" id="936">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1558" id="937">ze-I</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#942" id="942">⊢Γ</a><a class="Symbol" id="944">)</a>            <a class="Symbol" id="957">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1812" id="959">ze-I</a> <a class="Symbol" id="964">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#513" id="965">S⇒F-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#942" id="971">⊢Γ</a><a class="Symbol" id="973">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="977">S⇒F-tm</a> <a class="Symbol" id="984">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1629" id="985">su-I</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#990" id="990">⊢t</a><a class="Symbol" id="992">)</a>            <a class="Symbol" id="1005">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1883" id="1007">su-I</a> <a class="Symbol" id="1012">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1013">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#990" id="1020">⊢t</a><a class="Symbol" id="1022">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1026">S⇒F-tm</a> <a class="Symbol" id="1033">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1710" id="1034">N-E</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1038" id="1038">⊢T</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1041" id="1041">⊢s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1044" id="1044">⊢w</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1047" id="1047">⊢t</a><a class="Symbol" id="1049">)</a>    <a class="Symbol" id="1054">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1964" id="1056">N-E</a> <a class="Symbol" id="1060">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1061">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1038" id="1068">⊢T</a><a class="Symbol" id="1070">)</a> <a class="Symbol" id="1072">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1073">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1041" id="1080">⊢s</a><a class="Symbol" id="1082">)</a> <a class="Symbol" id="1084">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1085">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1044" id="1092">⊢w</a><a class="Symbol" id="1094">)</a> <a class="Symbol" id="1096">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1097">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1047" id="1104">⊢t</a><a class="Symbol" id="1106">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1110">S⇒F-tm</a> <a class="Symbol" id="1117">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1966" id="1118">Λ-I</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1122" id="1122">⊢t</a><a class="Symbol" id="1124">)</a>
    <a class="Keyword" id="1130">with</a> <a class="Function" href="Cumulative.Statics.Presup.html#515" id="1135">presup-tm</a> <a class="Symbol" id="1145">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1146">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1122" id="1153">⊢t</a><a class="Symbol" id="1155">)</a>
  <a class="Symbol" id="1159">...</a> <a class="Symbol" id="1163">|</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#803" id="1165">⊢∷</a> <a class="Symbol" id="1168">_</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1170" id="1170">⊢S</a> <a class="InductiveConstructor Operator" href="Agda.Builtin.Sigma.html#235" id="1173">,</a> <a class="Symbol" id="1175">_</a>           <a class="Symbol" id="1187">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#2220" id="1189">Λ-I</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1170" id="1193">⊢S</a> <a class="Symbol" id="1196">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1197">S⇒F-tm</a> <a class="Bound" id="1204">⊢t</a><a class="Symbol" id="1206">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1210">S⇒F-tm</a> <a class="Symbol" id="1217">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2059" id="1218">Λ-E</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1222" id="1222">⊢T</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1225" id="1225">⊢t</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1228" id="1228">⊢w</a><a class="Symbol" id="1230">)</a>
    <a class="Keyword" id="1236">with</a> <a class="Function" href="Cumulative.Statics.Presup.html#515" id="1241">presup-tm</a> <a class="Symbol" id="1251">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1252">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1225" id="1259">⊢t</a><a class="Symbol" id="1261">)</a>
  <a class="Symbol" id="1265">...</a>  <a class="Symbol" id="1270">|</a> <a class="Symbol" id="1272">_</a> <a class="InductiveConstructor Operator" href="Agda.Builtin.Sigma.html#235" id="1274">,</a> <a class="Symbol" id="1276">_</a> <a class="InductiveConstructor Operator" href="Agda.Builtin.Sigma.html#235" id="1278">,</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1280" id="1280">⊢Π</a>
       <a class="Keyword" id="1290">with</a> <a class="Function" href="Cumulative.Statics.Properties.Pi.html#319" id="1295">inv-Π-wf′</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1280" id="1305">⊢Π</a>
  <a class="Symbol" id="1310">...</a>     <a class="Symbol" id="1318">|</a> <a class="Symbol" id="1320">_</a> <a class="InductiveConstructor Operator" href="Agda.Builtin.Sigma.html#235" id="1322">,</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1324" id="1324">⊢S</a>            <a class="Symbol" id="1338">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#2398" id="1340">Λ-E</a> <a class="Symbol" id="1344">(</a><a class="Function" href="Cumulative.Statics.Misc.html#627" id="1345">lift-⊢-Se-max</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1324" id="1359">⊢S</a><a class="Symbol" id="1361">)</a> <a class="Symbol" id="1363">(</a><a class="Function" href="Cumulative.Statics.Misc.html#735" id="1364">lift-⊢-Se-max′</a> <a class="Symbol" id="1379">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1380">S⇒F-tm</a> <a class="Bound" id="1387">⊢T</a><a class="Symbol" id="1389">))</a> <a class="Symbol" id="1392">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1393">S⇒F-tm</a> <a class="Bound" id="1400">⊢t</a><a class="Symbol" id="1402">)</a> <a class="Symbol" id="1404">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1405">S⇒F-tm</a> <a class="Bound" id="1412">⊢w</a><a class="Symbol" id="1414">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1418">S⇒F-tm</a> <a class="Symbol" id="1425">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2302" id="1426">t[σ]</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1431" id="1431">⊢t</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1434" id="1434">⊢σ</a><a class="Symbol" id="1436">)</a>         <a class="Symbol" id="1446">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#2670" id="1448">t[σ]</a> <a class="Symbol" id="1453">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1454">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1431" id="1461">⊢t</a><a class="Symbol" id="1463">)</a> <a class="Symbol" id="1465">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1466">S⇒F-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1434" id="1472">⊢σ</a><a class="Symbol" id="1474">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1478">S⇒F-tm</a> <a class="Symbol" id="1485">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2427" id="1486">cumu</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1491" id="1491">⊢t</a><a class="Symbol" id="1493">)</a>            <a class="Symbol" id="1506">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#2795" id="1508">cumu</a> <a class="Symbol" id="1513">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1514">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1491" id="1521">⊢t</a><a class="Symbol" id="1523">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1527">S⇒F-tm</a> <a class="Symbol" id="1534">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2544" id="1535">conv</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1540" id="1540">⊢t</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1543" id="1543">T≈T′</a><a class="Symbol" id="1547">)</a>       <a class="Symbol" id="1555">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#2912" id="1557">conv</a> <a class="Symbol" id="1562">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1563">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1540" id="1570">⊢t</a><a class="Symbol" id="1572">)</a> <a class="Symbol" id="1574">(</a><a class="Function" href="Cumulative.Statics.Equiv.html#6990" id="1575">C⇒F-≈</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1543" id="1581">T≈T′</a><a class="Symbol" id="1585">)</a>


  <a id="S⇒F-s"></a><a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1591">S⇒F-s</a> <a class="Symbol" id="1597">:</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="1599">Γ</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#2688" id="1601">S.⊢s</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2187" id="1606">σ</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#2688" id="1608">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2123" id="1610">Δ</a> <a class="Symbol" id="1612">→</a>
          <a class="Comment" id="1624">--------------</a>
          <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="1649">Γ</a> <a class="Datatype Operator" href="Cumulative.Statics.Full.html#3054" id="1651">F.⊢s</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2187" id="1656">σ</a> <a class="Datatype Operator" href="Cumulative.Statics.Full.html#3054" id="1658">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2123" id="1660">Δ</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1664">S⇒F-s</a> <a class="Symbol" id="1670">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2731" id="1671">s-I</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1675" id="1675">⊢Γ</a><a class="Symbol" id="1677">)</a>           <a class="Symbol" id="1689">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#3097" id="1691">s-I</a> <a class="Symbol" id="1695">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#513" id="1696">S⇒F-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1675" id="1702">⊢Γ</a><a class="Symbol" id="1704">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1708">S⇒F-s</a> <a class="Symbol" id="1714">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2800" id="1715">s-wk</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1720" id="1720">⊢TΓ</a><a class="Symbol" id="1723">)</a>         <a class="Symbol" id="1733">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#3166" id="1735">s-wk</a> <a class="Symbol" id="1740">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#513" id="1741">S⇒F-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1720" id="1747">⊢TΓ</a><a class="Symbol" id="1750">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1754">S⇒F-s</a> <a class="Symbol" id="1760">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2884" id="1761">s-∘</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1765" id="1765">⊢σ</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1768" id="1768">⊢δ</a><a class="Symbol" id="1770">)</a>        <a class="Symbol" id="1779">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#3250" id="1781">s-∘</a> <a class="Symbol" id="1785">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1786">S⇒F-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1765" id="1792">⊢σ</a><a class="Symbol" id="1794">)</a> <a class="Symbol" id="1796">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1797">S⇒F-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1768" id="1803">⊢δ</a><a class="Symbol" id="1805">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1809">S⇒F-s</a> <a class="Symbol" id="1815">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2998" id="1816">s-,</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1820" id="1820">⊢σ</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1823" id="1823">⊢T</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1826" id="1826">⊢t</a><a class="Symbol" id="1828">)</a>     <a class="Symbol" id="1834">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#3364" id="1836">s-,</a> <a class="Symbol" id="1840">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1841">S⇒F-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1820" id="1847">⊢σ</a><a class="Symbol" id="1849">)</a> <a class="Symbol" id="1851">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1852">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1823" id="1859">⊢T</a><a class="Symbol" id="1861">)</a> <a class="Symbol" id="1863">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#639" id="1864">S⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1826" id="1871">⊢t</a><a class="Symbol" id="1873">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1877">S⇒F-s</a> <a class="Symbol" id="1883">(</a><a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#3169" id="1884">s-conv</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1891" id="1891">⊢σ</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1894" id="1894">Δ′≈Δ</a><a class="Symbol" id="1898">)</a>   <a class="Symbol" id="1902">=</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#3535" id="1904">s-conv</a> <a class="Symbol" id="1911">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1591" id="1912">S⇒F-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1891" id="1918">⊢σ</a><a class="Symbol" id="1920">)</a> <a class="Symbol" id="1922">(</a><a class="Function" href="Cumulative.Statics.Equiv.html#6629" id="1923">C⇒F-⊢≈</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#1894" id="1930">Δ′≈Δ</a><a class="Symbol" id="1934">)</a>


<a class="Keyword" id="1938">mutual</a>
  <a id="F⇒S-⊢"></a><a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="1947">F⇒S-⊢</a> <a class="Symbol" id="1953">:</a> <a class="Datatype Operator" href="Cumulative.Statics.Full.html#763" id="1955">F.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="1959">Γ</a> <a class="Symbol" id="1961">→</a>
          <a class="Comment" id="1973">-------</a>
          <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#898" id="1991">S.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="1995">Γ</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="1999">F⇒S-⊢</a> <a class="InductiveConstructor" href="Cumulative.Statics.Full.html#788" id="2005">⊢[]</a>        <a class="Symbol" id="2016">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#923" id="2018">⊢[]</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="2024">F⇒S-⊢</a> <a class="Symbol" id="2030">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#803" id="2031">⊢∷</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2034" id="2034">⊢Γ</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2037" id="2037">⊢T</a><a class="Symbol" id="2039">)</a> <a class="Symbol" id="2041">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#938" id="2043">⊢∷</a> <a class="Symbol" id="2046">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="2047">F⇒S-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2034" id="2053">⊢Γ</a><a class="Symbol" id="2055">)</a> <a class="Symbol" id="2057">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2058">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2037" id="2065">⊢T</a><a class="Symbol" id="2067">)</a>


  <a id="F⇒S-tm"></a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2073">F⇒S-tm</a> <a class="Symbol" id="2080">:</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="2082">Γ</a> <a class="Datatype Operator" href="Cumulative.Statics.Full.html#1298" id="2084">F.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2139" id="2088">t</a> <a class="Datatype Operator" href="Cumulative.Statics.Full.html#1298" id="2090">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2091" id="2092">T</a> <a class="Symbol" id="2094">→</a>
           <a class="Comment" id="2107">-------------</a>
           <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="2132">Γ</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#1044" id="2134">S.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2139" id="2138">t</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#1044" id="2140">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2091" id="2142">T</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2146">F⇒S-tm</a> <a class="Symbol" id="2153">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1338" id="2154">N-wf</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2159" id="2159">i</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2161" id="2161">⊢Γ</a><a class="Symbol" id="2163">)</a>          <a class="Symbol" id="2174">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1084" id="2176">N-wf</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2159" id="2181">i</a> <a class="Symbol" id="2183">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="2184">F⇒S-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2161" id="2190">⊢Γ</a><a class="Symbol" id="2192">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2196">F⇒S-tm</a> <a class="Symbol" id="2203">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1433" id="2204">Se-wf</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2210" id="2210">i</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2212" id="2212">⊢Γ</a><a class="Symbol" id="2214">)</a>         <a class="Symbol" id="2224">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1179" id="2226">Se-wf</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2210" id="2232">i</a> <a class="Symbol" id="2234">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="2235">F⇒S-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2212" id="2241">⊢Γ</a><a class="Symbol" id="2243">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2247">F⇒S-tm</a> <a class="Symbol" id="2254">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1540" id="2255">Π-wf</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2260" id="2260">⊢S</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2263" id="2263">⊢T</a><a class="Symbol" id="2265">)</a>         <a class="Symbol" id="2275">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1286" id="2277">Π-wf</a> <a class="Symbol" id="2282">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2283">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2260" id="2290">⊢S</a><a class="Symbol" id="2292">)</a> <a class="Symbol" id="2294">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2295">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2263" id="2302">⊢T</a><a class="Symbol" id="2304">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2308">F⇒S-tm</a> <a class="Symbol" id="2315">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1690" id="2316">vlookup</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2324" id="2324">⊢Γ</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2327" id="2327">T∈Γ</a><a class="Symbol" id="2330">)</a>     <a class="Symbol" id="2336">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1436" id="2338">vlookup</a> <a class="Symbol" id="2346">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="2347">F⇒S-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2324" id="2353">⊢Γ</a><a class="Symbol" id="2355">)</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2327" id="2357">T∈Γ</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2363">F⇒S-tm</a> <a class="Symbol" id="2370">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1812" id="2371">ze-I</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2376" id="2376">⊢Γ</a><a class="Symbol" id="2378">)</a>            <a class="Symbol" id="2391">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1558" id="2393">ze-I</a> <a class="Symbol" id="2398">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="2399">F⇒S-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2376" id="2405">⊢Γ</a><a class="Symbol" id="2407">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2411">F⇒S-tm</a> <a class="Symbol" id="2418">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1883" id="2419">su-I</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2424" id="2424">⊢t</a><a class="Symbol" id="2426">)</a>            <a class="Symbol" id="2439">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1629" id="2441">su-I</a> <a class="Symbol" id="2446">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2447">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2424" id="2454">⊢t</a><a class="Symbol" id="2456">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2460">F⇒S-tm</a> <a class="Symbol" id="2467">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#1964" id="2468">N-E</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2472" id="2472">⊢T</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2475" id="2475">⊢s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2478" id="2478">⊢w</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2481" id="2481">⊢t</a><a class="Symbol" id="2483">)</a>    <a class="Symbol" id="2488">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1710" id="2490">N-E</a> <a class="Symbol" id="2494">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2495">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2472" id="2502">⊢T</a><a class="Symbol" id="2504">)</a> <a class="Symbol" id="2506">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2507">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2475" id="2514">⊢s</a><a class="Symbol" id="2516">)</a> <a class="Symbol" id="2518">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2519">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2478" id="2526">⊢w</a><a class="Symbol" id="2528">)</a> <a class="Symbol" id="2530">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2531">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2481" id="2538">⊢t</a><a class="Symbol" id="2540">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2544">F⇒S-tm</a> <a class="Symbol" id="2551">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#2220" id="2552">Λ-I</a> <a class="Symbol" id="2556">_</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2558" id="2558">⊢t</a><a class="Symbol" id="2560">)</a>           <a class="Symbol" id="2572">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#1966" id="2574">Λ-I</a> <a class="Symbol" id="2578">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2579">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2558" id="2586">⊢t</a><a class="Symbol" id="2588">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2592">F⇒S-tm</a> <a class="Symbol" id="2599">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#2398" id="2600">Λ-E</a> <a class="Symbol" id="2604">_</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2606" id="2606">⊢T</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2609" id="2609">⊢t</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2612" id="2612">⊢w</a><a class="Symbol" id="2614">)</a>     <a class="Symbol" id="2620">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2059" id="2622">Λ-E</a> <a class="Symbol" id="2626">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2627">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2606" id="2634">⊢T</a><a class="Symbol" id="2636">)</a> <a class="Symbol" id="2638">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2639">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2609" id="2646">⊢t</a><a class="Symbol" id="2648">)</a> <a class="Symbol" id="2650">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2651">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2612" id="2658">⊢w</a><a class="Symbol" id="2660">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2664">F⇒S-tm</a> <a class="Symbol" id="2671">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#2670" id="2672">t[σ]</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2677" id="2677">⊢t</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2680" id="2680">⊢σ</a><a class="Symbol" id="2682">)</a>         <a class="Symbol" id="2692">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2302" id="2694">t[σ]</a> <a class="Symbol" id="2699">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2700">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2677" id="2707">⊢t</a><a class="Symbol" id="2709">)</a> <a class="Symbol" id="2711">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="2712">F⇒S-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2680" id="2718">⊢σ</a><a class="Symbol" id="2720">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2724">F⇒S-tm</a> <a class="Symbol" id="2731">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#2795" id="2732">cumu</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2737" id="2737">⊢t</a><a class="Symbol" id="2739">)</a>            <a class="Symbol" id="2752">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2427" id="2754">cumu</a> <a class="Symbol" id="2759">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2760">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2737" id="2767">⊢t</a><a class="Symbol" id="2769">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2773">F⇒S-tm</a> <a class="Symbol" id="2780">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#2912" id="2781">conv</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2786" id="2786">⊢t</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2789" id="2789">T≈T′</a><a class="Symbol" id="2793">)</a>       <a class="Symbol" id="2801">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2544" id="2803">conv</a> <a class="Symbol" id="2808">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="2809">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2786" id="2816">⊢t</a><a class="Symbol" id="2818">)</a> <a class="Symbol" id="2820">(</a><a class="Function" href="Cumulative.Statics.Equiv.html#1797" id="2821">F⇒C-≈</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2789" id="2827">T≈T′</a><a class="Symbol" id="2831">)</a>


  <a id="F⇒S-s"></a><a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="2837">F⇒S-s</a> <a class="Symbol" id="2843">:</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="2845">Γ</a> <a class="Datatype Operator" href="Cumulative.Statics.Full.html#3054" id="2847">F.⊢s</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2187" id="2852">σ</a> <a class="Datatype Operator" href="Cumulative.Statics.Full.html#3054" id="2854">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2123" id="2856">Δ</a> <a class="Symbol" id="2858">→</a>
          <a class="Comment" id="2870">--------------</a>
          <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="2895">Γ</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#2688" id="2897">S.⊢s</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2187" id="2902">σ</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#2688" id="2904">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2123" id="2906">Δ</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="2910">F⇒S-s</a> <a class="Symbol" id="2916">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#3097" id="2917">s-I</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2921" id="2921">⊢Γ</a><a class="Symbol" id="2923">)</a>           <a class="Symbol" id="2935">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2731" id="2937">s-I</a> <a class="Symbol" id="2941">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="2942">F⇒S-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2921" id="2948">⊢Γ</a><a class="Symbol" id="2950">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="2954">F⇒S-s</a> <a class="Symbol" id="2960">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#3166" id="2961">s-wk</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2966" id="2966">⊢Γ</a><a class="Symbol" id="2968">)</a>          <a class="Symbol" id="2979">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2800" id="2981">s-wk</a> <a class="Symbol" id="2986">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="2987">F⇒S-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#2966" id="2993">⊢Γ</a><a class="Symbol" id="2995">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="2999">F⇒S-s</a> <a class="Symbol" id="3005">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#3250" id="3006">s-∘</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3010" id="3010">⊢σ</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3013" id="3013">⊢δ</a><a class="Symbol" id="3015">)</a>        <a class="Symbol" id="3024">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2884" id="3026">s-∘</a> <a class="Symbol" id="3030">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="3031">F⇒S-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3010" id="3037">⊢σ</a><a class="Symbol" id="3039">)</a> <a class="Symbol" id="3041">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="3042">F⇒S-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3013" id="3048">⊢δ</a><a class="Symbol" id="3050">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="3054">F⇒S-s</a> <a class="Symbol" id="3060">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#3364" id="3061">s-,</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3065" id="3065">⊢σ</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3068" id="3068">⊢T</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3071" id="3071">⊢t</a><a class="Symbol" id="3073">)</a>     <a class="Symbol" id="3079">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#2998" id="3081">s-,</a> <a class="Symbol" id="3085">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="3086">F⇒S-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3065" id="3092">⊢σ</a><a class="Symbol" id="3094">)</a> <a class="Symbol" id="3096">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="3097">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3068" id="3104">⊢T</a><a class="Symbol" id="3106">)</a> <a class="Symbol" id="3108">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="3109">F⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3071" id="3116">⊢t</a><a class="Symbol" id="3118">)</a>
  <a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="3122">F⇒S-s</a> <a class="Symbol" id="3128">(</a><a class="InductiveConstructor" href="Cumulative.Statics.Full.html#3535" id="3129">s-conv</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3136" id="3136">⊢σ</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3139" id="3139">Δ′≈Δ</a><a class="Symbol" id="3143">)</a>   <a class="Symbol" id="3147">=</a> <a class="InductiveConstructor" href="Cumulative.Soundness.Typing.html#3169" id="3149">s-conv</a> <a class="Symbol" id="3156">(</a><a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="3157">F⇒S-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3136" id="3163">⊢σ</a><a class="Symbol" id="3165">)</a> <a class="Symbol" id="3167">(</a><a class="Function" href="Cumulative.Statics.Equiv.html#1620" id="3168">F⇒C-⊢≈</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3139" id="3175">Δ′≈Δ</a><a class="Symbol" id="3179">)</a>


<a class="Comment" id="3183">-- The Concise formulation is subsumed by the Sound formulation.</a>
<a class="Comment" id="3248">--</a>
<a class="Comment" id="3251">-- We could have also proved the other direction but in the actual proof we do not</a>
<a class="Comment" id="3334">-- need this property.</a>
<a id="C⇒S-⊢"></a><a class="Function" href="Cumulative.Soundness.Equiv.html#3357" id="3357">C⇒S-⊢</a> <a class="Symbol" id="3363">:</a> <a class="Datatype Operator" href="Cumulative.Statics.Concise.html#529" id="3365">C.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="3369">Γ</a> <a class="Symbol" id="3371">→</a>
        <a class="Comment" id="3381">-------</a>
        <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#898" id="3397">S.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="3401">Γ</a>
<a class="Function" href="Cumulative.Soundness.Equiv.html#3357" id="3403">C⇒S-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3409" id="3409">⊢Γ</a> <a class="Symbol" id="3412">=</a> <a class="Function" href="Cumulative.Soundness.Equiv.html#1947" id="3414">F⇒S-⊢</a> <a class="Symbol" id="3420">(</a><a class="Function" href="Cumulative.Statics.Equiv.html#5042" id="3421">C⇒F-⊢</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3409" id="3427">⊢Γ</a><a class="Symbol" id="3429">)</a>


<a id="C⇒S-tm"></a><a class="Function" href="Cumulative.Soundness.Equiv.html#3433" id="3433">C⇒S-tm</a> <a class="Symbol" id="3440">:</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="3442">Γ</a> <a class="Datatype Operator" href="Cumulative.Statics.Concise.html#978" id="3444">C.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2139" id="3448">t</a> <a class="Datatype Operator" href="Cumulative.Statics.Concise.html#978" id="3450">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2091" id="3452">T</a> <a class="Symbol" id="3454">→</a>
         <a class="Comment" id="3465">-------------</a>
         <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="3488">Γ</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#1044" id="3490">S.⊢</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2139" id="3494">t</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#1044" id="3496">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2091" id="3498">T</a>
<a class="Function" href="Cumulative.Soundness.Equiv.html#3433" id="3500">C⇒S-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3507" id="3507">⊢t</a> <a class="Symbol" id="3510">=</a> <a class="Function" href="Cumulative.Soundness.Equiv.html#2073" id="3512">F⇒S-tm</a> <a class="Symbol" id="3519">(</a><a class="Function" href="Cumulative.Statics.Equiv.html#5168" id="3520">C⇒F-tm</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3507" id="3527">⊢t</a><a class="Symbol" id="3529">)</a>


<a id="C⇒S-s"></a><a class="Function" href="Cumulative.Soundness.Equiv.html#3533" id="3533">C⇒S-s</a> <a class="Symbol" id="3539">:</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="3541">Γ</a> <a class="Datatype Operator" href="Cumulative.Statics.Concise.html#2553" id="3543">C.⊢s</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2187" id="3548">σ</a> <a class="Datatype Operator" href="Cumulative.Statics.Concise.html#2553" id="3550">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2123" id="3552">Δ</a> <a class="Symbol" id="3554">→</a>
        <a class="Comment" id="3564">--------------</a>
        <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2107" id="3587">Γ</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#2688" id="3589">S.⊢s</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2187" id="3594">σ</a> <a class="Datatype Operator" href="Cumulative.Soundness.Typing.html#2688" id="3596">∶</a> <a class="Generalizable" href="Cumulative.Statics.Syntax.html#2123" id="3598">Δ</a>
<a class="Function" href="Cumulative.Soundness.Equiv.html#3533" id="3600">C⇒S-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3606" id="3606">⊢σ</a> <a class="Symbol" id="3609">=</a> <a class="Function" href="Cumulative.Soundness.Equiv.html#2837" id="3611">F⇒S-s</a> <a class="Symbol" id="3617">(</a><a class="Function" href="Cumulative.Statics.Equiv.html#6280" id="3618">C⇒F-s</a> <a class="Bound" href="Cumulative.Soundness.Equiv.html#3606" id="3624">⊢σ</a><a class="Symbol" id="3626">)</a>
</pre></div></body></html>