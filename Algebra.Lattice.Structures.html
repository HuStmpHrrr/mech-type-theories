<!DOCTYPE HTML>

<html><head><meta charset="utf-8"/><title>Algebra.Lattice.Structures</title><link href="Agda.css" rel="stylesheet"/><script src="highlight-hover.js" type="text/javascript"></script></head><body><div class="navbar"><a class="active" href="README.html">Everything</a><a href="index.html" style="float:right">Home</a></div><div class="main"><pre class="Agda"><a class="Comment" id="1">------------------------------------------------------------------------</a>
<a class="Comment" id="74">-- The Agda standard library</a>
<a class="Comment" id="103">--</a>
<a class="Comment" id="106">-- Some lattice-like structures defined by properties of _∧_ and _∨_</a>
<a class="Comment" id="175">-- (not packed up with sets, operations, etc.)</a>
<a class="Comment" id="222">--</a>
<a class="Comment" id="225">-- For lattices defined via an order relation, see</a>
<a class="Comment" id="276">-- Relation.Binary.Lattice.</a>
<a class="Comment" id="304">------------------------------------------------------------------------</a>

<a class="Comment" id="378">-- The contents of this module should be accessed via `Algebra.Lattice`,</a>
<a class="Comment" id="451">-- unless you want to parameterise it via the equality relation.</a>

<a class="Symbol" id="517">{-#</a> <a class="Keyword" id="521">OPTIONS</a> <a class="Pragma" id="529">--cubical-compatible</a> <a class="Pragma" id="550">--safe</a> <a class="Symbol" id="557">#-}</a>

<a class="Keyword" id="562">open</a> <a class="Keyword" id="567">import</a> <a class="Module" href="Algebra.Core.html" id="574">Algebra.Core</a> <a class="Keyword" id="587">using</a> <a class="Symbol" id="593">(</a><a class="Function" href="Algebra.Core.html#484" id="594">Op₁</a><a class="Symbol" id="597">;</a> <a class="Function" href="Algebra.Core.html#527" id="599">Op₂</a><a class="Symbol" id="602">)</a>
<a class="Keyword" id="604">open</a> <a class="Keyword" id="609">import</a> <a class="Module" href="Data.Product.Base.html" id="616">Data.Product.Base</a> <a class="Keyword" id="634">using</a> <a class="Symbol" id="640">(</a><a class="Field" href="Data.Product.Base.html#636" id="641">proj₁</a><a class="Symbol" id="646">;</a> <a class="Field" href="Data.Product.Base.html#650" id="648">proj₂</a><a class="Symbol" id="653">)</a>
<a class="Keyword" id="655">open</a> <a class="Keyword" id="660">import</a> <a class="Module" href="Level.html" id="667">Level</a> <a class="Keyword" id="673">using</a> <a class="Symbol" id="679">(</a><a class="Primitive Operator" href="Agda.Primitive.html#961" id="680">_⊔_</a><a class="Symbol" id="683">)</a>
<a class="Keyword" id="685">open</a> <a class="Keyword" id="690">import</a> <a class="Module" href="Relation.Binary.Core.html" id="697">Relation.Binary.Core</a> <a class="Keyword" id="718">using</a> <a class="Symbol" id="724">(</a><a class="Function" href="Relation.Binary.Core.html#896" id="725">Rel</a><a class="Symbol" id="728">)</a>
<a class="Keyword" id="730">open</a> <a class="Keyword" id="735">import</a> <a class="Module" href="Relation.Binary.Bundles.html" id="742">Relation.Binary.Bundles</a> <a class="Keyword" id="766">using</a> <a class="Symbol" id="772">(</a><a class="Record" href="Relation.Binary.Bundles.html#1235" id="773">Setoid</a><a class="Symbol" id="779">)</a>
<a class="Keyword" id="781">open</a> <a class="Keyword" id="786">import</a> <a class="Module" href="Relation.Binary.Structures.html" id="793">Relation.Binary.Structures</a> <a class="Keyword" id="820">using</a> <a class="Symbol" id="826">(</a><a class="Record" href="Relation.Binary.Structures.html#1595" id="827">IsEquivalence</a><a class="Symbol" id="840">)</a>

<a class="Keyword" id="843">module</a> <a class="Module" href="Algebra.Lattice.Structures.html" id="850">Algebra.Lattice.Structures</a>
  <a class="Symbol" id="879">{</a><a class="Bound" href="Algebra.Lattice.Structures.html#880" id="880">a</a> <a class="Bound" href="Algebra.Lattice.Structures.html#882" id="882">ℓ</a><a class="Symbol" id="883">}</a> <a class="Symbol" id="885">{</a><a class="Bound" href="Algebra.Lattice.Structures.html#886" id="886">A</a> <a class="Symbol" id="888">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="890">Set</a> <a class="Bound" href="Algebra.Lattice.Structures.html#880" id="894">a</a><a class="Symbol" id="895">}</a>  <a class="Comment" id="898">-- The underlying set</a>
  <a class="Symbol" id="922">(</a><a class="Bound Operator" href="Algebra.Lattice.Structures.html#923" id="923">_≈_</a> <a class="Symbol" id="927">:</a> <a class="Function" href="Relation.Binary.Core.html#896" id="929">Rel</a> <a class="Bound" href="Algebra.Lattice.Structures.html#886" id="933">A</a> <a class="Bound" href="Algebra.Lattice.Structures.html#882" id="935">ℓ</a><a class="Symbol" id="936">)</a>    <a class="Comment" id="941">-- The underlying equality relation</a>
  <a class="Keyword" id="979">where</a>

<a class="Keyword" id="986">open</a> <a class="Keyword" id="991">import</a> <a class="Module" href="Algebra.Definitions.html" id="998">Algebra.Definitions</a> <a class="Bound Operator" href="Algebra.Lattice.Structures.html#923" id="1018">_≈_</a>
<a class="Keyword" id="1022">open</a> <a class="Keyword" id="1027">import</a> <a class="Module" href="Algebra.Structures.html" id="1034">Algebra.Structures</a> <a class="Bound Operator" href="Algebra.Lattice.Structures.html#923" id="1053">_≈_</a>

<a class="Comment" id="1058">------------------------------------------------------------------------</a>
<a class="Comment" id="1131">-- Structures with 1 binary operation</a>

<a id="IsSemilattice"></a><a class="Function" href="Algebra.Lattice.Structures.html#1170" id="1170">IsSemilattice</a> <a class="Symbol" id="1184">=</a> <a class="Record" href="Algebra.Structures.html#3912" id="1186">IsCommutativeBand</a>
<a class="Keyword" id="1204">module</a> <a id="IsSemilattice"></a><a class="Module" href="Algebra.Lattice.Structures.html#1211" id="1211">IsSemilattice</a> <a class="Symbol" id="1225">{</a><a class="Bound" href="Algebra.Lattice.Structures.html#1226" id="1226">∙</a><a class="Symbol" id="1227">}</a> <a class="Symbol" id="1229">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#1230" id="1230">L</a> <a class="Symbol" id="1232">:</a> <a class="Function" href="Algebra.Lattice.Structures.html#1170" id="1234">IsSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#1226" id="1248">∙</a><a class="Symbol" id="1249">)</a> <a class="Keyword" id="1251">where</a>
  <a class="Keyword" id="1259">open</a> <a class="Module" href="Algebra.Structures.html#3912" id="1264">IsCommutativeBand</a> <a class="Bound" href="Algebra.Lattice.Structures.html#1230" id="1282">L</a> <a class="Keyword" id="1284">public</a>
    <a class="Keyword" id="1295">using</a> <a class="Symbol" id="1301">(</a><a class="Field" href="Algebra.Structures.html#3974" id="1302">isBand</a><a class="Symbol" id="1308">;</a> <a class="Field" href="Algebra.Structures.html#3996" id="1310">comm</a><a class="Symbol" id="1314">)</a>
  <a class="Keyword" id="1318">open</a> <a class="Module" href="Algebra.Structures.html#3453" id="1323">IsBand</a> <a class="Field" href="Algebra.Structures.html#3974" id="1330">isBand</a> <a class="Keyword" id="1337">public</a>

<a class="Comment" id="1345">-- Used to bring names appropriate for a meet semilattice into scope.</a>
<a id="IsMeetSemilattice"></a><a class="Function" href="Algebra.Lattice.Structures.html#1415" id="1415">IsMeetSemilattice</a> <a class="Symbol" id="1433">=</a> <a class="Function" href="Algebra.Lattice.Structures.html#1170" id="1435">IsSemilattice</a>
<a class="Keyword" id="1449">module</a> <a id="IsMeetSemilattice"></a><a class="Module" href="Algebra.Lattice.Structures.html#1456" id="1456">IsMeetSemilattice</a> <a class="Symbol" id="1474">{</a><a class="Bound" href="Algebra.Lattice.Structures.html#1475" id="1475">∧</a><a class="Symbol" id="1476">}</a> <a class="Symbol" id="1478">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#1479" id="1479">L</a> <a class="Symbol" id="1481">:</a> <a class="Function" href="Algebra.Lattice.Structures.html#1415" id="1483">IsMeetSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#1475" id="1501">∧</a><a class="Symbol" id="1502">)</a> <a class="Keyword" id="1504">where</a>
  <a class="Keyword" id="1512">open</a> <a class="Module" href="Algebra.Lattice.Structures.html#1211" id="1517">IsSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#1479" id="1531">L</a> <a class="Keyword" id="1533">public</a>
    <a class="Keyword" id="1544">renaming</a>
    <a class="Symbol" id="1557">(</a> <a class="Function" href="Algebra.Structures.html#1798" id="1559">∙-cong</a>  <a class="Symbol" id="1567">to</a> <a class="Function" id="1570">∧-cong</a>
    <a class="Symbol" id="1581">;</a> <a class="Function" href="Algebra.Consequences.Base.html#996" id="1583">∙-congˡ</a> <a class="Symbol" id="1591">to</a> <a class="Function" id="1594">∧-congˡ</a>
    <a class="Symbol" id="1606">;</a> <a class="Function" href="Algebra.Consequences.Base.html#1087" id="1608">∙-congʳ</a> <a class="Symbol" id="1616">to</a> <a class="Function" id="1619">∧-congʳ</a>
    <a class="Symbol" id="1631">)</a>

<a class="Comment" id="1634">-- Used to bring names appropriate for a join semilattice into scope.</a>
<a id="IsJoinSemilattice"></a><a class="Function" href="Algebra.Lattice.Structures.html#1704" id="1704">IsJoinSemilattice</a> <a class="Symbol" id="1722">=</a> <a class="Function" href="Algebra.Lattice.Structures.html#1170" id="1724">IsSemilattice</a>
<a class="Keyword" id="1738">module</a> <a id="IsJoinSemilattice"></a><a class="Module" href="Algebra.Lattice.Structures.html#1745" id="1745">IsJoinSemilattice</a> <a class="Symbol" id="1763">{</a><a class="Bound" href="Algebra.Lattice.Structures.html#1764" id="1764">∨</a><a class="Symbol" id="1765">}</a> <a class="Symbol" id="1767">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#1768" id="1768">L</a> <a class="Symbol" id="1770">:</a> <a class="Function" href="Algebra.Lattice.Structures.html#1704" id="1772">IsJoinSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#1764" id="1790">∨</a><a class="Symbol" id="1791">)</a> <a class="Keyword" id="1793">where</a>
  <a class="Keyword" id="1801">open</a> <a class="Module" href="Algebra.Lattice.Structures.html#1211" id="1806">IsSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#1768" id="1820">L</a> <a class="Keyword" id="1822">public</a>
    <a class="Keyword" id="1833">renaming</a>
    <a class="Symbol" id="1846">(</a> <a class="Function" href="Algebra.Structures.html#1798" id="1848">∙-cong</a>  <a class="Symbol" id="1856">to</a> <a class="Function" id="1859">∨-cong</a>
    <a class="Symbol" id="1870">;</a> <a class="Function" href="Algebra.Consequences.Base.html#996" id="1872">∙-congˡ</a> <a class="Symbol" id="1880">to</a> <a class="Function" id="1883">∨-congˡ</a>
    <a class="Symbol" id="1895">;</a> <a class="Function" href="Algebra.Consequences.Base.html#1087" id="1897">∙-congʳ</a> <a class="Symbol" id="1905">to</a> <a class="Function" id="1908">∨-congʳ</a>
    <a class="Symbol" id="1920">)</a>

<a class="Comment" id="1923">------------------------------------------------------------------------</a>
<a class="Comment" id="1996">-- Structures with 1 binary operation &amp; 1 element</a>

<a class="Comment" id="2047">-- A bounded semi-lattice is the same thing as an idempotent commutative</a>
<a class="Comment" id="2120">-- monoid.</a>
<a id="IsBoundedSemilattice"></a><a class="Function" href="Algebra.Lattice.Structures.html#2131" id="2131">IsBoundedSemilattice</a> <a class="Symbol" id="2152">=</a> <a class="Record" href="Algebra.Structures.html#5821" id="2154">IsIdempotentCommutativeMonoid</a>
<a class="Keyword" id="2184">module</a> <a id="IsBoundedSemilattice"></a><a class="Module" href="Algebra.Lattice.Structures.html#2191" id="2191">IsBoundedSemilattice</a> <a class="Symbol" id="2212">{</a><a class="Bound" href="Algebra.Lattice.Structures.html#2213" id="2213">∙</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2215" id="2215">ε</a><a class="Symbol" id="2216">}</a> <a class="Symbol" id="2218">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#2219" id="2219">L</a> <a class="Symbol" id="2221">:</a> <a class="Function" href="Algebra.Lattice.Structures.html#2131" id="2223">IsBoundedSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2213" id="2244">∙</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2215" id="2246">ε</a><a class="Symbol" id="2247">)</a> <a class="Keyword" id="2249">where</a>

  <a class="Keyword" id="2258">open</a> <a class="Module" href="Algebra.Structures.html#5821" id="2263">IsIdempotentCommutativeMonoid</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2219" id="2293">L</a> <a class="Keyword" id="2295">public</a>
    <a class="Keyword" id="2306">renaming</a> <a class="Symbol" id="2315">(</a><a class="Function" href="Algebra.Structures.html#6270" id="2316">isCommutativeBand</a> <a class="Symbol" id="2334">to</a> <a class="Function" id="2337">isSemilattice</a><a class="Symbol" id="2350">)</a>


<a class="Comment" id="2354">-- Used to bring names appropriate for a bounded meet semilattice</a>
<a class="Comment" id="2420">-- into scope.</a>
<a id="IsBoundedMeetSemilattice"></a><a class="Function" href="Algebra.Lattice.Structures.html#2435" id="2435">IsBoundedMeetSemilattice</a> <a class="Symbol" id="2460">=</a> <a class="Function" href="Algebra.Lattice.Structures.html#2131" id="2462">IsBoundedSemilattice</a>
<a class="Keyword" id="2483">module</a> <a id="IsBoundedMeetSemilattice"></a><a class="Module" href="Algebra.Lattice.Structures.html#2490" id="2490">IsBoundedMeetSemilattice</a> <a class="Symbol" id="2515">{</a><a class="Bound" href="Algebra.Lattice.Structures.html#2516" id="2516">∧</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2518" id="2518">⊤</a><a class="Symbol" id="2519">}</a> <a class="Symbol" id="2521">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#2522" id="2522">L</a> <a class="Symbol" id="2524">:</a> <a class="Function" href="Algebra.Lattice.Structures.html#2435" id="2526">IsBoundedMeetSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2516" id="2551">∧</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2518" id="2553">⊤</a><a class="Symbol" id="2554">)</a>
  <a class="Keyword" id="2558">where</a>

  <a class="Keyword" id="2567">open</a> <a class="Module" href="Algebra.Lattice.Structures.html#2191" id="2572">IsBoundedSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2522" id="2593">L</a> <a class="Keyword" id="2595">public</a>
    <a class="Keyword" id="2606">using</a> <a class="Symbol" id="2612">(</a><a class="Function" href="Algebra.Structures.html#4847" id="2613">identity</a><a class="Symbol" id="2621">;</a> <a class="Function" href="Algebra.Structures.html#4916" id="2623">identityˡ</a><a class="Symbol" id="2632">;</a> <a class="Function" href="Algebra.Structures.html#4977" id="2634">identityʳ</a><a class="Symbol" id="2643">)</a>
    <a class="Keyword" id="2649">renaming</a> <a class="Symbol" id="2658">(</a><a class="Function" href="Algebra.Lattice.Structures.html#2337" id="2659">isSemilattice</a> <a class="Symbol" id="2673">to</a> <a class="Function" id="2676">isMeetSemilattice</a><a class="Symbol" id="2693">)</a>

  <a class="Keyword" id="2698">open</a> <a class="Module" href="Algebra.Lattice.Structures.html#1456" id="2703">IsMeetSemilattice</a> <a class="Function" href="Algebra.Lattice.Structures.html#2676" id="2721">isMeetSemilattice</a> <a class="Keyword" id="2739">public</a>


<a class="Comment" id="2748">-- Used to bring names appropriate for a bounded join semilattice</a>
<a class="Comment" id="2814">-- into scope.</a>
<a id="IsBoundedJoinSemilattice"></a><a class="Function" href="Algebra.Lattice.Structures.html#2829" id="2829">IsBoundedJoinSemilattice</a> <a class="Symbol" id="2854">=</a> <a class="Function" href="Algebra.Lattice.Structures.html#2131" id="2856">IsBoundedSemilattice</a>
<a class="Keyword" id="2877">module</a> <a id="IsBoundedJoinSemilattice"></a><a class="Module" href="Algebra.Lattice.Structures.html#2884" id="2884">IsBoundedJoinSemilattice</a> <a class="Symbol" id="2909">{</a><a class="Bound" href="Algebra.Lattice.Structures.html#2910" id="2910">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2912" id="2912">⊥</a><a class="Symbol" id="2913">}</a> <a class="Symbol" id="2915">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#2916" id="2916">L</a> <a class="Symbol" id="2918">:</a> <a class="Function" href="Algebra.Lattice.Structures.html#2829" id="2920">IsBoundedJoinSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2910" id="2945">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2912" id="2947">⊥</a><a class="Symbol" id="2948">)</a>
  <a class="Keyword" id="2952">where</a>

  <a class="Keyword" id="2961">open</a> <a class="Module" href="Algebra.Lattice.Structures.html#2191" id="2966">IsBoundedSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#2916" id="2987">L</a> <a class="Keyword" id="2989">public</a>
    <a class="Keyword" id="3000">using</a> <a class="Symbol" id="3006">(</a><a class="Function" href="Algebra.Structures.html#4847" id="3007">identity</a><a class="Symbol" id="3015">;</a> <a class="Function" href="Algebra.Structures.html#4916" id="3017">identityˡ</a><a class="Symbol" id="3026">;</a> <a class="Function" href="Algebra.Structures.html#4977" id="3028">identityʳ</a><a class="Symbol" id="3037">)</a>
    <a class="Keyword" id="3043">renaming</a> <a class="Symbol" id="3052">(</a><a class="Function" href="Algebra.Lattice.Structures.html#2337" id="3053">isSemilattice</a> <a class="Symbol" id="3067">to</a> <a class="Function" id="3070">isJoinSemilattice</a><a class="Symbol" id="3087">)</a>

  <a class="Keyword" id="3092">open</a> <a class="Module" href="Algebra.Lattice.Structures.html#1745" id="3097">IsJoinSemilattice</a> <a class="Function" href="Algebra.Lattice.Structures.html#3070" id="3115">isJoinSemilattice</a> <a class="Keyword" id="3133">public</a>

<a class="Comment" id="3141">------------------------------------------------------------------------</a>
<a class="Comment" id="3214">-- Structures with 2 binary operations</a>

<a class="Comment" id="3254">-- Note that `IsLattice` is not defined in terms of `IsMeetSemilattice`</a>
<a class="Comment" id="3326">-- and `IsJoinSemilattice` for two reasons:</a>
<a class="Comment" id="3370">--   1) it would result in a structure with two *different* proofs that</a>
<a class="Comment" id="3442">--   the equality relation `≈` is an equivalence relation.</a>
<a class="Comment" id="3501">--   2) the idempotence laws of ∨ and ∧ can be derived from the</a>
<a class="Comment" id="3565">--   absorption laws, which makes the corresponding "idem" fields</a>
<a class="Comment" id="3631">--   redundant.</a>
<a class="Comment" id="3647">--</a>
<a class="Comment" id="3650">-- It is possible to construct the `IsLattice` record from</a>
<a class="Comment" id="3709">-- `IsMeetSemilattice` and `IsJoinSemilattice` via the `IsLattice₂`</a>
<a class="Comment" id="3777">-- record found in `Algebra.Lattice.Structures.Biased`.</a>
<a class="Comment" id="3833">--</a>
<a class="Comment" id="3836">-- The derived idempotence laws are stated and proved in</a>
<a class="Comment" id="3893">-- `Algebra.Lattice.Properties.Lattice` along with the fact that every</a>
<a class="Comment" id="3964">-- lattice consists of two semilattices.</a>

<a class="Keyword" id="4006">record</a> <a id="IsLattice"></a><a class="Record" href="Algebra.Lattice.Structures.html#4013" id="4013">IsLattice</a> <a class="Symbol" id="4023">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#4024" id="4024">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4026" id="4026">∧</a> <a class="Symbol" id="4028">:</a> <a class="Function" href="Algebra.Core.html#527" id="4030">Op₂</a> <a class="Bound" href="Algebra.Lattice.Structures.html#886" id="4034">A</a><a class="Symbol" id="4035">)</a> <a class="Symbol" id="4037">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="4039">Set</a> <a class="Symbol" id="4043">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#880" id="4044">a</a> <a class="Primitive Operator" href="Agda.Primitive.html#961" id="4046">⊔</a> <a class="Bound" href="Algebra.Lattice.Structures.html#882" id="4048">ℓ</a><a class="Symbol" id="4049">)</a> <a class="Keyword" id="4051">where</a>
  <a class="Keyword" id="4059">field</a>
    <a id="IsLattice.isEquivalence"></a><a class="Field" href="Algebra.Lattice.Structures.html#4069" id="4069">isEquivalence</a> <a class="Symbol" id="4083">:</a> <a class="Record" href="Relation.Binary.Structures.html#1595" id="4085">IsEquivalence</a> <a class="Bound Operator" href="Algebra.Lattice.Structures.html#923" id="4099">_≈_</a>
    <a id="IsLattice.∨-comm"></a><a class="Field" href="Algebra.Lattice.Structures.html#4107" id="4107">∨-comm</a>        <a class="Symbol" id="4121">:</a> <a class="Function" href="Algebra.Definitions.html#1643" id="4123">Commutative</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4024" id="4135">∨</a>
    <a id="IsLattice.∨-assoc"></a><a class="Field" href="Algebra.Lattice.Structures.html#4141" id="4141">∨-assoc</a>       <a class="Symbol" id="4155">:</a> <a class="Function" href="Algebra.Definitions.html#1556" id="4157">Associative</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4024" id="4169">∨</a>
    <a id="IsLattice.∨-cong"></a><a class="Field" href="Algebra.Lattice.Structures.html#4175" id="4175">∨-cong</a>        <a class="Symbol" id="4189">:</a> <a class="Function" href="Algebra.Definitions.html#1336" id="4191">Congruent₂</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4024" id="4202">∨</a>
    <a id="IsLattice.∧-comm"></a><a class="Field" href="Algebra.Lattice.Structures.html#4208" id="4208">∧-comm</a>        <a class="Symbol" id="4222">:</a> <a class="Function" href="Algebra.Definitions.html#1643" id="4224">Commutative</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4026" id="4236">∧</a>
    <a id="IsLattice.∧-assoc"></a><a class="Field" href="Algebra.Lattice.Structures.html#4242" id="4242">∧-assoc</a>       <a class="Symbol" id="4256">:</a> <a class="Function" href="Algebra.Definitions.html#1556" id="4258">Associative</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4026" id="4270">∧</a>
    <a id="IsLattice.∧-cong"></a><a class="Field" href="Algebra.Lattice.Structures.html#4276" id="4276">∧-cong</a>        <a class="Symbol" id="4290">:</a> <a class="Function" href="Algebra.Definitions.html#1336" id="4292">Congruent₂</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4026" id="4303">∧</a>
    <a id="IsLattice.absorptive"></a><a class="Field" href="Algebra.Lattice.Structures.html#4309" id="4309">absorptive</a>    <a class="Symbol" id="4323">:</a> <a class="Function" href="Algebra.Definitions.html#4125" id="4325">Absorptive</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4024" id="4336">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4026" id="4338">∧</a>

  <a class="Keyword" id="4343">open</a> <a class="Module" href="Relation.Binary.Structures.html#1595" id="4348">IsEquivalence</a> <a class="Field" href="Algebra.Lattice.Structures.html#4069" id="4362">isEquivalence</a> <a class="Keyword" id="4376">public</a>

  <a id="IsLattice.∨-absorbs-∧"></a><a class="Function" href="Algebra.Lattice.Structures.html#4386" id="4386">∨-absorbs-∧</a> <a class="Symbol" id="4398">:</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4024" id="4400">∨</a> <a class="Function Operator" href="Algebra.Definitions.html#4046" id="4402">Absorbs</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4026" id="4410">∧</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#4386" id="4414">∨-absorbs-∧</a> <a class="Symbol" id="4426">=</a> <a class="Field" href="Data.Product.Base.html#636" id="4428">proj₁</a> <a class="Field" href="Algebra.Lattice.Structures.html#4309" id="4434">absorptive</a>

  <a id="IsLattice.∧-absorbs-∨"></a><a class="Function" href="Algebra.Lattice.Structures.html#4448" id="4448">∧-absorbs-∨</a> <a class="Symbol" id="4460">:</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4026" id="4462">∧</a> <a class="Function Operator" href="Algebra.Definitions.html#4046" id="4464">Absorbs</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4024" id="4472">∨</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#4448" id="4476">∧-absorbs-∨</a> <a class="Symbol" id="4488">=</a> <a class="Field" href="Data.Product.Base.html#650" id="4490">proj₂</a> <a class="Field" href="Algebra.Lattice.Structures.html#4309" id="4496">absorptive</a>

  <a id="IsLattice.∧-congˡ"></a><a class="Function" href="Algebra.Lattice.Structures.html#4510" id="4510">∧-congˡ</a> <a class="Symbol" id="4518">:</a> <a class="Function" href="Algebra.Definitions.html#1400" id="4520">LeftCongruent</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4026" id="4534">∧</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#4510" id="4538">∧-congˡ</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4546" id="4546">y≈z</a> <a class="Symbol" id="4550">=</a> <a class="Field" href="Algebra.Lattice.Structures.html#4276" id="4552">∧-cong</a> <a class="Function" href="Relation.Binary.Structures.html#1641" id="4559">refl</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4546" id="4564">y≈z</a>

  <a id="IsLattice.∧-congʳ"></a><a class="Function" href="Algebra.Lattice.Structures.html#4571" id="4571">∧-congʳ</a> <a class="Symbol" id="4579">:</a> <a class="Function" href="Algebra.Definitions.html#1477" id="4581">RightCongruent</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4026" id="4596">∧</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#4571" id="4600">∧-congʳ</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4608" id="4608">y≈z</a> <a class="Symbol" id="4612">=</a> <a class="Field" href="Algebra.Lattice.Structures.html#4276" id="4614">∧-cong</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4608" id="4621">y≈z</a> <a class="Function" href="Relation.Binary.Structures.html#1641" id="4625">refl</a>

  <a id="IsLattice.∨-congˡ"></a><a class="Function" href="Algebra.Lattice.Structures.html#4633" id="4633">∨-congˡ</a> <a class="Symbol" id="4641">:</a> <a class="Function" href="Algebra.Definitions.html#1400" id="4643">LeftCongruent</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4024" id="4657">∨</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#4633" id="4661">∨-congˡ</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4669" id="4669">y≈z</a> <a class="Symbol" id="4673">=</a> <a class="Field" href="Algebra.Lattice.Structures.html#4175" id="4675">∨-cong</a> <a class="Function" href="Relation.Binary.Structures.html#1641" id="4682">refl</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4669" id="4687">y≈z</a>

  <a id="IsLattice.∨-congʳ"></a><a class="Function" href="Algebra.Lattice.Structures.html#4694" id="4694">∨-congʳ</a> <a class="Symbol" id="4702">:</a> <a class="Function" href="Algebra.Definitions.html#1477" id="4704">RightCongruent</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4024" id="4719">∨</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#4694" id="4723">∨-congʳ</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4731" id="4731">y≈z</a> <a class="Symbol" id="4735">=</a> <a class="Field" href="Algebra.Lattice.Structures.html#4175" id="4737">∨-cong</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4731" id="4744">y≈z</a> <a class="Function" href="Relation.Binary.Structures.html#1641" id="4748">refl</a>


<a class="Keyword" id="4755">record</a> <a id="IsDistributiveLattice"></a><a class="Record" href="Algebra.Lattice.Structures.html#4762" id="4762">IsDistributiveLattice</a> <a class="Symbol" id="4784">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#4785" id="4785">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4787" id="4787">∧</a> <a class="Symbol" id="4789">:</a> <a class="Function" href="Algebra.Core.html#527" id="4791">Op₂</a> <a class="Bound" href="Algebra.Lattice.Structures.html#886" id="4795">A</a><a class="Symbol" id="4796">)</a> <a class="Symbol" id="4798">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="4800">Set</a> <a class="Symbol" id="4804">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#880" id="4805">a</a> <a class="Primitive Operator" href="Agda.Primitive.html#961" id="4807">⊔</a> <a class="Bound" href="Algebra.Lattice.Structures.html#882" id="4809">ℓ</a><a class="Symbol" id="4810">)</a> <a class="Keyword" id="4812">where</a>
  <a class="Keyword" id="4820">field</a>
    <a id="IsDistributiveLattice.isLattice"></a><a class="Field" href="Algebra.Lattice.Structures.html#4830" id="4830">isLattice</a>   <a class="Symbol" id="4842">:</a> <a class="Record" href="Algebra.Lattice.Structures.html#4013" id="4844">IsLattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4785" id="4854">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4787" id="4856">∧</a>
    <a id="IsDistributiveLattice.∨-distrib-∧"></a><a class="Field" href="Algebra.Lattice.Structures.html#4862" id="4862">∨-distrib-∧</a> <a class="Symbol" id="4874">:</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4785" id="4876">∨</a> <a class="Function Operator" href="Algebra.Definitions.html#3466" id="4878">DistributesOver</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4787" id="4894">∧</a>
    <a id="IsDistributiveLattice.∧-distrib-∨"></a><a class="Field" href="Algebra.Lattice.Structures.html#4900" id="4900">∧-distrib-∨</a> <a class="Symbol" id="4912">:</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4787" id="4914">∧</a> <a class="Function Operator" href="Algebra.Definitions.html#3466" id="4916">DistributesOver</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4785" id="4932">∨</a>

  <a class="Keyword" id="4937">open</a> <a class="Module" href="Algebra.Lattice.Structures.html#4013" id="4942">IsLattice</a> <a class="Field" href="Algebra.Lattice.Structures.html#4830" id="4952">isLattice</a> <a class="Keyword" id="4962">public</a>

  <a id="IsDistributiveLattice.∨-distribˡ-∧"></a><a class="Function" href="Algebra.Lattice.Structures.html#4972" id="4972">∨-distribˡ-∧</a> <a class="Symbol" id="4985">:</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4785" id="4987">∨</a> <a class="Function Operator" href="Algebra.Definitions.html#3228" id="4989">DistributesOverˡ</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4787" id="5006">∧</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#4972" id="5010">∨-distribˡ-∧</a> <a class="Symbol" id="5023">=</a> <a class="Field" href="Data.Product.Base.html#636" id="5025">proj₁</a> <a class="Field" href="Algebra.Lattice.Structures.html#4862" id="5031">∨-distrib-∧</a>

  <a id="IsDistributiveLattice.∨-distribʳ-∧"></a><a class="Function" href="Algebra.Lattice.Structures.html#5046" id="5046">∨-distribʳ-∧</a> <a class="Symbol" id="5059">:</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4785" id="5061">∨</a> <a class="Function Operator" href="Algebra.Definitions.html#3347" id="5063">DistributesOverʳ</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4787" id="5080">∧</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#5046" id="5084">∨-distribʳ-∧</a> <a class="Symbol" id="5097">=</a> <a class="Field" href="Data.Product.Base.html#650" id="5099">proj₂</a> <a class="Field" href="Algebra.Lattice.Structures.html#4862" id="5105">∨-distrib-∧</a>

  <a id="IsDistributiveLattice.∧-distribˡ-∨"></a><a class="Function" href="Algebra.Lattice.Structures.html#5120" id="5120">∧-distribˡ-∨</a> <a class="Symbol" id="5133">:</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4787" id="5135">∧</a> <a class="Function Operator" href="Algebra.Definitions.html#3228" id="5137">DistributesOverˡ</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4785" id="5154">∨</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#5120" id="5158">∧-distribˡ-∨</a> <a class="Symbol" id="5171">=</a> <a class="Field" href="Data.Product.Base.html#636" id="5173">proj₁</a> <a class="Field" href="Algebra.Lattice.Structures.html#4900" id="5179">∧-distrib-∨</a>

  <a id="IsDistributiveLattice.∧-distribʳ-∨"></a><a class="Function" href="Algebra.Lattice.Structures.html#5194" id="5194">∧-distribʳ-∨</a> <a class="Symbol" id="5207">:</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4787" id="5209">∧</a> <a class="Function Operator" href="Algebra.Definitions.html#3347" id="5211">DistributesOverʳ</a> <a class="Bound" href="Algebra.Lattice.Structures.html#4785" id="5228">∨</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#5194" id="5232">∧-distribʳ-∨</a> <a class="Symbol" id="5245">=</a> <a class="Field" href="Data.Product.Base.html#650" id="5247">proj₂</a> <a class="Field" href="Algebra.Lattice.Structures.html#4900" id="5253">∧-distrib-∨</a>

<a class="Comment" id="5266">------------------------------------------------------------------------</a>
<a class="Comment" id="5339">-- Structures with 2 binary ops, 1 unary op and 2 elements.</a>

<a class="Keyword" id="5400">record</a> <a id="IsBooleanAlgebra"></a><a class="Record" href="Algebra.Lattice.Structures.html#5407" id="5407">IsBooleanAlgebra</a> <a class="Symbol" id="5424">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#5425" id="5425">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5427" id="5427">∧</a> <a class="Symbol" id="5429">:</a> <a class="Function" href="Algebra.Core.html#527" id="5431">Op₂</a> <a class="Bound" href="Algebra.Lattice.Structures.html#886" id="5435">A</a><a class="Symbol" id="5436">)</a> <a class="Symbol" id="5438">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#5439" id="5439">¬</a> <a class="Symbol" id="5441">:</a> <a class="Function" href="Algebra.Core.html#484" id="5443">Op₁</a> <a class="Bound" href="Algebra.Lattice.Structures.html#886" id="5447">A</a><a class="Symbol" id="5448">)</a> <a class="Symbol" id="5450">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#5451" id="5451">⊤</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5453" id="5453">⊥</a> <a class="Symbol" id="5455">:</a> <a class="Bound" href="Algebra.Lattice.Structures.html#886" id="5457">A</a><a class="Symbol" id="5458">)</a> <a class="Symbol" id="5460">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="5462">Set</a> <a class="Symbol" id="5466">(</a><a class="Bound" href="Algebra.Lattice.Structures.html#880" id="5467">a</a> <a class="Primitive Operator" href="Agda.Primitive.html#961" id="5469">⊔</a> <a class="Bound" href="Algebra.Lattice.Structures.html#882" id="5471">ℓ</a><a class="Symbol" id="5472">)</a>
  <a class="Keyword" id="5476">where</a>

  <a class="Keyword" id="5485">field</a>
    <a id="IsBooleanAlgebra.isDistributiveLattice"></a><a class="Field" href="Algebra.Lattice.Structures.html#5495" id="5495">isDistributiveLattice</a> <a class="Symbol" id="5517">:</a> <a class="Record" href="Algebra.Lattice.Structures.html#4762" id="5519">IsDistributiveLattice</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5425" id="5541">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5427" id="5543">∧</a>
    <a id="IsBooleanAlgebra.∨-complement"></a><a class="Field" href="Algebra.Lattice.Structures.html#5549" id="5549">∨-complement</a>          <a class="Symbol" id="5571">:</a> <a class="Function" href="Algebra.Definitions.html#2330" id="5573">Inverse</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5451" id="5581">⊤</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5439" id="5583">¬</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5425" id="5585">∨</a>
    <a id="IsBooleanAlgebra.∧-complement"></a><a class="Field" href="Algebra.Lattice.Structures.html#5591" id="5591">∧-complement</a>          <a class="Symbol" id="5613">:</a> <a class="Function" href="Algebra.Definitions.html#2330" id="5615">Inverse</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5453" id="5623">⊥</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5439" id="5625">¬</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5427" id="5627">∧</a>
    <a id="IsBooleanAlgebra.¬-cong"></a><a class="Field" href="Algebra.Lattice.Structures.html#5633" id="5633">¬-cong</a>                <a class="Symbol" id="5655">:</a> <a class="Function" href="Algebra.Definitions.html#1276" id="5657">Congruent₁</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5439" id="5668">¬</a>

  <a class="Keyword" id="5673">open</a> <a class="Module" href="Algebra.Lattice.Structures.html#4762" id="5678">IsDistributiveLattice</a> <a class="Field" href="Algebra.Lattice.Structures.html#5495" id="5700">isDistributiveLattice</a> <a class="Keyword" id="5722">public</a>

  <a id="IsBooleanAlgebra.∨-complementˡ"></a><a class="Function" href="Algebra.Lattice.Structures.html#5732" id="5732">∨-complementˡ</a> <a class="Symbol" id="5746">:</a> <a class="Function" href="Algebra.Definitions.html#2152" id="5748">LeftInverse</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5451" id="5760">⊤</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5439" id="5762">¬</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5425" id="5764">∨</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#5732" id="5768">∨-complementˡ</a> <a class="Symbol" id="5782">=</a> <a class="Field" href="Data.Product.Base.html#636" id="5784">proj₁</a> <a class="Field" href="Algebra.Lattice.Structures.html#5549" id="5790">∨-complement</a>

  <a id="IsBooleanAlgebra.∨-complementʳ"></a><a class="Function" href="Algebra.Lattice.Structures.html#5806" id="5806">∨-complementʳ</a> <a class="Symbol" id="5820">:</a> <a class="Function" href="Algebra.Definitions.html#2240" id="5822">RightInverse</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5451" id="5835">⊤</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5439" id="5837">¬</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5425" id="5839">∨</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#5806" id="5843">∨-complementʳ</a> <a class="Symbol" id="5857">=</a> <a class="Field" href="Data.Product.Base.html#650" id="5859">proj₂</a> <a class="Field" href="Algebra.Lattice.Structures.html#5549" id="5865">∨-complement</a>

  <a id="IsBooleanAlgebra.∧-complementˡ"></a><a class="Function" href="Algebra.Lattice.Structures.html#5881" id="5881">∧-complementˡ</a> <a class="Symbol" id="5895">:</a> <a class="Function" href="Algebra.Definitions.html#2152" id="5897">LeftInverse</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5453" id="5909">⊥</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5439" id="5911">¬</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5427" id="5913">∧</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#5881" id="5917">∧-complementˡ</a> <a class="Symbol" id="5931">=</a> <a class="Field" href="Data.Product.Base.html#636" id="5933">proj₁</a> <a class="Field" href="Algebra.Lattice.Structures.html#5591" id="5939">∧-complement</a>

  <a id="IsBooleanAlgebra.∧-complementʳ"></a><a class="Function" href="Algebra.Lattice.Structures.html#5955" id="5955">∧-complementʳ</a> <a class="Symbol" id="5969">:</a> <a class="Function" href="Algebra.Definitions.html#2240" id="5971">RightInverse</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5453" id="5984">⊥</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5439" id="5986">¬</a> <a class="Bound" href="Algebra.Lattice.Structures.html#5427" id="5988">∧</a>
  <a class="Function" href="Algebra.Lattice.Structures.html#5955" id="5992">∧-complementʳ</a> <a class="Symbol" id="6006">=</a> <a class="Field" href="Data.Product.Base.html#650" id="6008">proj₂</a> <a class="Field" href="Algebra.Lattice.Structures.html#5591" id="6014">∧-complement</a>
</pre></div></body></html>