<!DOCTYPE HTML>

<html><head><meta charset="utf-8"/><title>Relation.Binary.Reasoning.Base.Partial</title><link href="Agda.css" rel="stylesheet"/><script src="highlight-hover.js" type="text/javascript"></script></head><body><div class="navbar"><a class="active" href="README.html">Everything</a><a href="index.html" style="float:right">Home</a></div><div class="main"><pre class="Agda"><a class="Comment" id="1">------------------------------------------------------------------------</a>
<a class="Comment" id="74">-- The Agda standard library</a>
<a class="Comment" id="103">--</a>
<a class="Comment" id="106">-- The basic code for equational reasoning with a non-reflexive relation</a>
<a class="Comment" id="179">------------------------------------------------------------------------</a>

<a class="Symbol" id="253">{-#</a> <a class="Keyword" id="257">OPTIONS</a> <a class="Pragma" id="265">--cubical-compatible</a> <a class="Pragma" id="286">--safe</a> <a class="Symbol" id="293">#-}</a>

<a class="Keyword" id="298">open</a> <a class="Keyword" id="303">import</a> <a class="Module" href="Function.Base.html" id="310">Function.Base</a> <a class="Keyword" id="324">using</a> <a class="Symbol" id="330">(</a><a class="Function Operator" href="Function.Base.html#4061" id="331">case_of_</a><a class="Symbol" id="339">)</a>
<a class="Keyword" id="341">open</a> <a class="Keyword" id="346">import</a> <a class="Module" href="Level.html" id="353">Level</a> <a class="Keyword" id="359">using</a> <a class="Symbol" id="365">(</a><a class="Primitive Operator" href="Agda.Primitive.html#961" id="366">_⊔_</a><a class="Symbol" id="369">)</a>
<a class="Keyword" id="371">open</a> <a class="Keyword" id="376">import</a> <a class="Module" href="Relation.Binary.Core.html" id="383">Relation.Binary.Core</a> <a class="Keyword" id="404">using</a> <a class="Symbol" id="410">(</a><a class="Function" href="Relation.Binary.Core.html#896" id="411">Rel</a><a class="Symbol" id="414">)</a>
<a class="Keyword" id="416">open</a> <a class="Keyword" id="421">import</a> <a class="Module" href="Relation.Binary.Definitions.html" id="428">Relation.Binary.Definitions</a> <a class="Keyword" id="456">using</a> <a class="Symbol" id="462">(</a><a class="Function" href="Relation.Binary.Definitions.html#2030" id="463">Transitive</a><a class="Symbol" id="473">;</a> <a class="Function" href="Relation.Binary.Definitions.html#1601" id="475">Trans</a><a class="Symbol" id="480">;</a> <a class="Function" href="Relation.Binary.Definitions.html#1355" id="482">Reflexive</a><a class="Symbol" id="491">)</a>
<a class="Keyword" id="493">open</a> <a class="Keyword" id="498">import</a> <a class="Module" href="Relation.Nullary.Decidable.html" id="505">Relation.Nullary.Decidable</a> <a class="Keyword" id="532">using</a> <a class="Symbol" id="538">(</a><a class="Record" href="Relation.Nullary.Decidable.Core.html#1966" id="539">Dec</a><a class="Symbol" id="542">;</a> <a class="InductiveConstructor" href="Relation.Nullary.Decidable.Core.html#2099" id="544">yes</a><a class="Symbol" id="547">;</a> <a class="InductiveConstructor" href="Relation.Nullary.Decidable.Core.html#2136" id="549">no</a><a class="Symbol" id="551">)</a>
<a class="Keyword" id="553">open</a> <a class="Keyword" id="558">import</a> <a class="Module" href="Relation.Binary.PropositionalEquality.Core.html" id="565">Relation.Binary.PropositionalEquality.Core</a> <a class="Symbol" id="608">as</a> <a class="Module" id="611">≡</a> <a class="Keyword" id="613">using</a> <a class="Symbol" id="619">(</a><a class="Datatype Operator" href="Agda.Builtin.Equality.html#150" id="620">_≡_</a><a class="Symbol" id="623">)</a>
<a class="Keyword" id="625">open</a> <a class="Keyword" id="630">import</a> <a class="Module" href="Relation.Binary.Reasoning.Syntax.html" id="637">Relation.Binary.Reasoning.Syntax</a>


<a class="Keyword" id="672">module</a> <a class="Module" href="Relation.Binary.Reasoning.Base.Partial.html" id="679">Relation.Binary.Reasoning.Base.Partial</a>
  <a class="Symbol" id="720">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#721" id="721">a</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#723" id="723">ℓ</a><a class="Symbol" id="724">}</a> <a class="Symbol" id="726">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#727" id="727">A</a> <a class="Symbol" id="729">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="731">Set</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#721" id="735">a</a><a class="Symbol" id="736">}</a> <a class="Symbol" id="738">(</a><a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Partial.html#739" id="739">_∼_</a> <a class="Symbol" id="743">:</a> <a class="Function" href="Relation.Binary.Core.html#896" id="745">Rel</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#727" id="749">A</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#723" id="751">ℓ</a><a class="Symbol" id="752">)</a> <a class="Symbol" id="754">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#755" id="755">trans</a> <a class="Symbol" id="761">:</a> <a class="Function" href="Relation.Binary.Definitions.html#2030" id="763">Transitive</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Partial.html#739" id="774">_∼_</a><a class="Symbol" id="777">)</a>
  <a class="Keyword" id="781">where</a>

<a class="Comment" id="788">------------------------------------------------------------------------</a>
<a class="Comment" id="861">-- Definition of "related to"</a>

<a class="Comment" id="892">-- This type allows us to track whether reasoning steps</a>
<a class="Comment" id="948">-- include _∼_ or not.</a>

<a class="Keyword" id="972">infix</a>  <a class="Number" id="979">4</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="981">_IsRelatedTo_</a>

<a class="Keyword" id="996">data</a> <a id="_IsRelatedTo_"></a><a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="1001">_IsRelatedTo_</a> <a class="Symbol" id="1015">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#727" id="1017">A</a> <a class="Symbol" id="1019">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#727" id="1021">A</a> <a class="Symbol" id="1023">→</a> <a class="Primitive" href="Agda.Primitive.html#388" id="1025">Set</a> <a class="Symbol" id="1029">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#721" id="1030">a</a> <a class="Primitive Operator" href="Agda.Primitive.html#961" id="1032">⊔</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#723" id="1034">ℓ</a><a class="Symbol" id="1035">)</a> <a class="Keyword" id="1037">where</a>
  <a id="_IsRelatedTo_.reflexive"></a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1045" id="1045">reflexive</a> <a class="Symbol" id="1055">:</a> <a class="Symbol" id="1057">∀</a> <a class="Symbol" id="1059">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1060" id="1060">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1062" id="1062">y</a><a class="Symbol" id="1063">}</a> <a class="Symbol" id="1065">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1060" id="1067">x</a> <a class="Datatype Operator" href="Agda.Builtin.Equality.html#150" id="1069">≡</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1062" id="1071">y</a> <a class="Symbol" id="1073">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1060" id="1075">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="1077">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1062" id="1089">y</a>
  <a id="_IsRelatedTo_.relTo"></a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1093" id="1093">relTo</a>     <a class="Symbol" id="1103">:</a> <a class="Symbol" id="1105">∀</a> <a class="Symbol" id="1107">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1108" id="1108">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1110" id="1110">y</a><a class="Symbol" id="1111">}</a> <a class="Symbol" id="1113">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1114" id="1114">x∼y</a> <a class="Symbol" id="1118">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1108" id="1120">x</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Partial.html#739" id="1122">∼</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1110" id="1124">y</a><a class="Symbol" id="1125">)</a> <a class="Symbol" id="1127">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1108" id="1129">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="1131">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1110" id="1143">y</a>

<a id="≡-go"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1146" id="1146">≡-go</a> <a class="Symbol" id="1151">:</a> <a class="Function" href="Relation.Binary.Definitions.html#1601" id="1153">Trans</a> <a class="Datatype Operator" href="Agda.Builtin.Equality.html#150" id="1159">_≡_</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="1163">_IsRelatedTo_</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="1177">_IsRelatedTo_</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1146" id="1191">≡-go</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1196" id="1196">x≡y</a> <a class="Symbol" id="1200">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1045" id="1201">reflexive</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1211" id="1211">y≡z</a><a class="Symbol" id="1214">)</a> <a class="Symbol" id="1216">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1045" id="1218">reflexive</a> <a class="Symbol" id="1228">(</a><a class="Function Operator" href="Function.Base.html#4061" id="1229">case</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1196" id="1234">x≡y</a> <a class="Function Operator" href="Function.Base.html#4061" id="1238">of</a> <a class="Symbol" id="1241">λ</a> <a class="Keyword" id="1243">where</a> <a class="InductiveConstructor" href="Agda.Builtin.Equality.html#207" id="1249">≡.refl</a> <a class="Symbol" id="1256">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1211" id="1258">y≡z</a><a class="Symbol" id="1261">)</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1146" id="1263">≡-go</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1268" id="1268">x≡y</a> <a class="Symbol" id="1272">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1093" id="1273">relTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1279" id="1279">y∼z</a><a class="Symbol" id="1282">)</a>     <a class="Symbol" id="1288">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1093" id="1290">relTo</a> <a class="Symbol" id="1296">(</a><a class="Function Operator" href="Function.Base.html#4061" id="1297">case</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1268" id="1302">x≡y</a> <a class="Function Operator" href="Function.Base.html#4061" id="1306">of</a> <a class="Symbol" id="1309">λ</a> <a class="Keyword" id="1311">where</a> <a class="InductiveConstructor" href="Agda.Builtin.Equality.html#207" id="1317">≡.refl</a> <a class="Symbol" id="1324">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1279" id="1326">y∼z</a><a class="Symbol" id="1329">)</a>

<a id="∼-go"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1332" id="1332">∼-go</a> <a class="Symbol" id="1337">:</a> <a class="Function" href="Relation.Binary.Definitions.html#1601" id="1339">Trans</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Partial.html#739" id="1345">_∼_</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="1349">_IsRelatedTo_</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="1363">_IsRelatedTo_</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1332" id="1377">∼-go</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1382" id="1382">x∼y</a> <a class="Symbol" id="1386">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1045" id="1387">reflexive</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1397" id="1397">y≡z</a><a class="Symbol" id="1400">)</a> <a class="Symbol" id="1402">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1093" id="1404">relTo</a> <a class="Symbol" id="1410">(</a><a class="Function Operator" href="Function.Base.html#4061" id="1411">case</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1397" id="1416">y≡z</a> <a class="Function Operator" href="Function.Base.html#4061" id="1420">of</a> <a class="Symbol" id="1423">λ</a> <a class="Keyword" id="1425">where</a> <a class="InductiveConstructor" href="Agda.Builtin.Equality.html#207" id="1431">≡.refl</a> <a class="Symbol" id="1438">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1382" id="1440">x∼y</a><a class="Symbol" id="1443">)</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1332" id="1445">∼-go</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1450" id="1450">x∼y</a> <a class="Symbol" id="1454">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1093" id="1455">relTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1461" id="1461">y∼z</a><a class="Symbol" id="1464">)</a>     <a class="Symbol" id="1470">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1093" id="1472">relTo</a> <a class="Symbol" id="1478">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#755" id="1479">trans</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1450" id="1485">x∼y</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1461" id="1489">y∼z</a><a class="Symbol" id="1492">)</a>

<a id="stop"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1495" id="1495">stop</a> <a class="Symbol" id="1500">:</a> <a class="Function" href="Relation.Binary.Definitions.html#1355" id="1502">Reflexive</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="1512">_IsRelatedTo_</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1495" id="1526">stop</a> <a class="Symbol" id="1531">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1045" id="1533">reflexive</a> <a class="InductiveConstructor" href="Agda.Builtin.Equality.html#207" id="1543">≡.refl</a>

<a class="Comment" id="1551">------------------------------------------------------------------------</a>
<a class="Comment" id="1624">-- Types that are used to ensure that the final relation proved by the</a>
<a class="Comment" id="1695">-- chain of reasoning can be converted into the required relation.</a>

<a class="Keyword" id="1763">data</a> <a id="IsRelTo"></a><a class="Datatype" href="Relation.Binary.Reasoning.Base.Partial.html#1768" id="1768">IsRelTo</a> <a class="Symbol" id="1776">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1777" id="1777">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1779" id="1779">y</a><a class="Symbol" id="1780">}</a> <a class="Symbol" id="1782">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1777" id="1784">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="1786">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1779" id="1798">y</a> <a class="Symbol" id="1800">→</a> <a class="Primitive" href="Agda.Primitive.html#388" id="1802">Set</a> <a class="Symbol" id="1806">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#721" id="1807">a</a> <a class="Primitive Operator" href="Agda.Primitive.html#961" id="1809">⊔</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#723" id="1811">ℓ</a><a class="Symbol" id="1812">)</a> <a class="Keyword" id="1814">where</a>
  <a id="IsRelTo.isRelTo"></a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1822" id="1822">isRelTo</a> <a class="Symbol" id="1830">:</a> <a class="Symbol" id="1832">∀</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1834" id="1834">x∼y</a> <a class="Symbol" id="1838">→</a> <a class="Datatype" href="Relation.Binary.Reasoning.Base.Partial.html#1768" id="1840">IsRelTo</a> <a class="Symbol" id="1848">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1093" id="1849">relTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1834" id="1855">x∼y</a><a class="Symbol" id="1858">)</a>

<a id="IsRelTo?"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1861" id="1861">IsRelTo?</a> <a class="Symbol" id="1870">:</a> <a class="Symbol" id="1872">∀</a> <a class="Symbol" id="1874">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1875" id="1875">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1877" id="1877">y</a><a class="Symbol" id="1878">}</a> <a class="Symbol" id="1880">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1881" id="1881">x∼y</a> <a class="Symbol" id="1885">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1875" id="1887">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="1889">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1877" id="1901">y</a><a class="Symbol" id="1902">)</a> <a class="Symbol" id="1904">→</a> <a class="Record" href="Relation.Nullary.Decidable.Core.html#1966" id="1906">Dec</a> <a class="Symbol" id="1910">(</a><a class="Datatype" href="Relation.Binary.Reasoning.Base.Partial.html#1768" id="1911">IsRelTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1881" id="1919">x∼y</a><a class="Symbol" id="1922">)</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1861" id="1924">IsRelTo?</a> <a class="Symbol" id="1933">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1093" id="1934">relTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1940" id="1940">x∼y</a><a class="Symbol" id="1943">)</a>   <a class="Symbol" id="1947">=</a> <a class="InductiveConstructor" href="Relation.Nullary.Decidable.Core.html#2099" id="1949">yes</a> <a class="Symbol" id="1953">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1822" id="1954">isRelTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#1940" id="1962">x∼y</a><a class="Symbol" id="1965">)</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1861" id="1967">IsRelTo?</a> <a class="Symbol" id="1976">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1045" id="1977">reflexive</a> <a class="Symbol" id="1987">_)</a> <a class="Symbol" id="1990">=</a> <a class="InductiveConstructor" href="Relation.Nullary.Decidable.Core.html#2136" id="1992">no</a> <a class="Symbol" id="1995">λ()</a>

<a id="extractRelTo"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#2000" id="2000">extractRelTo</a> <a class="Symbol" id="2013">:</a> <a class="Symbol" id="2015">∀</a> <a class="Symbol" id="2017">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2018" id="2018">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2020" id="2020">y</a><a class="Symbol" id="2021">}</a> <a class="Symbol" id="2023">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2024" id="2024">x∼y</a> <a class="Symbol" id="2028">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2018" id="2030">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="2032">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2020" id="2044">y</a><a class="Symbol" id="2045">}</a> <a class="Symbol" id="2047">→</a> <a class="Datatype" href="Relation.Binary.Reasoning.Base.Partial.html#1768" id="2049">IsRelTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2024" id="2057">x∼y</a> <a class="Symbol" id="2061">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2018" id="2063">x</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Partial.html#739" id="2065">∼</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2020" id="2067">y</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#2000" id="2069">extractRelTo</a> <a class="Symbol" id="2082">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1822" id="2083">isRelTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2091" id="2091">x∼y</a><a class="Symbol" id="2094">)</a> <a class="Symbol" id="2096">=</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2091" id="2098">x∼y</a>

<a id="relToSubRelation"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#2103" id="2103">relToSubRelation</a> <a class="Symbol" id="2120">:</a> <a class="Record" href="Relation.Binary.Reasoning.Syntax.html#1982" id="2122">SubRelation</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="2134">_IsRelatedTo_</a> <a class="Symbol" id="2148">_</a> <a class="Symbol" id="2150">_</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#2103" id="2152">relToSubRelation</a> <a class="Symbol" id="2169">=</a> <a class="Keyword" id="2171">record</a>
  <a class="Symbol" id="2180">{</a> <a class="Field" href="Relation.Binary.Reasoning.Syntax.html#2095" id="2182">IsS</a> <a class="Symbol" id="2186">=</a> <a class="Datatype" href="Relation.Binary.Reasoning.Base.Partial.html#1768" id="2188">IsRelTo</a>
  <a class="Symbol" id="2198">;</a> <a class="Field" href="Relation.Binary.Reasoning.Syntax.html#2120" id="2200">IsS?</a> <a class="Symbol" id="2205">=</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1861" id="2207">IsRelTo?</a>
  <a class="Symbol" id="2218">;</a> <a class="Field" href="Relation.Binary.Reasoning.Syntax.html#2163" id="2220">extract</a> <a class="Symbol" id="2228">=</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#2000" id="2230">extractRelTo</a>
  <a class="Symbol" id="2245">}</a>

<a class="Comment" id="2248">------------------------------------------------------------------------</a>
<a class="Comment" id="2321">-- Reasoning combinators</a>

<a class="Keyword" id="2347">open</a> <a class="Module" href="Relation.Binary.Reasoning.Syntax.html#2215" id="2352">begin-subrelation-syntax</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="2377">_IsRelatedTo_</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#2103" id="2391">relToSubRelation</a> <a class="Keyword" id="2408">public</a>
<a class="Keyword" id="2415">open</a> <a class="Module" href="Relation.Binary.Reasoning.Syntax.html#10945" id="2420">≡-syntax</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="2429">_IsRelatedTo_</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1146" id="2443">≡-go</a> <a class="Keyword" id="2448">public</a>
<a class="Keyword" id="2455">open</a> <a class="Module" href="Relation.Binary.Reasoning.Syntax.html#5234" id="2460">∼-syntax</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="2469">_IsRelatedTo_</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="2483">_IsRelatedTo_</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1332" id="2497">∼-go</a> <a class="Keyword" id="2502">public</a>
<a class="Keyword" id="2509">open</a> <a class="Module" href="Relation.Binary.Reasoning.Syntax.html#12264" id="2514">end-syntax</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="2525">_IsRelatedTo_</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Partial.html#1495" id="2539">stop</a> <a class="Keyword" id="2544">public</a>

<a class="Comment" id="2552">------------------------------------------------------------------------</a>
<a class="Comment" id="2625">-- DEPRECATED NAMES</a>
<a class="Comment" id="2645">------------------------------------------------------------------------</a>
<a class="Comment" id="2718">-- Please use the new names as continuing support for the old names is</a>
<a class="Comment" id="2789">-- not guaranteed.</a>

<a class="Comment" id="2809">-- Version 1.6</a>

<a class="Keyword" id="2825">infix</a>  <a class="Number" id="2832">3</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Partial.html#2841" id="2834">_∎⟨_⟩</a>

<a id="_∎⟨_⟩"></a><a class="Function Operator" href="Relation.Binary.Reasoning.Base.Partial.html#2841" id="2841">_∎⟨_⟩</a> <a class="Symbol" id="2847">:</a> <a class="Symbol" id="2849">∀</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2851" id="2851">x</a> <a class="Symbol" id="2853">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2851" id="2855">x</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Partial.html#739" id="2857">∼</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2851" id="2859">x</a> <a class="Symbol" id="2861">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2851" id="2863">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Partial.html#1001" id="2865">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2851" id="2877">x</a>
<a class="Symbol" id="2879">_</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Partial.html#2841" id="2881">∎⟨</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2884" id="2884">x∼x</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Partial.html#2841" id="2888">⟩</a> <a class="Symbol" id="2890">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Partial.html#1093" id="2892">relTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Partial.html#2884" id="2898">x∼x</a>
<a class="Symbol" id="2902">{-#</a> <a class="Keyword" id="2906">WARNING_ON_USAGE</a> <a class="Pragma" id="2923">_∎⟨_⟩</a>
<a class="String" id="2929">"Warning: _∎⟨_⟩ was deprecated in v1.6.
Please use _∎ instead if used in a chain, otherwise simply provide
the proof of reflexivity directly without using these combinators."</a>
<a class="Symbol" id="3104">#-}</a>
</pre></div></body></html>