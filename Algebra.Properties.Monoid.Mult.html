<!DOCTYPE HTML>

<html><head><meta charset="utf-8"/><title>Algebra.Properties.Monoid.Mult</title><link href="Agda.css" rel="stylesheet"/><script src="highlight-hover.js" type="text/javascript"></script></head><body><div class="navbar"><a class="active" href="README.html">Everything</a><a href="index.html" style="float:right">Home</a></div><div class="main"><pre class="Agda"><a class="Comment" id="1">------------------------------------------------------------------------</a>
<a class="Comment" id="74">-- The Agda standard library</a>
<a class="Comment" id="103">--</a>
<a class="Comment" id="106">-- Multiplication over a monoid (i.e. repeated addition)</a>
<a class="Comment" id="163">------------------------------------------------------------------------</a>

<a class="Symbol" id="237">{-#</a> <a class="Keyword" id="241">OPTIONS</a> <a class="Pragma" id="249">--cubical-compatible</a> <a class="Pragma" id="270">--safe</a> <a class="Symbol" id="277">#-}</a>

<a class="Keyword" id="282">open</a> <a class="Keyword" id="287">import</a> <a class="Module" href="Algebra.Bundles.html" id="294">Algebra.Bundles</a> <a class="Keyword" id="310">using</a> <a class="Symbol" id="316">(</a><a class="Record" href="Algebra.Bundles.html#6051" id="317">Monoid</a><a class="Symbol" id="323">)</a>
<a class="Keyword" id="325">open</a> <a class="Keyword" id="330">import</a> <a class="Module" href="Data.Nat.Base.html" id="337">Data.Nat.Base</a> <a class="Symbol" id="351">as</a> <a class="Module" id="354">ℕ</a> <a class="Keyword" id="356">using</a> <a class="Symbol" id="362">(</a><a class="Datatype" href="Agda.Builtin.Nat.html#203" id="363">ℕ</a><a class="Symbol" id="364">;</a> <a class="InductiveConstructor" href="Agda.Builtin.Nat.html#221" id="366">zero</a><a class="Symbol" id="370">;</a> <a class="InductiveConstructor" href="Agda.Builtin.Nat.html#234" id="372">suc</a><a class="Symbol" id="375">;</a> <a class="Record" href="Data.Nat.Base.html#3260" id="377">NonZero</a><a class="Symbol" id="384">)</a>
<a class="Keyword" id="386">open</a> <a class="Keyword" id="391">import</a> <a class="Module" href="Relation.Binary.Core.html" id="398">Relation.Binary.Core</a> <a class="Keyword" id="419">using</a> <a class="Symbol" id="425">(</a><a class="Function Operator" href="Relation.Binary.Core.html#1577" id="426">_Preserves_⟶_</a><a class="Symbol" id="439">;</a> <a class="Function Operator" href="Relation.Binary.Core.html#1703" id="441">_Preserves₂_⟶_⟶_</a><a class="Symbol" id="457">)</a>
<a class="Keyword" id="459">open</a> <a class="Keyword" id="464">import</a> <a class="Module" href="Relation.Binary.PropositionalEquality.Core.html" id="471">Relation.Binary.PropositionalEquality.Core</a> <a class="Symbol" id="514">as</a> <a class="Module" id="517">P</a> <a class="Keyword" id="519">using</a> <a class="Symbol" id="525">(</a><a class="Datatype Operator" href="Agda.Builtin.Equality.html#150" id="526">_≡_</a><a class="Symbol" id="529">)</a>

<a class="Keyword" id="532">module</a> <a class="Module" href="Algebra.Properties.Monoid.Mult.html" id="539">Algebra.Properties.Monoid.Mult</a> <a class="Symbol" id="570">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#571" id="571">a</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#573" id="573">ℓ</a><a class="Symbol" id="574">}</a> <a class="Symbol" id="576">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#577" id="577">M</a> <a class="Symbol" id="579">:</a> <a class="Record" href="Algebra.Bundles.html#6051" id="581">Monoid</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#571" id="588">a</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#573" id="590">ℓ</a><a class="Symbol" id="591">)</a> <a class="Keyword" id="593">where</a>

<a class="Comment" id="600">-- View of the monoid operator as addition</a>
<a class="Keyword" id="643">open</a> <a class="Module" href="Algebra.Bundles.html#6051" id="648">Monoid</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#577" id="655">M</a>
  <a class="Keyword" id="659">renaming</a>
  <a class="Symbol" id="670">(</a> <a class="Field Operator" href="Algebra.Bundles.html#6180" id="672">_∙_</a>       <a class="Symbol" id="682">to</a> <a class="Field Operator" id="685">_+_</a>
  <a class="Symbol" id="691">;</a> <a class="Function" href="Algebra.Structures.html#1315" id="693">∙-cong</a>    <a class="Symbol" id="703">to</a> <a class="Function" id="706">+-cong</a>
  <a class="Symbol" id="715">;</a> <a class="Function" href="Algebra.Structures.html#1526" id="717">∙-congʳ</a>   <a class="Symbol" id="727">to</a> <a class="Function" id="730">+-congʳ</a>
  <a class="Symbol" id="740">;</a> <a class="Function" href="Algebra.Structures.html#1465" id="742">∙-congˡ</a>   <a class="Symbol" id="752">to</a> <a class="Function" id="755">+-congˡ</a>
  <a class="Symbol" id="765">;</a> <a class="Function" href="Algebra.Structures.html#4136" id="767">identityˡ</a> <a class="Symbol" id="777">to</a> <a class="Function" id="780">+-identityˡ</a>
  <a class="Symbol" id="794">;</a> <a class="Function" href="Algebra.Structures.html#4197" id="796">identityʳ</a> <a class="Symbol" id="806">to</a> <a class="Function" id="809">+-identityʳ</a>
  <a class="Symbol" id="823">;</a> <a class="Function" href="Algebra.Structures.html#2977" id="825">assoc</a>     <a class="Symbol" id="835">to</a> <a class="Function" id="838">+-assoc</a>
  <a class="Symbol" id="848">;</a> <a class="Field" href="Algebra.Bundles.html#6207" id="850">ε</a>         <a class="Symbol" id="860">to</a> <a class="Field" id="863">0#</a>
  <a class="Symbol" id="868">)</a>

<a class="Keyword" id="871">open</a> <a class="Keyword" id="876">import</a> <a class="Module" href="Relation.Binary.Reasoning.Setoid.html" id="883">Relation.Binary.Reasoning.Setoid</a> <a class="Function" href="Algebra.Structures.html#1390" id="916">setoid</a>

<a class="Keyword" id="924">open</a> <a class="Keyword" id="929">import</a> <a class="Module" href="Algebra.Definitions.html" id="936">Algebra.Definitions</a> <a class="Field Operator" href="Algebra.Bundles.html#6151" id="956">_≈_</a>

<a class="Comment" id="961">------------------------------------------------------------------------</a>
<a class="Comment" id="1034">-- Definition</a>

<a class="Keyword" id="1049">open</a> <a class="Keyword" id="1054">import</a> <a class="Module" href="Algebra.Definitions.RawMonoid.html" id="1061">Algebra.Definitions.RawMonoid</a> <a class="Function" href="Algebra.Bundles.html#6444" id="1091">rawMonoid</a> <a class="Keyword" id="1101">public</a>
  <a class="Keyword" id="1110">using</a> <a class="Symbol" id="1116">(</a><a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1117">_×_</a><a class="Symbol" id="1120">)</a>

<a class="Comment" id="1123">------------------------------------------------------------------------</a>
<a class="Comment" id="1196">-- Properties of _×_</a>

<a id="×-congʳ"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1218" id="1218">×-congʳ</a> <a class="Symbol" id="1226">:</a> <a class="Symbol" id="1228">∀</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1230" id="1230">n</a> <a class="Symbol" id="1232">→</a> <a class="Symbol" id="1234">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1230" id="1235">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1237">×_</a><a class="Symbol" id="1239">)</a> <a class="Function Operator" href="Relation.Binary.Core.html#1577" id="1241">Preserves</a> <a class="Field Operator" href="Algebra.Bundles.html#6151" id="1251">_≈_</a> <a class="Function Operator" href="Relation.Binary.Core.html#1577" id="1255">⟶</a> <a class="Field Operator" href="Algebra.Bundles.html#6151" id="1257">_≈_</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1218" id="1261">×-congʳ</a> <a class="Number" id="1269">0</a>       <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1277" id="1277">x≈x′</a> <a class="Symbol" id="1282">=</a> <a class="Function" href="Relation.Binary.Structures.html#1596" id="1284">refl</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1218" id="1289">×-congʳ</a> <a class="Symbol" id="1297">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#234" id="1298">suc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1302" id="1302">n</a><a class="Symbol" id="1303">)</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1305" id="1305">x≈x′</a> <a class="Symbol" id="1310">=</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#706" id="1312">+-cong</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1305" id="1319">x≈x′</a> <a class="Symbol" id="1324">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1218" id="1325">×-congʳ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1302" id="1333">n</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1305" id="1335">x≈x′</a><a class="Symbol" id="1339">)</a>

<a id="×-cong"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1342" id="1342">×-cong</a> <a class="Symbol" id="1349">:</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1351">_×_</a> <a class="Function Operator" href="Relation.Binary.Core.html#1703" id="1355">Preserves₂</a> <a class="Datatype Operator" href="Agda.Builtin.Equality.html#150" id="1366">_≡_</a> <a class="Function Operator" href="Relation.Binary.Core.html#1703" id="1370">⟶</a> <a class="Field Operator" href="Algebra.Bundles.html#6151" id="1372">_≈_</a> <a class="Function Operator" href="Relation.Binary.Core.html#1703" id="1376">⟶</a> <a class="Field Operator" href="Algebra.Bundles.html#6151" id="1378">_≈_</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1342" id="1382">×-cong</a> <a class="Symbol" id="1389">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1390" id="1390">n</a><a class="Symbol" id="1391">}</a> <a class="InductiveConstructor" href="Agda.Builtin.Equality.html#207" id="1393">P.refl</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1400" id="1400">x≈x′</a> <a class="Symbol" id="1405">=</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#1218" id="1407">×-congʳ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1390" id="1415">n</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1400" id="1417">x≈x′</a>

<a id="×-congˡ"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1423" id="1423">×-congˡ</a> <a class="Symbol" id="1431">:</a> <a class="Symbol" id="1433">∀</a> <a class="Symbol" id="1435">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1436" id="1436">x</a><a class="Symbol" id="1437">}</a> <a class="Symbol" id="1439">→</a> <a class="Symbol" id="1441">(</a><a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1442">_×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1436" id="1445">x</a><a class="Symbol" id="1446">)</a> <a class="Function Operator" href="Relation.Binary.Core.html#1577" id="1448">Preserves</a> <a class="Datatype Operator" href="Agda.Builtin.Equality.html#150" id="1458">_≡_</a> <a class="Function Operator" href="Relation.Binary.Core.html#1577" id="1462">⟶</a> <a class="Field Operator" href="Algebra.Bundles.html#6151" id="1464">_≈_</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1423" id="1468">×-congˡ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1476" id="1476">m≡n</a> <a class="Symbol" id="1480">=</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#1342" id="1482">×-cong</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1476" id="1489">m≡n</a> <a class="Function" href="Relation.Binary.Structures.html#1596" id="1493">refl</a>

<a class="Comment" id="1499">-- _×_ is homomorphic with respect to _ℕ+_/_+_.</a>

<a id="×-homo-+"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1548" id="1548">×-homo-+</a> <a class="Symbol" id="1557">:</a> <a class="Symbol" id="1559">∀</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1561" id="1561">x</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1563" id="1563">m</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1565" id="1565">n</a> <a class="Symbol" id="1567">→</a> <a class="Symbol" id="1569">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1563" id="1570">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#336" id="1572">ℕ.+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1565" id="1576">n</a><a class="Symbol" id="1577">)</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1579">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1561" id="1581">x</a> <a class="Field Operator" href="Algebra.Bundles.html#6151" id="1583">≈</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1563" id="1585">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1587">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1561" id="1589">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="1591">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1565" id="1593">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1595">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1561" id="1597">x</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1548" id="1599">×-homo-+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1608" id="1608">x</a> <a class="Number" id="1610">0</a>       <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1618" id="1618">n</a> <a class="Symbol" id="1620">=</a> <a class="Function" href="Relation.Binary.Structures.html#1622" id="1622">sym</a> <a class="Symbol" id="1626">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#780" id="1627">+-identityˡ</a> <a class="Symbol" id="1639">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1618" id="1640">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1642">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1608" id="1644">x</a><a class="Symbol" id="1645">))</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1548" id="1648">×-homo-+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1657">x</a> <a class="Symbol" id="1659">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#234" id="1660">suc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1664" id="1664">m</a><a class="Symbol" id="1665">)</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1667" id="1667">n</a> <a class="Symbol" id="1669">=</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Syntax.html#1510" id="1671">begin</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1679">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="1681">+</a> <a class="Symbol" id="1683">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1664" id="1684">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#336" id="1686">ℕ.+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1667" id="1690">n</a><a class="Symbol" id="1691">)</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1693">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1695">x</a>    <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7049" id="1700">≈⟨</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#706" id="1703">+-cong</a> <a class="Function" href="Relation.Binary.Structures.html#1596" id="1710">refl</a> <a class="Symbol" id="1715">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1548" id="1716">×-homo-+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1725">x</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1664" id="1727">m</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1667" id="1729">n</a><a class="Symbol" id="1730">)</a> <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7049" id="1732">⟩</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1736">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="1738">+</a> <a class="Symbol" id="1740">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1664" id="1741">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1743">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1745">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="1747">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1667" id="1749">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1751">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1753">x</a><a class="Symbol" id="1754">)</a>  <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7049" id="1757">≈⟨</a> <a class="Function" href="Relation.Binary.Structures.html#1622" id="1760">sym</a> <a class="Symbol" id="1764">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#838" id="1765">+-assoc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1773">x</a> <a class="Symbol" id="1775">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1664" id="1776">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1778">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1780">x</a><a class="Symbol" id="1781">)</a> <a class="Symbol" id="1783">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1667" id="1784">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1786">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1788">x</a><a class="Symbol" id="1789">))</a> <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7049" id="1792">⟩</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1796">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="1798">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1664" id="1800">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1802">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1804">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="1806">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1667" id="1808">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1810">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1657" id="1812">x</a>    <a class="Function Operator" href="Relation.Binary.Reasoning.Syntax.html#12283" id="1817">∎</a>

<a id="×-idem"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1820" id="1820">×-idem</a> <a class="Symbol" id="1827">:</a> <a class="Symbol" id="1829">∀</a> <a class="Symbol" id="1831">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1832" id="1832">c</a><a class="Symbol" id="1833">}</a> <a class="Symbol" id="1835">→</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="1837">_+_</a> <a class="Function Operator" href="Algebra.Definitions.html#3637" id="1841">IdempotentOn</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1832" id="1854">c</a> <a class="Symbol" id="1856">→</a>
         <a class="Symbol" id="1867">∀</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1869" id="1869">n</a> <a class="Symbol" id="1871">→</a> <a class="Symbol" id="1873">.{{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1876" id="1876">_</a> <a class="Symbol" id="1878">:</a> <a class="Record" href="Data.Nat.Base.html#3260" id="1880">NonZero</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1869" id="1888">n</a><a class="Symbol" id="1889">}}</a> <a class="Symbol" id="1892">→</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1869" id="1894">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1896">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1832" id="1898">c</a> <a class="Field Operator" href="Algebra.Bundles.html#6151" id="1900">≈</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1832" id="1902">c</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1820" id="1904">×-idem</a> <a class="Symbol" id="1911">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1912" id="1912">c</a><a class="Symbol" id="1913">}</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1915" id="1915">idem</a> <a class="Symbol" id="1920">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#234" id="1921">suc</a> <a class="InductiveConstructor" href="Agda.Builtin.Nat.html#221" id="1925">zero</a><a class="Symbol" id="1929">)</a>    <a class="Symbol" id="1934">=</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#809" id="1936">+-identityʳ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1912" id="1948">c</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1820" id="1950">×-idem</a> <a class="Symbol" id="1957">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1958" id="1958">c</a><a class="Symbol" id="1959">}</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1961" id="1961">idem</a> <a class="Symbol" id="1966">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#234" id="1967">suc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1971" id="1971">n</a><a class="Symbol" id="1972">@(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#234" id="1974">suc</a> <a class="Symbol" id="1978">_))</a> <a class="Symbol" id="1982">=</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Syntax.html#1510" id="1984">begin</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1958" id="1992">c</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="1994">+</a> <a class="Symbol" id="1996">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1971" id="1997">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1999">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1958" id="2001">c</a><a class="Symbol" id="2002">)</a> <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7049" id="2004">≈⟨</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#755" id="2007">+-congˡ</a> <a class="Symbol" id="2015">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1820" id="2016">×-idem</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1961" id="2023">idem</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1971" id="2028">n</a> <a class="Symbol" id="2030">)</a> <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7049" id="2032">⟩</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1958" id="2036">c</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="2038">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1958" id="2040">c</a>       <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7049" id="2048">≈⟨</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1961" id="2051">idem</a> <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7049" id="2056">⟩</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1958" id="2060">c</a>           <a class="Function Operator" href="Relation.Binary.Reasoning.Syntax.html#12283" id="2072">∎</a>

<a id="×-assocˡ"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#2075" id="2075">×-assocˡ</a> <a class="Symbol" id="2084">:</a> <a class="Symbol" id="2086">∀</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2088" id="2088">x</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2090" id="2090">m</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2092" id="2092">n</a> <a class="Symbol" id="2094">→</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2090" id="2096">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2098">×</a> <a class="Symbol" id="2100">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2092" id="2101">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2103">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2088" id="2105">x</a><a class="Symbol" id="2106">)</a> <a class="Field Operator" href="Algebra.Bundles.html#6151" id="2108">≈</a> <a class="Symbol" id="2110">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2090" id="2111">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#539" id="2113">ℕ.*</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2092" id="2117">n</a><a class="Symbol" id="2118">)</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2120">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2088" id="2122">x</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#2075" id="2124">×-assocˡ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2133" id="2133">x</a> <a class="InductiveConstructor" href="Agda.Builtin.Nat.html#221" id="2135">zero</a>    <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2143" id="2143">n</a> <a class="Symbol" id="2145">=</a> <a class="Function" href="Relation.Binary.Structures.html#1596" id="2147">refl</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#2075" id="2152">×-assocˡ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2161" id="2161">x</a> <a class="Symbol" id="2163">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#234" id="2164">suc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2168" id="2168">m</a><a class="Symbol" id="2169">)</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2171" id="2171">n</a> <a class="Symbol" id="2173">=</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Syntax.html#1510" id="2175">begin</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2171" id="2183">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2185">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2161" id="2187">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="2189">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2168" id="2191">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2193">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2171" id="2195">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2197">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2161" id="2199">x</a>     <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7049" id="2205">≈⟨</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#755" id="2208">+-congˡ</a> <a class="Symbol" id="2216">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#2075" id="2217">×-assocˡ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2161" id="2226">x</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2168" id="2228">m</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2171" id="2230">n</a><a class="Symbol" id="2231">)</a> <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7049" id="2233">⟩</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2171" id="2237">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2239">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2161" id="2241">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#685" id="2243">+</a> <a class="Symbol" id="2245">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2168" id="2246">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#539" id="2248">ℕ.*</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2171" id="2252">n</a><a class="Symbol" id="2253">)</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2255">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2161" id="2257">x</a> <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7074" id="2259">≈⟨</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#1548" id="2262">×-homo-+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2161" id="2271">x</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2171" id="2273">n</a> <a class="Symbol" id="2275">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2168" id="2276">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#539" id="2278">ℕ.*</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2171" id="2282">n</a><a class="Symbol" id="2283">)</a> <a class="Function" href="Relation.Binary.Reasoning.Syntax.html#7074" id="2285">⟨</a>
  <a class="Symbol" id="2289">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#234" id="2290">suc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2168" id="2294">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#539" id="2296">ℕ.*</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2171" id="2300">n</a><a class="Symbol" id="2301">)</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2303">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2161" id="2305">x</a>     <a class="Function Operator" href="Relation.Binary.Reasoning.Syntax.html#12283" id="2311">∎</a>
</pre></div></body></html>