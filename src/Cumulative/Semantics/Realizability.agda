{-# OPTIONS --without-K --safe #-}

open import Level
open import Axiom.Extensionality.Propositional

-- Realizability of the PER model
--
-- Fundamentally, realizability states that if two values are related, then their
-- corresponding syntactic normal forms are equal. More precisely, realizability
-- states that the following subsumption relations:
--
--       Bot âŠ† El i A âŠ† Top
--             ğ•Œ i    âŠ† TopT
--
-- Due to these subsumptions, we can always derive Top or TopT from El or ğ•Œ and thus
-- obtain the equality we want.
module Cumulative.Semantics.Realizability (fext : Extensionality 0â„“ (suc 0â„“)) where

open import Data.Nat.Induction
open import Lib

open import Cumulative.Semantics.Domain
open import Cumulative.Semantics.Evaluation
open import Cumulative.Semantics.PER
open import Cumulative.Semantics.Properties.PER.Core fext
open import Cumulative.Semantics.Readback


private
  module Real i (rec : âˆ€ {j} â†’ j < i â†’ âˆ€ {A Aâ€²} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ j) â†’ A â‰ˆ Aâ€² âˆˆ TopT) where
    mutual

      BotâŠ†El : (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’
               c â‰ˆ câ€² âˆˆ Bot â†’
               â†‘ A c â‰ˆ â†‘ Aâ€² câ€² âˆˆ El i Aâ‰ˆAâ€²
      BotâŠ†El (ne Câ‰ˆCâ€²) câ‰ˆcâ€²             = ne câ‰ˆcâ€²
      BotâŠ†El N câ‰ˆcâ€²                     = ne câ‰ˆcâ€²
      BotâŠ†El (Uâ€² j<i) câ‰ˆcâ€²
        rewrite ğ•Œ-wellfounded-â‰¡-ğ•Œ _ j<i = ne câ‰ˆcâ€²
      BotâŠ†El {Î  A _ _} {Î  Aâ€² _ _} {c} {câ€²} (Î  iA RT) câ‰ˆcâ€² {b} {bâ€²} bâ‰ˆbâ€²
        with RT bâ‰ˆbâ€²
      ...  | record
             { âŸ¦TâŸ§   = âŸ¦TâŸ§
             ; âŸ¦Tâ€²âŸ§  = âŸ¦Tâ€²âŸ§
             ; â†˜âŸ¦TâŸ§  = â†˜âŸ¦TâŸ§
             ; â†˜âŸ¦Tâ€²âŸ§ = â†˜âŸ¦Tâ€²âŸ§
             ; Tâ‰ˆTâ€²  = Tâ‰ˆTâ€²
             } = record
             { fa = [ âŸ¦TâŸ§ ] c $â€² â†“ A b
             ; faâ€² = [ âŸ¦Tâ€²âŸ§ ] câ€² $â€² â†“ Aâ€² bâ€²
             ; â†˜fa = $âˆ™ A c â†˜âŸ¦TâŸ§
             ; â†˜faâ€² = $âˆ™ Aâ€² câ€² â†˜âŸ¦Tâ€²âŸ§
             ; faâ‰ˆfaâ€² = BotâŠ†El Tâ‰ˆTâ€² helper
             }
        where helper : (c $ â†“ A b) â‰ˆ câ€² $ â†“ Aâ€² bâ€² âˆˆ Bot
              helper n
                with câ‰ˆcâ€² n | ElâŠ†Top iA bâ‰ˆbâ€² n
              ...  | u , â†˜u , â†˜uâ€²
                   | w , â†˜w , â†˜wâ€² = u $ w , R$ n â†˜u â†˜w , R$ n â†˜uâ€² â†˜wâ€²


      ElâŠ†Top : (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’
               a â‰ˆ aâ€² âˆˆ El i Aâ‰ˆAâ€² â†’
               â†“ A a â‰ˆ â†“ Aâ€² aâ€² âˆˆ Top
      ElâŠ†Top (ne Câ‰ˆCâ€²) (ne câ‰ˆcâ€²) n
        with câ‰ˆcâ€² n
      ...  | u , â†˜u , â†˜uâ€²     = ne u , Rne n â†˜u , Rne n â†˜uâ€²
      ElâŠ†Top N ze n           = ze , Rze n , Rze n
      ElâŠ†Top N (su aâ‰ˆb) n
        with ElâŠ†Top N aâ‰ˆb n
      ...  | w , â†˜w , â†˜wâ€²     = su w , Rsu n â†˜w , Rsu n â†˜wâ€²
      ElâŠ†Top N (ne câ‰ˆcâ€²) n
        with câ‰ˆcâ€² n
      ...  | u , â†˜u , â†˜uâ€²     = ne u , RN n â†˜u , RN n â†˜uâ€²
      ElâŠ†Top (Uâ€² j<i) Aâ‰ˆAâ€² n
        rewrite ğ•Œ-wellfounded-â‰¡-ğ•Œ _ j<i
        with rec j<i Aâ‰ˆAâ€² n
      ...  | W , â†˜W , â†˜Wâ€²     = W , RU n â†˜W , RU n â†˜Wâ€²
      ElâŠ†Top (Î  iA RT) aâ‰ˆaâ€² n
        with BotâŠ†El iA (Bot-l n)
      ...  | zâ‰ˆzâ€²
           with RT zâ‰ˆzâ€² | aâ‰ˆaâ€² zâ‰ˆzâ€²
      ...     | record { âŸ¦TâŸ§ = âŸ¦TâŸ§ ; âŸ¦Tâ€²âŸ§ = âŸ¦Tâ€²âŸ§ ; â†˜âŸ¦TâŸ§ = â†˜âŸ¦TâŸ§ ; â†˜âŸ¦Tâ€²âŸ§ = â†˜âŸ¦Tâ€²âŸ§ ; Tâ‰ˆTâ€² = Tâ‰ˆTâ€² }
              | record { fa = fa ; faâ€² = faâ€² ; â†˜fa = â†˜fa ; â†˜faâ€² = â†˜faâ€² ; faâ‰ˆfaâ€² = faâ‰ˆfaâ€² }
              with ElâŠ†Top Tâ‰ˆTâ€² faâ‰ˆfaâ€² (1 + n)
      ...        | w , â†˜w , â†˜wâ€² = Î› w , RÎ› n â†˜fa â†˜âŸ¦TâŸ§ â†˜w , RÎ› n â†˜faâ€² â†˜âŸ¦Tâ€²âŸ§ â†˜wâ€²


    ğ•ŒâŠ†TopT : (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’ A â‰ˆ Aâ€² âˆˆ TopT
    ğ•ŒâŠ†TopT (ne Câ‰ˆCâ€²) n
      with Câ‰ˆCâ€² n
    ...  | V , â†˜V , â†˜Vâ€²          = ne V , Rne n â†˜V , Rne n â†˜Vâ€²
    ğ•ŒâŠ†TopT N n                   = N , RN n , RN n
    ğ•ŒâŠ†TopT (Uâ€² j<i) n            = Se _ , RU n , RU n
    ğ•ŒâŠ†TopT (Î  iA RT) n
      with BotâŠ†El iA (Bot-l n)
    ...  | zâ‰ˆzâ€²
         with RT zâ‰ˆzâ€²
    ...     | record { âŸ¦TâŸ§ = âŸ¦TâŸ§ ; âŸ¦Tâ€²âŸ§ = âŸ¦Tâ€²âŸ§ ; â†˜âŸ¦TâŸ§ = â†˜âŸ¦TâŸ§ ; â†˜âŸ¦Tâ€²âŸ§ = â†˜âŸ¦Tâ€²âŸ§ ; Tâ‰ˆTâ€² = Tâ‰ˆTâ€² }
            with ğ•ŒâŠ†TopT iA n | ğ•ŒâŠ†TopT Tâ‰ˆTâ€² (1 + n)
    ...        | W , â†˜W , â†˜Wâ€²
               | Wâ‚ , â†˜Wâ‚ , â†˜Wâ‚â€² = Î  W Wâ‚ , RÎ  n â†˜W â†˜âŸ¦TâŸ§ â†˜Wâ‚ , RÎ  n â†˜Wâ€² â†˜âŸ¦Tâ€²âŸ§ â†˜Wâ‚â€²



ğ•ŒâŠ†TopT : âˆ€ {i} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’ A â‰ˆ Aâ€² âˆˆ TopT
ğ•ŒâŠ†TopT Aâ‰ˆAâ€² = <-Measure.wfRec ((Î» i â†’ âˆ€ {A Aâ€²} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’ A â‰ˆ Aâ€² âˆˆ TopT))
                              Real.ğ•ŒâŠ†TopT _ Aâ‰ˆAâ€²

BotâŠ†El : âˆ€ {i} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’
         c â‰ˆ câ€² âˆˆ Bot â†’
         â†‘ A c â‰ˆ â†‘ Aâ€² câ€² âˆˆ El i Aâ‰ˆAâ€²
BotâŠ†El {i = i} = Real.BotâŠ†El i Î» _ â†’ ğ•ŒâŠ†TopT

ElâŠ†Top : âˆ€ {i} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’
         a â‰ˆ aâ€² âˆˆ El i Aâ‰ˆAâ€² â†’
         â†“ A a â‰ˆ â†“ Aâ€² aâ€² âˆˆ Top
ElâŠ†Top {i = i} = Real.ElâŠ†Top i (Î» _ â†’ ğ•ŒâŠ†TopT)
