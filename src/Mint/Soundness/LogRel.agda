{-# OPTIONS --without-K --safe #-}

-- Definitions of logical relations for the gluing model and semantic judgments
module Mint.Soundness.LogRel where

open import Lib hiding (lookup)
open import Data.Nat
open import Data.Nat.Properties

open import Mint.Statics public
open import Mint.Semantics.Domain public
open import Mint.Semantics.Evaluation public
open import Mint.Semantics.PER public

open import Mint.Soundness.Restricted public


-- This function transform a substitution into a UMoT
--
-- Since the model is Kripke, we have a notion of monotonicity on both syntactic and
-- semantic sides. On the syntactic side, the monotonicity is with respect to
-- substitutions; while on the semantic side, the monotonicity is with respect to
-- UMoTs. This function thus connects these two notions of monotonicity. In the
-- definition of the gluing model, the Kripke structure is substitutions, so we use
-- this function to obtain UMoTs to describe the monotonicity on the semantic side.
mt : Substs â†’ UMoT
mt I        = vone
mt wk       = vone
mt (Ïƒ , _)  = mt Ïƒ
mt (Ïƒ ï¼› n) = ins (mt Ïƒ) n
mt (Ïƒ âˆ˜ Î´)  = mt Ïƒ Ã¸ mt Î´

-------------------------------------
-- Gluing models for terms and types

-- Gluing model for natural numbers
infix 4 _âŠ¢_âˆ¶NÂ®_âˆˆNat

data _âŠ¢_âˆ¶NÂ®_âˆˆNat : Ctxs â†’ Exp â†’ D â†’ Set where
  ze : Î“ âŠ¢ t â‰ˆ ze âˆ¶ N â†’
       -----------------
       Î“ âŠ¢ t âˆ¶NÂ® ze âˆˆNat
  su : Î“ âŠ¢ t â‰ˆ su tâ€² âˆ¶ N â†’
       Î“ âŠ¢ tâ€² âˆ¶NÂ® a âˆˆNat â†’
       --------------------
       Î“ âŠ¢ t âˆ¶NÂ® su a âˆˆNat
  ne : (câˆˆ : c âˆˆâ€² Bot) â†’
       (âˆ€ {Î” Ïƒ} â†’ Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ let (u , _) = câˆˆ (map len Î”) (mt Ïƒ) in Î” âŠ¢ t [ Ïƒ ] â‰ˆ Neâ‡’Exp u âˆ¶ N) â†’
       -----------------------
       Î“ âŠ¢ t âˆ¶NÂ® â†‘ N c âˆˆNat

-- Helper concepts for the gluing model
--
-- These definitions are defined for the purpose of having more structural condition
-- management.  One can find unfolded definitions using conjunctions in the comments
-- near the definition of the gluing model.

-- Gluing model for â–¡
--
-- Here R is always the gluing model for types GT
--
-- Essentially the gluing model recurses down to GT. The Kripke structure is decribed by the field krip.
record Gluâ–¡ i Î“ T (R : Substs â†’ â„• â†’ Ctxs â†’ Typ â†’ Set) : Set where
  field
    GT   : Typ
    Tâ‰ˆ   : Î“ âŠ¢ T â‰ˆ â–¡ GT âˆ¶ Se i
    krip : âˆ€ Î¨s â†’ âŠ¢ Î¨s ++âº Î” â†’ Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ R Ïƒ (len Î¨s) (Î¨s ++âº Î”) (GT [ Ïƒ ï¼› len Î¨s ])

-- Gluing model for the box constructor
--
-- Here R is always the gluing model for terms (unbox t)
--
-- The gluing model ensures coherence after elimination.
record â–¡Krip Î¨s Î” t T Ïƒ a (R : Substs â†’ â„• â†’ Ctxs â†’ Exp â†’ Typ â†’ D â†’ Set) : Set where
  field
    ua  : D
    â†˜ua : unboxâˆ™ len Î¨s , a [ mt Ïƒ ] â†˜ ua
    rel : R Ïƒ (len Î¨s) (Î¨s ++âº Î”) (unbox (len Î¨s) (t [ Ïƒ ])) (T [ Ïƒ ï¼› len Î¨s ]) ua


record Glubox i Î“ t T a
              {A B} (Aâ‰ˆB : A â‰ˆ B âˆˆ ğ•Œ i)
              (R : Substs â†’ â„• â†’ Ctxs â†’ Exp â†’ Typ â†’ D â†’ Set) : Set where
  field
    GT   : Typ
    tâˆ¶T  : Î“ âŠ¢ t âˆ¶ T
    aâˆˆEl : a âˆˆâ€² El i Aâ‰ˆB
    Tâ‰ˆ   : Î“ âŠ¢ T â‰ˆ â–¡ GT âˆ¶ Se i
    krip : âˆ€ Î¨s â†’ âŠ¢ Î¨s ++âº Î” â†’ Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ â–¡Krip Î¨s Î” t GT Ïƒ a R

-- Gluing model for Î 
record Î Rel i Î” IT OT Ïƒ
             (iA : âˆ€ (Îº : UMoT) â†’ A [ Îº ] â‰ˆ B [ Îº ] âˆˆ ğ•Œ i)
             (RI : Substs â†’ Ctxs â†’ Typ â†’ Set)
             (RO : âˆ€ {a aâ€²} Ïƒ â†’ a â‰ˆ aâ€² âˆˆ El i (iA (mt Ïƒ)) â†’ Ctxs â†’ Typ â†’ Set)
             (Rs : Substs â†’ Ctxs â†’ Exp â†’ Typ â†’ D â†’ Set) : Set where
  field
    IT-rel : RI Ïƒ Î” (IT [ Ïƒ ])
    OT-rel : Rs Ïƒ Î” s (IT [ Ïƒ ]) a â†’ (aâˆˆ : a âˆˆâ€² El i (iA (mt Ïƒ))) â†’ RO Ïƒ aâˆˆ Î” (OT [ Ïƒ , s ])


record GluÎ  i Î“ T {A B}
            (iA : âˆ€ (Îº : UMoT) â†’ A [ Îº ] â‰ˆ B [ Îº ] âˆˆ ğ•Œ i)
            (RI : Substs â†’ Ctxs â†’ Typ â†’ Set)
            (RO : âˆ€ {a aâ€²} Ïƒ â†’ a â‰ˆ aâ€² âˆˆ El i (iA (mt Ïƒ)) â†’ Ctxs â†’ Typ â†’ Set)
            (Rs : Substs â†’ Ctxs â†’ Exp â†’ Typ â†’ D â†’ Set) : Set where
  field
    IT   : Typ
    OT   : Typ
    -- need this prop or it is too difficult to invert
    âŠ¢OT  : IT âˆº Î“ âŠ¢ OT âˆ¶ Se i
    Tâ‰ˆ   : Î“ âŠ¢ T â‰ˆ Î  IT OT âˆ¶ Se i
    krip : Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ Î Rel i Î” IT OT Ïƒ iA RI RO Rs


-- Gluing model for Î›
record Î›Kripke Î” t T f a (R$ : Ctxs â†’ Exp â†’ Typ â†’ D â†’ Set) : Set where
  field
    fa : D
    â†˜fa : f âˆ™ a â†˜ fa
    Â®fa : R$ Î” t T fa

record Î›Rel i Î” t IT OT Ïƒ f
             (iA : âˆ€ (Îº : UMoT) â†’ A [ Îº ] â‰ˆ B [ Îº ] âˆˆ ğ•Œ i)
             (RI : Substs â†’ Ctxs â†’ Typ â†’ Set)
             (Rs : Substs â†’ Ctxs â†’ Exp â†’ Typ â†’ D â†’ Set)
             (R$ : âˆ€ {a aâ€²} Ïƒ â†’ a â‰ˆ aâ€² âˆˆ El i (iA (mt Ïƒ)) â†’ Ctxs â†’ Exp â†’ Typ â†’ D â†’ Set) : Set where
  field
    IT-rel : RI Ïƒ Î” (IT [ Ïƒ ])
    ap-rel : Rs Ïƒ Î” s (IT [ Ïƒ ]) b â†’ (bâˆˆ : b âˆˆâ€² El i (iA (mt Ïƒ))) â†’ Î›Kripke Î” (t [ Ïƒ ] $ s) (OT [ Ïƒ , s ]) (f [ mt Ïƒ ]) b (R$ Ïƒ bâˆˆ)

  flipped-ap-rel : (bâˆˆ : b âˆˆâ€² El i (iA (mt Ïƒ))) â†’ âˆ€ {s} â†’ Rs Ïƒ Î” s (IT [ Ïƒ ]) b â†’ Î›Kripke Î” (t [ Ïƒ ] $ s) (OT [ Ïƒ , s ]) (f [ mt Ïƒ ]) b (R$ Ïƒ bâˆˆ)
  flipped-ap-rel bâˆˆ R = ap-rel R bâˆˆ

record GluÎ› i Î“ t T a {A B Tâ€² Tâ€³ Ï Ïâ€²}
            (iA : âˆ€ (Îº : UMoT) â†’ A [ Îº ] â‰ˆ B [ Îº ] âˆˆ ğ•Œ i)
            (RT : âˆ€ {a aâ€²} (Îº : UMoT) â†’ a â‰ˆ aâ€² âˆˆ El i (iA Îº) â†’ Î RT Tâ€² (Ï [ Îº ] â†¦ a) Tâ€³ (Ïâ€² [ Îº ] â†¦ aâ€²) (ğ•Œ i))
            (RI : Substs â†’ Ctxs â†’ Typ â†’ Set)
            (Rs : Substs â†’ Ctxs â†’ Exp â†’ Typ â†’ D â†’ Set)
            (R$ : âˆ€ {a aâ€²} Ïƒ â†’ a â‰ˆ aâ€² âˆˆ El i (iA (mt Ïƒ)) â†’ Ctxs â†’ Exp â†’ Typ â†’ D â†’ Set) : Set where
  field
    tâˆ¶T  : Î“ âŠ¢ t âˆ¶ T
    aâˆˆEl : a âˆˆâ€² El i (Î  iA RT)
    IT   : Typ
    OT   : Typ
    -- need this prop or it is too difficult to invert
    âŠ¢OT  : IT âˆº Î“ âŠ¢ OT âˆ¶ Se i
    Tâ‰ˆ   : Î“ âŠ¢ T â‰ˆ Î  IT OT âˆ¶ Se i
    krip : Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ Î›Rel i Î” t IT OT Ïƒ a iA RI Rs R$

-- Gluing model for universes
record GluU j i Î“ t T A (R : A âˆˆâ€² ğ•Œ j â†’ Set) : Set where
  field
    tâˆ¶T : Î“ âŠ¢ t âˆ¶ T
    Tâ‰ˆ  : Î“ âŠ¢ T â‰ˆ Se j âˆ¶ Se i
    Aâˆˆğ•Œ : A âˆˆâ€² ğ•Œ j
    rel : R Aâˆˆğ•Œ

-- Gluing model for neutral terms
record GluNe i Î“ t T (câˆˆâŠ¥ : c âˆˆâ€² Bot) (Câ‰ˆCâ€² : C â‰ˆ Câ€² âˆˆ Bot) : Set where
  field
    tâˆ¶T : Î“ âŠ¢ t âˆ¶ T
    âŠ¢T  : Î“ âŠ¢ T âˆ¶ Se i
    krip : Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’
           let V , _ = Câ‰ˆCâ€² (map len Î”) (mt Ïƒ)
               u , _ = câˆˆâŠ¥ (map len Î”) (mt Ïƒ)
           in Î” âŠ¢ T [ Ïƒ ] â‰ˆ Neâ‡’Exp V âˆ¶ Se i
            Ã— Î” âŠ¢ t [ Ïƒ ] â‰ˆ Neâ‡’Exp u âˆ¶ T [ Ïƒ ]

-- The definition of the gluing model
--
-- Due to cumulative universes, we must do a well-founded induction on the universe
-- level i.  The gluing model has two relations:
--
-- Î“ âŠ¢ T Â® Aâ‰ˆB : T and A (and B) are related at level i. It is Aâ‰ˆB again due to the
-- proof relevant nature of MLTT.
--
-- Î“ âŠ¢ t âˆ¶ T Â® a âˆˆEl Aâ‰ˆB : t and a are related. t has type T and a is in El Aâ‰ˆB. T and
-- Aâ‰ˆB are related in level i.
module Glu i (rec : âˆ€ {j} â†’ j < i â†’ âˆ€ {A B} â†’ Ctxs â†’ Typ â†’ A â‰ˆ B âˆˆ ğ•Œ j â†’ Set) where
  infix 4 _âŠ¢_Â®_ _âŠ¢_âˆ¶_Â®_âˆˆEl_

  mutual

    _âŠ¢_Â®_ : Ctxs â†’ Typ â†’ A â‰ˆ B âˆˆ ğ•Œ i â†’ Set
    Î“ âŠ¢ T Â® ne Câ‰ˆCâ€²      = Î“ âŠ¢ T âˆ¶ Se i Ã— âˆ€ {Î” Ïƒ} â†’ Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ let V , _ = Câ‰ˆCâ€² (map len Î”) (mt Ïƒ) in Î” âŠ¢ T [ Ïƒ ] â‰ˆ Neâ‡’Exp V âˆ¶ Se i
    Î“ âŠ¢ T Â® N            = Î“ âŠ¢ T â‰ˆ N âˆ¶ Se i
    Î“ âŠ¢ T Â® U {j} j<i eq = Î“ âŠ¢ T â‰ˆ Se j âˆ¶ Se i
    Î“ âŠ¢ T Â® â–¡ Aâ‰ˆB        = Gluâ–¡ i Î“ T (Î» Ïƒ n â†’ _âŠ¢_Â® Aâ‰ˆB (ins (mt Ïƒ) n))
                           -- âˆƒ Î» GT â†’ Î“ âŠ¢ T â‰ˆ â–¡ GT âˆ¶ Se i
                           --   Ã— âˆ€ Î¨s â†’ âŠ¢ Î¨s ++âº Î” â†’ Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ Î¨s ++âº Î” âŠ¢ GT [ Ïƒ ï¼› len Î¨s ] Â® Aâ‰ˆB (ins (mt Ïƒ) (len Î¨s))
    Î“ âŠ¢ T Â® Î  iA RT      = GluÎ  i Î“ T iA (Î» Ïƒ â†’ _âŠ¢_Â® iA (mt Ïƒ)) (Î» Ïƒ aâˆˆ â†’ _âŠ¢_Â® Î RT.Tâ‰ˆTâ€² (RT (mt Ïƒ) aâˆˆ)) (Î» Ïƒ â†’ _âŠ¢_âˆ¶_Â®_âˆˆEl iA (mt Ïƒ))
                           -- âˆƒâ‚‚ Î» IT OT â†’ Î“ âŠ¢ T â‰ˆ Î  IT OT âˆ¶ Se i
                           --    Ã— IT âˆº Î“ âŠ¢ OT âˆ¶ Se i
                           --    Ã— âˆ€ {Î” Ïƒ} â†’ Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’
                           --                (Î” âŠ¢ IT [ Ïƒ ] Â® iA (mt Ïƒ))
                           --              Ã— âˆ€ {s a} (irel : Î” âŠ¢ s âˆ¶ IT [ Ïƒ ] Â® a âˆˆEl iA (mt Ïƒ)) (aâˆˆ : a âˆˆâ€² El i (iA (mt Ïƒ))) â†’ Î” âŠ¢ OT [ Ïƒ , s ] Â® Î RT.Tâ‰ˆTâ€² (RT (mt Ïƒ) aâˆˆ)

    _âŠ¢_âˆ¶_Â®_âˆˆEl_ : Ctxs â†’ Exp â†’ Typ â†’ D â†’ A â‰ˆ B âˆˆ ğ•Œ i â†’ Set
    Î“ âŠ¢ t âˆ¶ T Â® a âˆˆEl ne Câ‰ˆCâ€²      = Î£ (a âˆˆâ€² Neu) Î» { (ne câˆˆâŠ¥) â†’ GluNe i Î“ t T câˆˆâŠ¥ Câ‰ˆCâ€² }
    Î“ âŠ¢ t âˆ¶ T Â® a âˆˆEl N            = Î“ âŠ¢ t âˆ¶NÂ® a âˆˆNat Ã— Î“ âŠ¢ T â‰ˆ N âˆ¶ Se i
    Î“ âŠ¢ t âˆ¶ T Â® a âˆˆEl U {j} j<i eq = GluU j i Î“ t T a (rec j<i Î“ t)
                                   --   Î“ âŠ¢ t âˆ¶ T
                                   -- Ã— Î“ âŠ¢ T â‰ˆ Se j âˆ¶ Se i
                                   -- Ã— A âˆˆâ€² ğ•Œ j
                                   -- Ã— Î“ âŠ¢ t Â® Aâˆˆğ•Œ
    Î“ âŠ¢ t âˆ¶ T Â® a âˆˆEl â–¡ Aâ‰ˆB        = Glubox i Î“ t T a (â–¡ Aâ‰ˆB) (Î» Ïƒ n â†’ _âŠ¢_âˆ¶_Â®_âˆˆEl Aâ‰ˆB (ins (mt Ïƒ) n))
                                   -- Î“ âŠ¢ t âˆ¶ T Ã— a âˆˆâ€² El i (â–¡ Aâ‰ˆ B) Ã—
                                   -- âˆƒ Î» GT â†’ Î“ âŠ¢ T â‰ˆ â–¡ GT âˆ¶ Se i
                                   --   Ã— âˆ€ Î¨s â†’ âŠ¢ Î¨s ++âº Î” â†’ Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ âˆƒ Î» ub â†’ unbox (len Î¨s) âˆ™ a [ mt Ïƒ ] â†˜ ub Ã— Î¨s ++âº Î” âŠ¢ unbox (len Î¨s) (t [ Ïƒ ]) âˆ¶ GT [ Ïƒ ï¼› len Î¨s ] Â® ub âˆˆEl (Aâ‰ˆB (ins (mt Ïƒ) (len Î¨s)))
    Î“ âŠ¢ t âˆ¶ T Â® a âˆˆEl Î  iA RT      = GluÎ› i Î“ t T a iA RT (Î» Ïƒ â†’ _âŠ¢_Â® iA (mt Ïƒ)) (Î» Ïƒ â†’ _âŠ¢_âˆ¶_Â®_âˆˆEl iA (mt Ïƒ)) (Î» Ïƒ bâˆˆ â†’ _âŠ¢_âˆ¶_Â®_âˆˆEl Î RT.Tâ‰ˆTâ€² (RT (mt Ïƒ) bâˆˆ))
                                   -- Î“ âŠ¢ t âˆ¶ T Ã— (a âˆˆâ€² El i (Î  iA RT))
                                   --   Ã— âˆƒâ‚‚ Î» IT OT â†’ Î“ âŠ¢ T â‰ˆ Î  IT OT âˆ¶ Se i
                                   --                Ã— IT âˆº Î“ âŠ¢ OT âˆ¶ Se i
                                   --                Ã— âˆ€ {Î” Ïƒ} â†’ Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’
                                   --                          (Î” âŠ¢ IT [ Ïƒ ] Â® iA (mt Ïƒ))
                                   --                         Ã— âˆ€ {s b} (irel : Î” âŠ¢ s âˆ¶ IT [ Ïƒ ] Â® b âˆˆEl iA (mt Ïƒ)) (bâˆˆ : b âˆˆâ€² El i (iA (mt Ïƒ))) â†’ âˆƒ Î» ap â†’ a [ mt Ïƒ ] âˆ™ b â†˜ ap Ã— Î” âŠ¢ t [ Ïƒ ] $ s âˆ¶ OT [ Ïƒ , s ] Â® ap âˆˆEl Î RT.Tâ‰ˆTâ€² (RT (mt Ïƒ) bâˆˆ)


-- Similar to the PER model, we tie the knot using well-founded induction.
Glu-wellfounded : âˆ€ i {j} â†’ j < i â†’ âˆ€ {A B} â†’ Ctxs â†’ Typ â†’ A â‰ˆ B âˆˆ ğ•Œ j â†’ Set
Glu-wellfounded .(suc _) {j} (sâ‰¤s j<i) = Glu._âŠ¢_Â®_ j Î» jâ€²<j â†’ Glu-wellfounded _ (â‰¤-trans jâ€²<j j<i)

private
  module G i = Glu i (Glu-wellfounded i)

infix 4 _âŠ¢_Â®[_]_ _âŠ¢_âˆ¶_Â®[_]_âˆˆEl_

-- T and A are related at level i
_âŠ¢_Â®[_]_ : Ctxs â†’ Typ â†’ âˆ€ i â†’ A â‰ˆ B âˆˆ ğ•Œ i â†’ Set
Î“ âŠ¢ T Â®[ i ] Aâ‰ˆB = G._âŠ¢_Â®_ i Î“ T Aâ‰ˆB

-- t of type T and a of type A are related at level i
_âŠ¢_âˆ¶_Â®[_]_âˆˆEl_ : Ctxs â†’ Exp â†’ Typ â†’ âˆ€ i â†’ D â†’ A â‰ˆ B âˆˆ ğ•Œ i â†’ Set
Î“ âŠ¢ t âˆ¶ T Â®[ i ] a âˆˆEl Aâ‰ˆB = G._âŠ¢_âˆ¶_Â®_âˆˆEl_ i Î“ t T a Aâ‰ˆB


-- In the PER model, we have three extrema PERs: Bot, Top and TopT. They relates equal
-- neutral values, equal normal values and equal normal semantic types after readback,
-- respctively. Similarly, we need the same notions in the gluing model. We need:
--
-- Â®â†“         : t and c are related iff t and any readback of c are equivalent.
-- Â®â†‘ (value) : t and a are related iff t and any readback of a are equivalent.
-- Â®â†‘ (type)  : T and A are related iff T and any readback of A are equivalent.
infix 4 _âŠ¢_âˆ¶_Â®â†“[_]_âˆˆEl_ _âŠ¢_âˆ¶_Â®â†‘[_]_âˆˆEl_  _âŠ¢_Â®â†‘[_]_

record _âŠ¢_âˆ¶_Â®â†“[_]_âˆˆEl_ Î“ t T i c (Aâ‰ˆB : A â‰ˆ B âˆˆ ğ•Œ i) : Set where
  field
    tâˆ¶T  : Î“ âŠ¢ t âˆ¶ T
    Tâˆ¼A  : Î“ âŠ¢ T Â®[ i ] Aâ‰ˆB
    câˆˆâŠ¥  : c âˆˆâ€² Bot
    krip : Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ let u , _ = câˆˆâŠ¥ (map len Î”) (mt Ïƒ) in Î” âŠ¢ t [ Ïƒ ] â‰ˆ Neâ‡’Exp u âˆ¶ T [ Ïƒ ]


record _âŠ¢_âˆ¶_Â®â†‘[_]_âˆˆEl_ Î“ t T i a (Aâ‰ˆB : A â‰ˆ B âˆˆ ğ•Œ i) : Set where
  field
    tâˆ¶T  : Î“ âŠ¢ t âˆ¶ T
    Tâˆ¼A  : Î“ âŠ¢ T Â®[ i ] Aâ‰ˆB
    aâˆˆâŠ¤  : â†“ A a â‰ˆ â†“ B a âˆˆ Top
    krip : Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ let w , _ = aâˆˆâŠ¤ (map len Î”) (mt Ïƒ) in Î” âŠ¢ t [ Ïƒ ] â‰ˆ Nfâ‡’Exp w âˆ¶ T [ Ïƒ ]

record _âŠ¢_Â®â†‘[_]_ Î“ T i (Aâ‰ˆB : A â‰ˆ B âˆˆ ğ•Œ i) : Set where
  field
    tâˆ¶T  : Î“ âŠ¢ T âˆ¶ Se i
    AâˆˆâŠ¤  : A â‰ˆ B âˆˆ TopT
    krip : Î” âŠ¢r Ïƒ âˆ¶ Î“ â†’ let W , _ = AâˆˆâŠ¤ (map len Î”) (mt Ïƒ) in Î” âŠ¢ T [ Ïƒ ] â‰ˆ Nfâ‡’Exp W âˆ¶ Se i


---------------------------------
-- Gluing model for substitutions

-- Helper predicates for each case of context stacks

-- R is always the gluing model for substitutions
record GluÎº Î“ Ïƒ Î” (Ï : Envs) (R : Ctxs â†’ Substs â†’ Envs â†’ Set) : Set where
  field
    âŠ¢Ïƒ   : Î“ âŠ¢s Ïƒ âˆ¶ [] âˆ·âº Î”
    Î¨sâ»  : List Ctx
    Î“âˆ¥   : Ctxs
    Ïƒâˆ¥   : Substs
    Î“â‰¡   : Î“ â‰¡ Î¨sâ» ++âº Î“âˆ¥
    â‰ˆÏƒâˆ¥  : Î“âˆ¥ âŠ¢s Ïƒ âˆ¥ 1 â‰ˆ Ïƒâˆ¥ âˆ¶ Î”
    Oâ‰¡   : O Ïƒ 1 â‰¡ projâ‚ (Ï 0)
    lenâ‰¡ : len Î¨sâ» â‰¡ O Ïƒ 1
    step : R Î“âˆ¥ Ïƒâˆ¥ (Ï âˆ¥ 1)


-- R is always the gluing model for substitutions
record Gluâˆº i Î“ Ïƒ T Î” (Ï : Envs) (R : Ctxs â†’ Substs â†’ Envs â†’ Set) : Set where
  field
    âŠ¢Ïƒ   : Î“ âŠ¢s Ïƒ âˆ¶ T âˆº Î”
    pÏƒ   : Substs
    v0Ïƒ  : Exp
    âŸ¦TâŸ§  : D
    â‰ˆpÏƒ  : Î“ âŠ¢s p Ïƒ â‰ˆ pÏƒ âˆ¶ Î”
    â‰ˆv0Ïƒ : Î“ âŠ¢ v 0 [ Ïƒ ] â‰ˆ v0Ïƒ âˆ¶ T [ pÏƒ ]
    â†˜âŸ¦TâŸ§ : âŸ¦ T âŸ§ drop Ï â†˜ âŸ¦TâŸ§
    Tâˆˆğ•Œ  : âŸ¦TâŸ§ âˆˆâ€² ğ•Œ i
    tâˆ¼Ï0 : Î“ âŠ¢ v0Ïƒ âˆ¶ (T [ pÏƒ ]) Â®[ i ] (lookup Ï 0) âˆˆEl Tâˆˆğ•Œ
    step : R Î“ pÏƒ (drop Ï)


-- On paper this predicate denotes Î” âŠ¢ T [ Ïƒ ] Â®[ i ] âŸ¦TâŸ§(Ï)
record GluTyp i Î” T (Ïƒ : Substs) Ï : Set where
  field
    âŸ¦TâŸ§   : D
    â†˜âŸ¦TâŸ§  : âŸ¦ T âŸ§ Ï â†˜ âŸ¦TâŸ§
    Tâˆˆğ•Œ   : âŸ¦TâŸ§ âˆˆâ€² ğ•Œ i
    Tâˆ¼âŸ¦TâŸ§ : Î” âŠ¢ T [ Ïƒ ] Â®[ i ] Tâˆˆğ•Œ

-- The definition of gluing model for substitutions
--
-- Similar to the PER model where we use induction-recursion to relate the
-- interpretations of substitutions, here we also use induction-recursion to relate
-- substitutions and evaluation environments.
--
-- This definition is not seen in others' work (A. Abel, D. Gratzer, etc.), where the
-- gluing for substitutions are done by induction on simply the constructor of the
-- contexts (or context stacks in our case). This model will work for cumulative
-- universes in a set-theoretic model, because in these proof one crucial step after
-- proving cumulativity of the model is to take the limit of the universe level, and
-- then the universe level is not spoken of. In type theory, on the other hand, we
-- cannot take limits. This thus forces us to consider universe levels explicitly,
-- leading us to this more precise model.
--
-- Another benefit of this higher precision is that this model can be adapted to
-- non-cumulative universe pretty well. In aforementioned work, since the step of
-- taking limit is essential, the information about universe levels is gone. It
-- becomes unclear how one can easily remove the step of taking limits and adapt their
-- proofs to non-cumulativity. On the other hand, our model keeps track of unvierse
-- levels so there is no problem to obtain the level from our model whenever it is
-- needed.
infix 4 âŠ©_ _âŠ¢s_âˆ¶_Â®_

mutual

  -- This definition is almost the same as âŠ¢_ except that it has one more condition in
  -- âŠ©âˆº.
  data âŠ©_ : Ctxs â†’ Set where
    âŠ©[] : âŠ© [] âˆ· []
    âŠ©Îº  : âŠ© Î“ â†’ âŠ© [] âˆ·âº Î“
    âŠ©âˆº  : âˆ€ {i} (âŠ©Î“ : âŠ© Î“) â†’
          Î“ âŠ¢ T âˆ¶ Se i â†’
          -- This condition says, given any related Ïƒ and Ï, T[Ïƒ] and its evaluation
          -- are related at level i.
          (âˆ€ {Î” Ïƒ Ï} â†’ Î” âŠ¢s Ïƒ âˆ¶ âŠ©Î“ Â® Ï â†’ GluTyp i Î” T Ïƒ Ï) â†’
          âŠ© (T âˆº Î“)

  -- The gluing model for substitutions
  --
  -- This model glues substitutions and evaluation environments. It is recursive on âŠ©_
  -- so this model is again inductive-recursive. We can see that in the âŠ©âˆº case, we
  -- use the universe level. This removes our need to take limits as done in a more
  -- set-thereotic setting.
  _âŠ¢s_âˆ¶_Â®_ : Ctxs â†’ Substs â†’ âŠ© Î” â†’ Envs â†’ Set
  Î” âŠ¢s Ïƒ âˆ¶ âŠ©[] Â® Ï                 = Î” âŠ¢s Ïƒ âˆ¶ [] âˆ· []
  Î” âŠ¢s Ïƒ âˆ¶ âŠ©Îº {Î“} âŠ©Î“ Â® Ï           = GluÎº Î” Ïƒ Î“ Ï (_âŠ¢s_âˆ¶ âŠ©Î“ Â®_)
  Î” âŠ¢s Ïƒ âˆ¶ âŠ©âˆº {Î“} {T} {i} âŠ©Î“ âŠ¢T gT Â® Ï = Gluâˆº i Î” Ïƒ T Î“ Ï (_âŠ¢s_âˆ¶ âŠ©Î“ Â®_)

âŠ©â‡’âŠ¢ : âŠ© Î“ â†’ âŠ¢ Î“
âŠ©â‡’âŠ¢ âŠ©[]          = âŠ¢[]
âŠ©â‡’âŠ¢ (âŠ©Îº âŠ©Î“)      = âŠ¢Îº (âŠ©â‡’âŠ¢ âŠ©Î“)
âŠ©â‡’âŠ¢ (âŠ©âˆº âŠ©Î“ âŠ¢T _) = âŠ¢âˆº (âŠ©â‡’âŠ¢ âŠ©Î“) âŠ¢T

-- On paper this predicate denotes Î” âŠ¢ t [ Ïƒ ] âˆ¶ T [ Ïƒ ] Â®[ i ] âŸ¦ t âŸ§(Ï) âˆˆEl âŸ¦TâŸ§(Ï)
record GluExp i Î” t T (Ïƒ : Substs) Ï : Set where
  field
    âŸ¦TâŸ§   : D
    âŸ¦tâŸ§   : D
    â†˜âŸ¦TâŸ§  : âŸ¦ T âŸ§ Ï â†˜ âŸ¦TâŸ§
    â†˜âŸ¦tâŸ§  : âŸ¦ t âŸ§ Ï â†˜ âŸ¦tâŸ§
    Tâˆˆğ•Œ   : âŸ¦TâŸ§ âˆˆâ€² ğ•Œ i
    tâˆ¼âŸ¦tâŸ§ : Î” âŠ¢ t [ Ïƒ ] âˆ¶ T [ Ïƒ ] Â®[ i ] âŸ¦tâŸ§ âˆˆEl Tâˆˆğ•Œ

record GluSubsts Î” Ï„ (âŠ©Î“â€² : âŠ© Î“â€²) Ïƒ Ï : Set where
  field
    âŸ¦Ï„âŸ§    : Envs
    â†˜âŸ¦Ï„âŸ§   : âŸ¦ Ï„ âŸ§s Ï â†˜ âŸ¦Ï„âŸ§
    Ï„Ïƒâˆ¼âŸ¦Ï„âŸ§ : Î” âŠ¢s Ï„ âˆ˜ Ïƒ âˆ¶ âŠ©Î“â€² Â® âŸ¦Ï„âŸ§


------------------------------------
-- Definitions of semantic judgments

infix 4 _âŠ©_âˆ¶_ _âŠ©s_âˆ¶_

record _âŠ©_âˆ¶_ Î“ t T : Set where
  field
    âŠ©Î“   : âŠ© Î“
    -- This level always remembers the level of T and thus allows easy adaptation to non-cumulativity.
    lvl  : â„•
    krip : Î” âŠ¢s Ïƒ âˆ¶ âŠ©Î“ Â® Ï â†’ GluExp lvl Î” t T Ïƒ Ï


record _âŠ©s_âˆ¶_ Î“ Ï„ Î“â€² : Set where
  field
    âŠ©Î“   : âŠ© Î“
    âŠ©Î“â€²  : âŠ© Î“â€²
    krip : Î” âŠ¢s Ïƒ âˆ¶ âŠ©Î“ Â® Ï â†’ GluSubsts Î” Ï„ âŠ©Î“â€² Ïƒ Ï
