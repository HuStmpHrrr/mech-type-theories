{-# OPTIONS --without-K --safe #-}

-- Definition of the PER model
--
-- The PER model relates two domain values such that the syntactic terms they
-- represent are equivalent. Since we are handling MLTT with full a Ï‰ universe, we must
-- use a feature, induction-recursion, to strengthen the logical power of the
-- meta-language.
module MLTT.Semantics.PER where

open import Data.Nat.Properties

open import Lib hiding (lookup)
open import MLTT.Semantics.Domain
open import MLTT.Semantics.Evaluation
open import MLTT.Semantics.Readback
open import Relation.Binary using (Rel)

Ty : Setâ‚
Ty = Rel D _

Ev : Setâ‚
Ev = Rel Env _

-- Two neutral domain values are related if they are read back equal
Bot : Dn â†’ Dn â†’ Set
Bot c câ€² = âˆ€ n â†’ âˆƒ Î» u â†’ Re n - c â†˜ u Ã— Re n - câ€² â†˜ u

-- Two normal domain values are related if they are read back equal
Top : Df â†’ Df â†’ Set
Top d dâ€² = âˆ€ n â†’ âˆƒ Î» w â†’ Rf n - d â†˜ w Ã— Rf n - dâ€² â†˜ w

-- Two domain values intended to represent types are related if they are read back equal
TopT : D â†’ D â†’ Set
TopT A B = âˆ€ n â†’ âˆƒ Î» W â†’ Rty n - A â†˜ W Ã— Rty n - B â†˜ W

-- A PER to model natural number values
data Nat : Ty where
  ze : ze â‰ˆ ze âˆˆ Nat
  su : a â‰ˆ b âˆˆ Nat â†’
       -----------------
       su a â‰ˆ su b âˆˆ Nat
  ne : c â‰ˆ câ€² âˆˆ Bot â†’
       --------------------
       â†‘ N c â‰ˆ â†‘ N câ€² âˆˆ Nat

-- Neutral type values are related simply when the neutral values themselves are related by Bot
data Neu : Ty where
  ne : c â‰ˆ câ€² âˆˆ Bot â†’
       ---------------------
       â†‘ A c â‰ˆ â†‘ Aâ€² câ€² âˆˆ Neu

-- Now we move on to defining the PER model. To model the universes, we use
-- Tarski-style encoding, i.e. for a universe level i, ğ•Œ i is a PER relating two
-- domain values (or "codes") that are types (i.e. elements of the i'th level
-- universe). If A â‰ˆ B âˆˆ ğ•Œ i, then El i A relates two values that are elements of the
-- set encoded by A.
--
-- Unfortunately, this method only works on paper. In type theory, we must consider
-- the effect of proof relevance, when defining El, we must take a witness
-- Aâ‰ˆB : A â‰ˆ B âˆˆ ğ•Œ i instead of just A, and El is defined by recursion on Aâ‰ˆB, while ğ•Œ
-- itself is defined inductively, hence a induction-recursion definition.
--
-- Finally, we need a well-founded definition in order to handle cumulative
-- universe. In a non-cumulative setting, we expect that this extra well-founded layer
-- by the universe level can be taken away, because we can only grow the universe
-- level by one each time.


-- Helper definitions for the PER model

-- The record for relating return types of Î 's
--
-- Here R is always ğ•Œ i, so on paper, it represents âŸ¦TâŸ§(Ï) â‰ˆ âŸ¦Tâ€²âŸ§(Ïâ€²) âˆˆ ğ•Œ i.
record Î RT T Ï Tâ€² Ïâ€² R : Set where
  field
    âŸ¦TâŸ§   : D
    âŸ¦Tâ€²âŸ§  : D
    â†˜âŸ¦TâŸ§  : âŸ¦ T âŸ§ Ï â†˜ âŸ¦TâŸ§
    â†˜âŸ¦Tâ€²âŸ§ : âŸ¦ Tâ€² âŸ§ Ïâ€² â†˜ âŸ¦Tâ€²âŸ§
    Tâ‰ˆTâ€²  : âŸ¦TâŸ§ â‰ˆ âŸ¦Tâ€²âŸ§ âˆˆ R


-- The record for relating values of type Î  A T Ï
--
-- Here R is always El i âŸ¦TâŸ§(Ï â†¦ a), so on paper, it represents f a â‰ˆ fâ€² aâ€² âˆˆ El i âŸ¦TâŸ§(Ï â†¦ a).
record Î Ì‚ (f a fâ€² aâ€² : D) R : Set where
  field
    fa     : D
    faâ€²    : D
    â†˜fa    : f âˆ™ a â†˜ fa
    â†˜faâ€²   : fâ€² âˆ™ aâ€² â†˜ faâ€²
    faâ‰ˆfaâ€² : fa â‰ˆ faâ€² âˆˆ R

module PERDef (i : â„•) (Univ : âˆ€ {j} â†’ j < i â†’ Ty) where

  mutual

    data ğ•Œ : Ty where
      ne : C â‰ˆ Câ€² âˆˆ Bot â†’
           â†‘ A C â‰ˆ â†‘ Aâ€² Câ€² âˆˆ ğ•Œ
      N  : N â‰ˆ N âˆˆ ğ•Œ
      U  : âˆ€ {j jâ€²} â†’
           j < i â†’            -- cumulativity only requires j < i
           j â‰¡ jâ€² â†’           -- keeping equality here helps with --without-K settings
           --------------
           U j â‰ˆ U jâ€² âˆˆ ğ•Œ
      Î   : (iA : A â‰ˆ Aâ€² âˆˆ ğ•Œ) â†’
           (âˆ€ {a aâ€²} â†’
              a â‰ˆ aâ€² âˆˆ El iA â†’
              Î RT T (Ï â†¦ a) Tâ€² (Ïâ€² â†¦ aâ€²) ğ•Œ) â†’
           -------------------------
           Î  A T Ï â‰ˆ Î  Aâ€² Tâ€² Ïâ€² âˆˆ ğ•Œ


    El : A â‰ˆ B âˆˆ ğ•Œ â†’ Ty
    El (ne Câ‰ˆCâ€²)  = Neu
    El N          = Nat
    El (U j<i eq) = Univ j<i
    El (Î  iA RT)  = Î» f fâ€² â†’ âˆ€ {a b} (inp : a â‰ˆ b âˆˆ El iA) â†’ Î Ì‚ (f) a (fâ€²) b (El (Î RT.Tâ‰ˆTâ€² (RT inp)))

-- Now we tie the knot and expose ğ•Œ and El in the wild.
ğ•Œ-wellfounded : âˆ€ i {j} â†’ j < i â†’ Ty
ğ•Œ-wellfounded .(suc _) (sâ‰¤s {j} j<i) = PERDef.ğ•Œ j (Î» jâ€²<j â†’ ğ•Œ-wellfounded _ (â‰¤-trans jâ€²<j j<i))

private
  module M i = PERDef i (ğ•Œ-wellfounded i)

open M hiding (ğ•Œ; El) public

pattern Uâ€² i<j = U i<j refl

ğ•Œ : â„• â†’ Ty
ğ•Œ = M.ğ•Œ

-- cannot omit `i`. not sure why
El : âˆ€ i â†’ A â‰ˆ B âˆˆ ğ•Œ i â†’ Ty
El i = M.El i


-- On paper, it represents âŸ¦TâŸ§(Ï) â‰ˆ âŸ¦Tâ€²âŸ§(Ïâ€²) âˆˆ ğ•Œ i.
record RelTyp i T Ï Tâ€² Ïâ€² : Set where
  field
    âŸ¦TâŸ§   : D
    âŸ¦Tâ€²âŸ§  : D
    â†˜âŸ¦TâŸ§  : âŸ¦ T âŸ§ Ï â†˜ âŸ¦TâŸ§
    â†˜âŸ¦Tâ€²âŸ§ : âŸ¦ Tâ€² âŸ§ Ïâ€² â†˜ âŸ¦Tâ€²âŸ§
    Tâ‰ˆTâ€²  : âŸ¦TâŸ§ â‰ˆ âŸ¦Tâ€²âŸ§ âˆˆ ğ•Œ i

-- PER model for context stacks and global evaluation environments
--
-- Again we use induction-recursion here in order to model related context stacks and
-- related evaluation environments.
--
-- âŠ¨ Î“ â‰ˆ Î” means that Î“ and Î” are two related contexts so that every
-- corresponding types in them are related after evaluation. The PER for
-- evaluation environments is defined recursively on âŠ¨ Î“ â‰ˆ Î”. On paper, we write
--
--       Ï â‰ˆ Ïâ€² âˆˆ âŸ¦ Î“ âŸ§
--
-- where âŠ¨ Î“ â‰ˆ Î“, but this again will not work in Agda due to proof relevance. For
-- this reason, we must prove some extra properties.
infix 4 âŠ¨_â‰ˆ_ âŠ¨_
mutual
  data âŠ¨_â‰ˆ_ : Ctx â†’ Ctx â†’ Set where
    []-â‰ˆ   : âŠ¨ [] â‰ˆ []
    âˆ·-cong : âˆ€ {i} â†’
             (Î“â‰ˆÎ” : âŠ¨ Î“ â‰ˆ Î”) â†’
             (âˆ€ {Ï Ïâ€²} â†’ Ï â‰ˆ Ïâ€² âˆˆ âŸ¦ Î“â‰ˆÎ” âŸ§Ï â†’ RelTyp i T Ï Tâ€² Ïâ€²) â†’
             ----------------
             âŠ¨ T âˆ· Î“ â‰ˆ Tâ€² âˆ· Î”

  âŸ¦_âŸ§Ï : âŠ¨ Î“ â‰ˆ Î” â†’ Ev
  âŸ¦ []-â‰ˆ âŸ§Ï Ï Ïâ€²           = âŠ¤
  âŸ¦ âˆ·-cong Î“â‰ˆÎ” rel âŸ§Ï Ï Ïâ€² = Î£ (drop Ï â‰ˆ drop Ïâ€² âˆˆ âŸ¦ Î“â‰ˆÎ” âŸ§Ï) Î» Ïâ‰ˆÏâ€² â†’ let open RelTyp (rel Ïâ‰ˆÏâ€²) in lookup Ï 0 â‰ˆ lookup Ïâ€² 0 âˆˆ El _ Tâ‰ˆTâ€²

âŠ¨_ : Ctx â†’ Set
âŠ¨ Î“ = âŠ¨ Î“ â‰ˆ Î“
