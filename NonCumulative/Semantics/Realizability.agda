{-# OPTIONS --without-K --safe #-}

open import Level
open import Axiom.Extensionality.Propositional

-- Realizability of the PER model
--
-- Fundamentally, realizability states that if two values are related, then their
-- corresponding syntactic normal forms are equal. More precisely, realizability
-- states that the following subsumption relations:
--
--       Bot âŠ† El i A âŠ† Top
--             ğ•Œ i    âŠ† TopT
--
-- Due to these subsumptions, we can always derive Top or TopT from El or ğ•Œ and thus
-- obtain the equality we want.
module NonCumulative.Semantics.Realizability (fext : Extensionality 0â„“ (suc 0â„“)) where

open import Data.Nat.Properties
open import Data.Nat.Induction
open import Lib

open import NonCumulative.Semantics.Domain
open import NonCumulative.Semantics.Evaluation
open import NonCumulative.Semantics.PER
open import NonCumulative.Semantics.Properties.PER.Core fext
open import NonCumulative.Semantics.Readback


private
  module Real where
    mutual

      BotâŠ†El : âˆ€ i
               (real : âˆ€ j â†’ j < i â†’ âˆ€ {A Aâ€²} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ j) â†’ A â‰ˆ Aâ€² âˆˆ TopT j)
               (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’
               c â‰ˆ câ€² âˆˆ Bot â†’
               â†‘ i A c â‰ˆ â†‘ i Aâ€² câ€² âˆˆ El i Aâ‰ˆAâ€²
      BotâŠ†El i real (neâ€² Câ‰ˆCâ€²) câ‰ˆcâ€²        = neâ€² câ‰ˆcâ€²
      BotâŠ†El i real Nâ€² câ‰ˆcâ€²                = ne câ‰ˆcâ€²
      BotâŠ†El i real Uâ€² câ‰ˆcâ€²                = neâ€² câ‰ˆcâ€²
      BotâŠ†El {Î  _ A _ _} {Î  _ Aâ€² _ _} {c} {câ€²} i real (Î â€² {j} {k} iA RT) câ‰ˆcâ€² {a} {b} aâ‰ˆb
        rewrite ğ•Œ-wf-gen j (Î Iâ‰¤â€² j k refl)
        rewrite ğ•Œ-wf-gen k (Î Oâ‰¤â€² j k refl)
        with RT aâ‰ˆb
      ...  |  record { âŸ¦TâŸ§ = âŸ¦TâŸ§ ; âŸ¦Tâ€²âŸ§ = âŸ¦Tâ€²âŸ§ ; â†˜âŸ¦TâŸ§ = â†˜âŸ¦TâŸ§ ; â†˜âŸ¦Tâ€²âŸ§ = â†˜âŸ¦Tâ€²âŸ§ ; Tâ‰ˆTâ€² = Tâ‰ˆTâ€² }
        = record
        { fa     = [ âŸ¦TâŸ§ â†™ k ] c $â€² â†“ j A a
        ; faâ€²    = [ âŸ¦Tâ€²âŸ§ â†™ k ] câ€² $â€² â†“ j Aâ€² b
        ; â†˜fa    = $âˆ™ A c â†˜âŸ¦TâŸ§ refl
        ; â†˜faâ€²   = $âˆ™ Aâ€² câ€² â†˜âŸ¦Tâ€²âŸ§ refl
        ; faâ‰ˆfaâ€² = BotâŠ†El k (Î» jâ€² jâ€²<i â†’ real jâ€² (â‰¤-trans jâ€²<i (mâ‰¤nâŠ”m j k))) Tâ‰ˆTâ€² helper
        }
        where helper : (c $ â†“ j A a) â‰ˆ câ€² $ â†“ j Aâ€² b âˆˆ Bot
              helper n
                with câ‰ˆcâ€² n | ElâŠ†Top j (Î» jâ€² jâ€²<i â†’ real jâ€² (â‰¤-trans jâ€²<i (mâ‰¤mâŠ”n j k))) iA aâ‰ˆb n
              ...  | u , â†˜u , â†˜uâ€²
                   | w , â†˜w , â†˜wâ€² = u $ w , R$ n â†˜u â†˜w , R$ n â†˜uâ€² â†˜wâ€²
      BotâŠ†El {Li _ _ A} {Li _ _ Aâ€²} {c} {câ€²} i real (Lâ€² {j} {k} Aâ‰ˆAâ€²) câ‰ˆcâ€²
        rewrite ğ•Œ-wf-gen k (Liâ‰¤â€² j k refl)
        = record
        { ua    = â†‘ k A (unli c)
        ; ub    = â†‘ k Aâ€² (unli câ€²)
        ; â†˜ua   = unliâ†˜ refl
        ; â†˜ub   = unliâ†˜ refl
        ; uaâ‰ˆub = BotâŠ†El k (Î» l l<k â†’ real l (â‰¤-trans l<k (mâ‰¤n+m k j))) Aâ‰ˆAâ€² helper
        }
        where helper : unli c â‰ˆ unli câ€² âˆˆ Bot
              helper n
                with câ‰ˆcâ€² n
              ...  | u , â†˜u , â†˜uâ€² = unlift u , Runli n â†˜u , Runli n â†˜uâ€²

      ElâŠ†Top : âˆ€ i
               (real : âˆ€ j â†’ j < i â†’ âˆ€ {A Aâ€²} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ j) â†’ A â‰ˆ Aâ€² âˆˆ TopT j)
               (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’
               a â‰ˆ aâ€² âˆˆ El i Aâ‰ˆAâ€² â†’
               â†“ i A a â‰ˆ â†“ i Aâ€² aâ€² âˆˆ Top
      ElâŠ†Top i real (neâ€² Câ‰ˆCâ€²) (neâ€² câ‰ˆcâ€²) n
        with câ‰ˆcâ€² n
      ...  | u , â†˜u , â†˜uâ€²                     = ne u , Rneâ€² n â†˜u , Rneâ€² n â†˜uâ€²
      ElâŠ†Top .0 real Nâ€² ze n                  = ze , Rze n , Rze n
      ElâŠ†Top .0 real Nâ€² (su aâ‰ˆaâ€²) n
        with ElâŠ†Top _ real Nâ€² aâ‰ˆaâ€² n
      ...  | w , â†˜w , â†˜wâ€²                     = su w , Rsu n â†˜w , Rsu n â†˜wâ€²
      ElâŠ†Top .0 real Nâ€² (ne câ‰ˆcâ€²) n
        with câ‰ˆcâ€² n
      ...  | u , â†˜u , â†˜uâ€²                     = ne u , RN n â†˜u , RN n â†˜uâ€²
      ElâŠ†Top i real (Uâ€² {j}) aâ‰ˆaâ€² n
        rewrite ğ•Œ-wf-gen j Î» l<j â†’ (â‰¤-trans l<j (â‰¤-reflexive refl))
        with real _ (â‰¤-reflexive refl) aâ‰ˆaâ€² n
      ...  | W , â†˜W , â†˜Wâ€²                     = W , RUâ€² n â†˜W , RUâ€² n â†˜Wâ€²
      ElâŠ†Top i real (Î â€² {j} {k} iA RT) aâ‰ˆaâ€² n
        rewrite ğ•Œ-wf-gen j (Î Iâ‰¤â€² j k refl)
        rewrite ğ•Œ-wf-gen k (Î Oâ‰¤â€² j k refl)
        with BotâŠ†El _ (Î» _ l<j â†’ real _ (â‰¤-trans l<j (mâ‰¤mâŠ”n j k))) iA (Bot-l n)
      ...  | zâ‰ˆzâ€²
           with RT zâ‰ˆzâ€² | aâ‰ˆaâ€² zâ‰ˆzâ€²
      ...     | record { âŸ¦TâŸ§ = âŸ¦TâŸ§ ; âŸ¦Tâ€²âŸ§ = âŸ¦Tâ€²âŸ§ ; â†˜âŸ¦TâŸ§ = â†˜âŸ¦TâŸ§ ; â†˜âŸ¦Tâ€²âŸ§ = â†˜âŸ¦Tâ€²âŸ§ ; Tâ‰ˆTâ€² = Tâ‰ˆTâ€² }
              | record { fa = fa ; faâ€² = faâ€² ; â†˜fa = â†˜fa ; â†˜faâ€² = â†˜faâ€² ; faâ‰ˆfaâ€² = faâ‰ˆfaâ€² }
              with ElâŠ†Top _ (Î» _ l<k â†’ real _ (â‰¤-trans l<k (mâ‰¤nâŠ”m j k))) Tâ‰ˆTâ€² faâ‰ˆfaâ€² (1 + n)
                 | ğ•ŒâŠ†TopT _ (Î» _ l<j â†’ real _ (â‰¤-trans l<j (mâ‰¤mâŠ”n j k))) iA n
      ...        | w , â†˜w , â†˜wâ€²
                 | W , â†˜W , â†˜Wâ€² = Î› (W â†™ j) w , RÎ› n â†˜W â†˜fa â†˜âŸ¦TâŸ§ â†˜w refl , RÎ› n â†˜Wâ€² â†˜faâ€² â†˜âŸ¦Tâ€²âŸ§ â†˜wâ€² refl
      ElâŠ†Top i real (Lâ€² {j} {k} Aâ‰ˆAâ€²) aâ‰ˆaâ€² n
        rewrite ğ•Œ-wf-gen k (Liâ‰¤â€² j k refl)
        with aâ‰ˆaâ€²
      ...  | record { ua = ua ; ub = ub ; â†˜ua = â†˜ua ; â†˜ub = â†˜ub ; uaâ‰ˆub = uaâ‰ˆub }
           with ElâŠ†Top _ (Î» _ l<k â†’ real _ (â‰¤-trans l<k (mâ‰¤n+m k j))) Aâ‰ˆAâ€² uaâ‰ˆub n
      ...     | w , â†˜w , â†˜wâ€²                  = liftt j w , Rli n â†˜ua â†˜w refl , Rli n â†˜ub â†˜wâ€² refl

      ğ•ŒâŠ†TopT : âˆ€ i
               (real : âˆ€ j â†’ j < i â†’ âˆ€ {A Aâ€²} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ j) â†’ A â‰ˆ Aâ€² âˆˆ TopT j)
               (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’ A â‰ˆ Aâ€² âˆˆ TopT i
      ğ•ŒâŠ†TopT i real (neâ€² Câ‰ˆCâ€²) n
        with Câ‰ˆCâ€² n
      ...  | V , â†˜V , â†˜Vâ€²        = ne V , Rne n â†˜V refl , Rne n â†˜Vâ€² refl
      ğ•ŒâŠ†TopT i real Nâ€² n         = N , RN n , RN n
      ğ•ŒâŠ†TopT i real (Uâ€² {j}) n   = Se j , RU n refl , RU n refl
      ğ•ŒâŠ†TopT i real (Î â€² {j} {k} iA RT) n
        rewrite ğ•Œ-wf-gen j (Î Iâ‰¤â€² j k refl)
        rewrite ğ•Œ-wf-gen k (Î Oâ‰¤â€² j k refl)
        with BotâŠ†El _ (Î» _ l<j â†’ real _ (â‰¤-trans l<j (mâ‰¤mâŠ”n j k))) iA (Bot-l n)
      ...  | zâ‰ˆzâ€²
           with RT zâ‰ˆzâ€²
      ...     | record { âŸ¦TâŸ§ = âŸ¦TâŸ§ ; âŸ¦Tâ€²âŸ§ = âŸ¦Tâ€²âŸ§ ; â†˜âŸ¦TâŸ§ = â†˜âŸ¦TâŸ§ ; â†˜âŸ¦Tâ€²âŸ§ = â†˜âŸ¦Tâ€²âŸ§ ; Tâ‰ˆTâ€² = Tâ‰ˆTâ€² }
              with ğ•ŒâŠ†TopT _ (Î» _ l<j â†’ real _ (â‰¤-trans l<j (mâ‰¤mâŠ”n j k))) iA n
                 | ğ•ŒâŠ†TopT _ (Î» _ l<k â†’ real _ (â‰¤-trans l<k (mâ‰¤nâŠ”m j k))) Tâ‰ˆTâ€² (1 + n)
      ...        | W , â†˜W , â†˜Wâ€²
                 | Wâ‚ , â†˜Wâ‚ , â†˜Wâ‚â€² = Î  (W â†™ j) (Wâ‚ â†™ k) , RÎ  n â†˜W â†˜âŸ¦TâŸ§ â†˜Wâ‚ refl , RÎ  n â†˜Wâ€² â†˜âŸ¦Tâ€²âŸ§ â†˜Wâ‚â€² refl
      ğ•ŒâŠ†TopT i real (Lâ€² {j} {k} Aâ‰ˆAâ€²) n
        rewrite ğ•Œ-wf-gen k (Liâ‰¤â€² j k refl)
        with ğ•ŒâŠ†TopT _ (Î» _ l<k â†’ real _ (â‰¤-trans l<k (mâ‰¤n+m k j))) Aâ‰ˆAâ€² n
      ...  | W , â†˜W , â†˜Wâ€²        = Liftt j (W â†™ k) , RL n â†˜W refl , RL n â†˜Wâ€² refl



ğ•ŒâŠ†TopT : âˆ€ {i} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’ A â‰ˆ Aâ€² âˆˆ TopT i
ğ•ŒâŠ†TopT {i = i} Aâ‰ˆAâ€² = <-Measure.wfRec (Î» i â†’ âˆ€ {A Aâ€²} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’ A â‰ˆ Aâ€² âˆˆ TopT i)
                                      (Î» i real â†’ Real.ğ•ŒâŠ†TopT i real)
                                      i Aâ‰ˆAâ€²

BotâŠ†El : âˆ€ {i} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’
         c â‰ˆ câ€² âˆˆ Bot â†’
         â†‘ i A c â‰ˆ â†‘ i Aâ€² câ€² âˆˆ El i Aâ‰ˆAâ€²
BotâŠ†El {i = i} = Real.BotâŠ†El i Î» _ _ â†’ ğ•ŒâŠ†TopT

ElâŠ†Top : âˆ€ {i} (Aâ‰ˆAâ€² : A â‰ˆ Aâ€² âˆˆ ğ•Œ i) â†’
         a â‰ˆ aâ€² âˆˆ El i Aâ‰ˆAâ€² â†’
         â†“ i A a â‰ˆ â†“ i Aâ€² aâ€² âˆˆ Top
ElâŠ†Top {i = i} = Real.ElâŠ†Top i (Î» _ _ â†’ ğ•ŒâŠ†TopT)
